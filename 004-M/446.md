Active Coal Lynx

Medium

# Irreversible role assignment in `Roles.sol::_setRole()`

### Summary

The [`_setRole()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/Roles.sol#L29-L33) function in the `Roles.sol` does not allow the removal of roles from addresses. This function can only add roles, not remove them, despite having a status parameter that suggests it should be able to do both. Compromised addresses of privileged roles can not be removed.

### Root Cause

Use of the bitwise OR operator (|) without any mechanism to unset bits. This results in roles being permanently set once assigned, with no way to revoke them.
```solidity
    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
        emit RoleUpdated(user, role, status);
    }
 ```

### Internal pre-conditions

1) An address should be given a privileged role with the `setRole()` function.

### External pre-conditions

1) There must to be a need to remove the privileged role from the assigned address. This can be due to the address being compromised or the address acting maliciously.

### Attack Path

1) An address is given a privileged role in the contracts.
2) This address acts maliciously or is compromised.
3) Admin when trying to remove this address' role will call the `setRole()` function with the 3rd parameter `bool status` being false.
4) Since the `_setRole()` function does not make use the `bool status` it is impossible to remove the role from this address.
5) Admin can try to override this address' role by setting another role to it, however this will also not work and it will not override previously given roles.

### Impact

It is impossible to remove an assigned role from an address. This can cause critical issues in the contracts when a privileged address is acting maliciously or when this address is compromised.

### PoC

The following contract can be used to observe the vulnerability, setting `status` to false will not change anything. Neither is it possible to override the role to remove it.
```solidity
contract RolesVulnerability {
    mapping(address => bytes32) private _addressRoles;


    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
    }

    function setRole(address user, uint8 role, bool status) external virtual {
        _setRole(user, role, status);
    }

    function _hasRole(address user, uint8 role) public view returns(bool) {
        uint256 roles = uint256(_addressRoles[user]);
        return (roles & (1 << role)) > 0;
    }

    function checkRole(address user, uint8 role) public virtual view returns(bool) {
        if (!_hasRole(user, role)) {
            return (false);
        }
        else return (true);
    }
}
```

### Mitigation

Function can be updated as shown below:
```solidity
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    if (status) {
        roles = roles | (1 << role);
    } else {
        roles = roles & ~(1 << role);
    }
    _addressRoles[user] = bytes32(roles);
    emit RoleUpdated(user, role, status);
}
```