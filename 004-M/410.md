Future Butter Horse

Medium

# Inability to Revoke User Permissions in Roles Contract

## Summary

The `Roles` contract has a function to set a role for a user - `setRole`, but lacks the functionality to remove a role from an account. This will result in the default owner of the contract being unable to remove a malicious user's permissions.

## Vulnerability Detail

In the [`Roles.setRole`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/Roles.sol#L35-L37) function, there's an input parameter `status` intended to grant or remove a user's permission. However, the internal [`_setRoles`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/Roles.sol#L29-L33) function called by the `setRole` only uses this parameter in the emitted event, rather than implementing the intended behavior of adding or removing a role.

## Impact

There is no way to revoke granted users' permissions. If someone is given an admin role and behaves maliciously, the administrator cannot remove their permissions.

## Proof Of Concept

```diff
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Roles} from "../contracts/Roles.sol";

contract POC is Test {
    RolesTestContract public roles;
    address admin = makeAddr("admin");
    address alice = makeAddr("alice");

    function setUp() public {
        vm.prank(admin);
        roles = new RolesTestContract();
    }

    function test_removeRole() public {
        emit log_named_bytes32("alice", roles.getRoles(alice));
        
        vm.prank(admin);
        roles.setRole(alice, 0, true);
        emit log_named_bytes32("alice", roles.getRoles(alice));
        
        vm.prank(admin);
        roles.setRole(alice, 0, false);
        emit log_named_bytes32("alice", roles.getRoles(alice));
    }
}

contract RolesTestContract is Roles {
    constructor() {
        _setRole(msg.sender, 0, true);
    }
}
```

```diff
Logs:
  alice: 0x0000000000000000000000000000000000000000000000000000000000000000
  alice: 0x0000000000000000000000000000000000000000000000000000000000000001
  alice: 0x0000000000000000000000000000000000000000000000000000000000000001
```

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/Roles.sol#L36

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/Roles.sol#L30-L31

## Tool used

Manual Review

## Recommendation

Implement functionality to revoke permissions. When`status`is`false`, clear the bit at the `role` position to`0`using a bitwise AND operation.

```diff
    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        
-       _addressRoles[user] = bytes32(roles | (1 << role));

+       if (status) {
+           _addressRoles[user] = bytes32(roles | (1 << role));
+       } else {
+           _addressRoles[user] = bytes32(roles & ~(1 << role));
+       }
        
        emit RoleUpdated(user, role, status);
    }
```