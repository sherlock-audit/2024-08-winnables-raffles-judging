Deep Crimson Cuckoo

Medium

# Incorrect Bit Manipulation Leads to Immutable Roles

## Summary

The `_setRole` function in the Roles contract always sets the role bit regardless of the intended action (granting or revoking), making all roles, including the admin role (Role 0), effectively immutable once set.

## Vulnerability Detail

Included below is the relevant code from the [Roles](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/Roles.sol#L29-L33) followed by key insights:

```solidity
    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
        emit RoleUpdated(user, role, status);
    }
```

The contest README states:

> Role 0 is an admin role and it allows its members to grant or deny roles to other users.

However despite the README's acknowledgement that admins should be able to "deny" the current implementation leaves them unable to do so. It's also worth noting that if an admin did call `_setRole` with status false they would actually grant the role they explicitly intended to "deny". 

The issues is that the function uses a bitwise OR operation (`|`) to set the role bit. This operation will always set the bit to 1, regardless of whether `status` is true or false.


Bitwise Operations:

- `1 << role` creates a bitmask with only the bit at position `role` set to 1.
- `roles | (1 << role)` will always set this bit to 1 in the roles variable.


## Proof of Concept
```solidity
contract RolesTest is Test {
    WinnablesTicket public winnablesTicket;
    address public user1 = address(2);

    function setUp() public {
        winnablesTicket = new WinnablesTicket();
    }

    function testIncorrectRoleSetting() public {
        // Grant role 1 to user1
        winnablesTicket.setRole(user1, 1, true);
        
        // Attempt to revoke role 1 from user1
        winnablesTicket.setRole(user1, 1, false);
        
        // Check if user1 still has role 1
        assertTrue(winnablesTicket.getRoles(user1) & bytes32(uint256(1 << 1)) != bytes32(0), "Role 1 should not be revoked");
    }
}
```

This test demonstrates that roles cannot be revoked once granted, highlighting the incorrect implementation of the role setting logic.

## Impact

The current implementation creates a false sense of security and control over roles through the status parameter which is effectively ignored. The reality is that once a role is granted, it can never be revoked. This applies to all roles, including the admin role (Role 0). Additionally  the function emits a RoleUpdated event with the status parameter. This can be misleading, as it suggests the role was updated according to the status, when in reality, it was always set.




## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/Roles.sol#L29-L33

## Tool used

Manual Review

## Recommendation

Modify the `_setRole` function to correctly handle both granting and revoking of roles:

```solidity
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    if (status) {
        roles = roles | (1 << role);
    } else {
        roles = roles & ~(1 << role);
    }
    _addressRoles[user] = bytes32(roles);
    emit RoleUpdated(user, role, status);
}
```

This implementation uses the `status` parameter to determine whether to set or clear the role bit, allowing for both granting and revoking of roles.