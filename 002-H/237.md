Custom Rosewood Bear

High

# Missing updates to `_lockedETH` state variable in the `WinnablesTicketManager.refundPlayer` function lead to loss of funds

### Summary

The `WinnablesTicketManager.refundPlayer` does not update `_lockedETH` state variable after processing a refund, resulting in the `_lockedETH` value to be more than the actual value. As a result, the difference amount will be indefinitely locked in the `WinnablesTicketManager` contract.

### Description

The `_lockedETH` represents part of the `WinnablesTicketManager` contract balance that is inaccessible to the protocol admin. The `ETH` used to buy the tickets for an ongoing raffle is considered locked till the winner is declared. 

A snippet of [`WinnablesTicketManager.buyTickets`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L206) that locks the received ETH

```solidity
    function buyTickets(
    [...]
    )
        unchecked {
             [...]
            _lockedETH += msg.value;
        }
```

If the sale for the raffle succeeds then the winner is declared and the contract makes the ETH raised from the sale available to the protocol team by updating the `_lockedETH` value.

  A snippet of [`WinnablesTicketManager.propagatePrizeWinner`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L343) unlocking the locked ETH

```solidity
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
       // [...]
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
```

The protocol admin can then withdraw the ETH from ticket sales using the [`WinnablesTicketManager.withdrawETH`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L300-L306) function:

```solidity
    function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
            balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }
```

If the raffle is cancelled then the buyers can get refund by calling the [`WinnablesTicketManager.refundPlayers`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215-L227) function:

```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
   }
```

The `refundPlayers` function is transferring locked ETH from the ticket sale. However, the function is not updating the `_lockedETH` state variable. As a result, the contract considers more ETH to be locked than the actual with the difference equal to the refunded amount.

The ETH unlocked from other winning raffles gets locked because of this difference.

Exploit scenario:

Assume `_lockedETH = 15 ETH` and `address(WinnablesTicketManager).balance = 15 ETH` at time T0

1. T0. Admin creates three raffles A, B and C. A and B end before C.
2. T1. Users buy tickets worth of `11 ETH` for raffle `A`, tickets worth of `10 ETH` for raffle `B`.
     - `_lockedETH = 15 + 11 + 10 ETH = 36 ETH`, `balance = 36 ETH`
3. T2. Raffle `A` and `B` end without meeting the `minThresholdTickets` limit and the raffles are cancelled.
4. T3. Users withdraw locked ETH using the `refundPlayers` function for raffle `A` and `B`
    - `_lockedETH = 36 ETH`, `balance = 15 ETH`
5. T4. Users buy tickets worth of `20 ETH` for raffle `C`
     - `_lockedETH = 36 + 20 ETH`, `balance = 35 ETH`.
6. T5. Raffle `C` ends and meets the threshold requirements. Winner is propagated and the ETH from ticket sales becomes unlocked
   - `_lockedETH = 36 ETH`, `balance = 35 ETH`.
7. The protocol admin cannot withdraw the ETH from ticket sales of raffle `C`. 

The `_lockedETH` should be `15 ETH`. however it is `36 ETH` == `15 ETH`(correct `_lockedETH`) + `11 + 10 ETH` (refunded amount from cancelled raffles).

### Root Cause

The  [`WinnablesTicketManager.refundPlayers`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215-L227) function does not subtract refunded amount from the `_lockedETH` state variable.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The amount equal to the refunded amount for cancelled raffles of the ticket sales becomes locked in the `WinnablesTicketManager` contract.

### PoC

_No response_

### Mitigation

Subtract the refunded amount from the `_lockedETH` value in the `refundPlayers` function