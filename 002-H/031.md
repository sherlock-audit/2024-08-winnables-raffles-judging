Dandy Blonde Carp

High

# `refundPlayers()` doesn't update the `lockedETH`

## Summary
`refundPlayers()` doesn't update the `lockedETH`

## Vulnerability Detail
When a user buy tickets using `buyTickets()`, it `increases` the lockedETH(which is ETH amount that might be needed for a refund)
```solidity
   function buyTickets(
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        bytes calldata signature
    ) external payable {
...
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
  @>        _lockedETH += msg.value;
        }
...
    }
```
Also admin can withdraw all the ETH, except lockedETH using withdrawETH()
```solidity
   function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
@>          balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }
```
Now the problem is, when user claim their refund using `refundPlayers()` for `CANCELED` raffle then it transfers the ETH to participants but doesn't `decrease` the amount of lockedETH
```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length;) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
  @>        _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked {
                ++i;
            }
        }
    }
```

## Impact
Admin will not be able to withdraw the full ETH balance because lockedETH is not updated

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L206
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L224

## Tool used
Manual Review

## Recommendation
Reduce the amount of lockedETH in refundPlayers()
```diff

    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length;) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
+           _lockedETH -= amountToSend;
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked {
                ++i;
            }
        }
    }
```