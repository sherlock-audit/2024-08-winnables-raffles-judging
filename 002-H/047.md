Itchy Mahogany Turtle

High

# Refund Actions Will Inadvertently Lock Contract Owners' ETH

### Summary

The variable '_lockedETH,' which is intended to track the quantity of ETH reserved for potential refunds, is only incremented in the 'buyTickets' method but not decremented appropriately in the 'refundPlayers' method after a raffle is canceled and refunds are issued.

This mismanagement of state leads to a perpetual increase in the reported ETH locked within the contract with every cancellation and refund process, regardless of the actual ETH being returned to participants. Essentially, while the ETH is correctly returned to raffle participants, the contract fails to recognize these disbursements, falsely inflating the balance of locked funds.

As a result, over time, an increasing amount of ETH—exactly equal to the total ever refunded—is incorrectly considered as locked. 

### Root Cause

The root cause of the issue within the `WinnablesTicketManager` smart contract stems from an oversight in the state management of the `_lockedETH` variable. This variable is designed to track the amount of ETH that is currently reserved for potential refunds, ensuring that the contract has sufficient liquidity to fulfill refund obligations when necessary.

When a participant purchases tickets via the `buyTickets` method, the amount of ETH sent with the transaction is added to the `_lockedETH` variable. This process correctly reflects that a portion of the contract’s ETH balance is now potentially refundable if the associated raffle is canceled:

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L206
```solidity
_lockedETH += msg.value;

```

However, when a raffle is canceled and refunds are issued through the `refundPlayers` method, the contract correctly computes and distributes the appropriate refund amounts to participants but fails to update the `_lockedETH` variable to reflect this disbursement. Here is the part of the method where refunds are issued to each player:

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L218C9-L227C10

```solidity
for (uint256 i = 0; i < players.length; i++) {
    ...
    uint256 amountToSend = (participation & type(uint128).max);
    _sendETH(amountToSend, player);
    ...
}

```

Although `amountToSend` is successfully transferred to each player, there is no corresponding decrease in `_lockedETH`. This results in `_lockedETH` maintaining a record of funds as still being locked even after they have been refunded.

### Internal pre-conditions

The `WinnablesTicketManager.sol` needs to have more then 1 total raffle. 

### External pre-conditions

A raffle fails to sell enough tickets in time and should be canceled.

### Attack Path

Just the normal action of canceling a raffle.  

### Impact

Since `_lockedETH` is never reduced following successful refunds, the contract believes more ETH is locked than actually is. These funds are then not available for withdrawal by the contract owner or for allocation to future raffles, even though in reality, these funds should be freely available for use.

### PoC

For testing I changed a few small parts of the WinnablesTicketManger.sol file.

[first ](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L42)
```solidity
uint256 public _lockedETH; // @audit I changed this to public for use in testing 
```

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L190C9-L190C74

```solidity
//_checkPurchaseSig(raffleId, ticketCount, blockNumber, signature);
        // @audit No signature check during testing. 

```
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L282C9-L286C11

```solidity
// _sendCCIPMessage(  @audit I shut down the ccip call for testing 
        //     prizeManager,
        //     chainSelector,
        //     abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        // );

```


Foundry test file 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/WinnablesTicketManager.sol";
import "../contracts/WinnablesTicket.sol";
import "../contracts/interfaces/IWinnables.sol"; 


contract MockIERC20 is IERC20 {
    function totalSupply() external view returns (uint256) {}
    function balanceOf(address account) external view returns (uint256) {}
    function transfer(address recipient, uint256 amount) external returns (bool) {}
    function allowance(address owner, address spender) external view returns (uint256) {}
    function approve(address spender, uint256 amount) external returns (bool) {}
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {}
}


contract MockVRFConsumerBaseV2 is VRFConsumerBaseV2 {
    constructor(address coordinator) VRFConsumerBaseV2(coordinator) {}

    // Implementing the abstract function with an empty body for testing
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {}
}

contract WinnablesTicketManagerTest is Test, IWinnables {
    WinnablesTicketManager winnablesTicketManager;
    WinnablesTicket winnablesticket;
    address mockLinkToken;
    address mockVrfCoordinator;
    uint64 subscriptionId = 1;
    bytes32 keyHash = "0xabcdef";
    address mockTicketsContract;
    address mockCCIPRouter;

    struct Any2EVMMessage {
        bytes32 messageId; // MessageId corresponding to ccipSend on source
        uint64 sourceChainSelector; // Source chain selector
        bytes sender; // abi.decode(sender) if coming from an EVM chain
        bytes data; // payload sent in original message
        EVMTokenAmount[] tokenAmounts; // Tokens and their amounts at destination
    }

    struct RaffleDetails {
        uint64 startsAt;
        uint64 endsAt;
        uint32 minTicketsThreshold;
        uint32 maxTicketSupply;
        uint32 maxHoldings;
        uint256 totalRaised;
        RaffleStatus status;
        uint256 chainlinkRequestId;
    }
    
    struct EVMTokenAmount {
        address token; // token address on local blockchain
        uint256 amount;
    }

    function setUp() public {
        mockLinkToken = address(new MockIERC20());
        mockVrfCoordinator = address(new MockVRFConsumerBaseV2(address(this)));
        winnablesticket = new WinnablesTicket();
        mockCCIPRouter = address(this);
        
        winnablesTicketManager = new WinnablesTicketManager(
            mockLinkToken, 
            mockVrfCoordinator, 
            subscriptionId,
            keyHash, 
            address(winnablesticket), 
            mockCCIPRouter
        );

        winnablesticket.setRole(address(winnablesTicketManager), 1, true);
    }

    function getAndLogRaffleDetails(uint256 raffleId) internal returns (uint64, uint64, uint32, uint32, uint32, uint256, RaffleStatus, uint256) {
        (uint64 startsOut, uint64 endsOut, uint32 minOut, uint32 maxOut, uint32 maxHoldOut, uint256 totalRaisedOut, RaffleStatus statusOut, uint256 chainlinkRequestIdOut) 
            = winnablesTicketManager.getRaffle(raffleId);

        console.log("");
        console.log("Tickets sold :", winnablesticket.supplyOf(raffleId));    
        console.log("");
        console.log("Starts At: %s, Ends At: %s", startsOut, endsOut);
        console.log("Min Tickets: %s, Max Tickets: %s, Max Holdings: %s", minOut, maxOut, maxHoldOut);
        console.log("Total Raised: %s, Status: %s, Chainlink Request ID: %s", totalRaisedOut, uint(statusOut), chainlinkRequestIdOut);
        console.log("");

        return (startsOut, endsOut, minOut, maxOut, maxHoldOut, totalRaisedOut, statusOut, chainlinkRequestIdOut);

    }

    function logBalanceVsLockedEth() internal {
        uint256 contractBalance = address(winnablesTicketManager).balance;
        uint256 lockedEth = winnablesTicketManager._lockedETH();

        console.log("Contract Eth Balance : ", contractBalance);
        console.log("Contract _lockedEth : ", lockedEth);
    }


    function testEthBecomesLockedAfterRefund() public {
    uint256 raffleId = 1;
    uint64 startsAt = 1;
    uint64 endsAt = startsAt + 1 days;
    uint32 minTickets = 10;
    uint32 maxTickets = 100;
    uint32 maxHoldings = 5;

    // Simulate the prize being locked via a CCIP message
    address senderAddress = address(this); // Simulate sender address
    uint64 chainSelector = 1; // Simulate a chain selector, typically the identifier for the chain
    address _senderAddress = senderAddress;    
    bytes memory ccipData = abi.encode(raffleId);

    // Encode and construct the Any2EVMMessage
    Client.EVMTokenAmount[] memory emptyTokenAmounts = new Client.EVMTokenAmount[](0);
    Client.Any2EVMMessage memory message = Client.Any2EVMMessage({
        messageId: keccak256(abi.encodePacked(block.timestamp)),
        sourceChainSelector: chainSelector,
        sender: abi.encode(_senderAddress),
        data: ccipData,
        destTokenAmounts: emptyTokenAmounts
    });

    winnablesTicketManager.setCCIPCounterpart(_senderAddress, chainSelector, true);

    vm.warp(800);

    vm.prank(_senderAddress); // Mock the caller to be the sender or router

    winnablesTicketManager.ccipReceive(message);
    
    winnablesTicketManager.createRaffle(raffleId, startsAt, endsAt, minTickets, maxTickets, maxHoldings);

    //uint256 timestamp = block.timestamp;
    console.log("current timestamp:  ", block.timestamp);

    // Check initial raffle state
    getAndLogRaffleDetails(raffleId);

    // Warp time to after the raffle start time
    vm.warp(startsAt + 1 hours);

    // Assume a pseudo-blockchain state change
    vm.roll(block.number + 1);

    // Define users
    address[] memory users = new address[](3);
    users[0] = address(0x123);
    users[1] = address(0x456);
    users[2] = address(0x789);

    // Setup balances for the users (assuming the contract is payable and can receive ETH)
    for (uint i = 0; i < users.length; ++i) {
        vm.deal(users[i], 1 ether); // provide each user with 1 ETH
    }

    // Each user buys tickets
    for (uint i = 0; i < users.length; ++i) {
        vm.prank(users[i]); // execute the following transaction as user[i]
        winnablesTicketManager.buyTickets{value: 0.1 ether}(raffleId, 1, block.number + 1000, ""); // Assuming 0.1 ETH per ticket, no need for a signature for now
    }

    // Log final states after participation
    // for (uint i = 0; i < users.length; ++i) {
    //     getAndLogParticipationDetails(raffleId, users[i]);
    // }

    getAndLogRaffleDetails(raffleId);

    logBalanceVsLockedEth();

    vm.warp(startsAt + 3 days);
    winnablesTicketManager.cancelRaffle(address(10), 1, 1);

    
    console.log("");
    console.log("Raffle canceled ");

    getAndLogRaffleDetails(raffleId);

    logBalanceVsLockedEth();

    // Create a dynamic array with participant addresses
    address[] memory participants = new address[](3);
    participants[0] = address(0x123);
    participants[1] = address(0x456);
    participants[2] = address(0x789);

    // Call the refundPlayers function with the raffle ID and the address array
    winnablesTicketManager.refundPlayers(1, participants);

    console.log("players refunded");

    logBalanceVsLockedEth();
}


}

```


Log

```solidity
[PASS] testEthBecomesLockedAfterRefund() (gas: 551014)
Logs:
  current timestamp:   800

  Tickets sold : 0

  Starts At: 1, Ends At: 86401
  Min Tickets: 10, Max Tickets: 100, Max Holdings: 5
  Total Raised: 0, Status: 2, Chainlink Request ID: 0


  Tickets sold : 3

  Starts At: 1, Ends At: 86401
  Min Tickets: 10, Max Tickets: 100, Max Holdings: 5
  Total Raised: 300000000000000000, Status: 2, Chainlink Request ID: 0

  Contract Eth Balance :  300000000000000000
  Contract _lockedEth :  300000000000000000

  Raffle canceled

  Tickets sold : 3

  Starts At: 1, Ends At: 86401
  Min Tickets: 10, Max Tickets: 100, Max Holdings: 5
  Total Raised: 300000000000000000, Status: 7, Chainlink Request ID: 0

  Contract Eth Balance :  300000000000000000
  Contract _lockedEth :  300000000000000000
  players refunded
  Contract Eth Balance :  0
  Contract _lockedEth :  300000000000000000

```


### Mitigation

The most direct way to resolve the issue is to adjust the refundPlayers method to decrement _lockedETH appropriately when refunds are processed. This involves adding a line of code to subtract the refunded amount from _lockedETH within the refund loop:

```solidity
for (uint256 i = 0; i < players.length; i++) {
    ...
    uint256 amountToSend = (participation & type(uint128).max);
    _sendETH(amountToSend, player);
    _lockedETH -= amountToSend;  // Decrement the locked ETH count
    ...
}

```