Rich Marigold Swan

High

# [High] Refunding users from a cancelled raffle doesn't decrease the _lockedETH in WinnablesTicketManager.sol

## Summary

Users can enter a raffle and if it reaches the necessary requirements for drawing, a winner is picked and the ETH spent for buying tickets is released [`_lockedETH -= raffle.totalRaised`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L343)  so the admin - one who has onlyRole(0) - can withdraw the ETH used to pay for entry with[`withdrawETH`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L300) function. That function only lets the admin withdraw the balance - _lockedETH:

```solidity
balance = address(this).balance - _lockedETH;
```

If a raffle doesn't reach the necessary requirements it's cancelled and the users can be refunded their ETH for buying the tickets. But that ETH isn't decreased from `_lockedETH`

## Vulnerability Details

If a raffle doesn't reach the necessary requirements it's cancelled and the users can be refunded their ETH for buying the tickets in [`refundPlayers` function](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215). But that ETH isn't decreased from `_lockedETH` which leads to mismatch between the funds that are locked and can't be withdrawn by the admin and the funds that actually are free to be withdrawn, which can lead to funds being locked forver and admin unable to withdraw the ETH from tickets sales. 

## Impact

High

## Code Snippet
```solidity

function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }

function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
            balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }

function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }

```

## Tool used

Manual Review

## Recommendation
When refunding players, decrease _lockedETH with the amount that was refunded: 

`_lockedETH -= amountToSend`


```solidity

function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _lockedETH -= amountToSend;
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }

```
