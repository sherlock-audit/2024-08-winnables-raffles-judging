Funny Pickle Grasshopper

High

# `WinnablesTicketManager::refundPlayers` function not decrementing the `_lockedETH` will result in reduced balance admin can withdraw for token sales.

## Summary
The `_lockedETH` variable in `WinnablesTicketManager` tracks the fund that are sent by users for raffle participation and initially the funds they send increments the `_lockedETH` which ensures that admin can only withdraw the funds collected for raffles after they are propagated to ethereum chain.
But for the case when a raffle is cancelled, the users are allowed to take all the funds they sent for raffle participation, and as funds are refunded which means that the `_lockedETH` amount should decrease, but `refundPlayers` lacks the implementation to reduce `_lockedETH`, as a result of which effective withdrawal balance for admins, i.e., (total ticket manager balance - _lockedETH) would always be less than what it should be. 
For cancelled raffle, suppose if all funds are refunded, still `_lockedETH` holds that amount which will prevent admin to withdraw the funds from other successful raffles as the net withdrawable balance is impacted by extra funds not decremented in `_lockedETH` and thus leads to stuck funds. 

## Vulnerability Detail
- The vulnerability is present in the `refundPlayers` function where it sends the participation funds back to the participants but doesn't decrement the corresponding locked funds depicted by `_lockedETH`.
- The `_lockedETH` variable allows the admin to withdraw the funds from ticket sales only from raffles that are propagated, but it results in bad accounting when it is not decremented when refunds are carried out for cancelled raffle.
- As the balance of ticket manager decreases (by refund), but the `_lockedETH` still holds that amount, as a result of which the overall withdrawable balance for admins also decreases.
- Thus admins can't withdraw the actual amount as it is decreased by the extra amount in `_lockedETH`, which was not decreased during refunds.

## Impact
Funds will be stuck in `WinnablesTicketManager` and the admins will never be able to withdraw them and thus gets less or no incentives from raffles.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215

## PoC
Below is the coded PoC in foundry, that depicts the vulnerability.

Pre-requisites: Integrating foundry in hardhat.
1. Install hardhat-foundry:
```bash
yarn add --dev @nomicfoundation/hardhat-foundry
``` 
2. Import in hardhat config:
```js
require("@nomicfoundation/hardhat-foundry");
```
3. Complete the setup:
```bash
npx hardhat init-foundry
```


Run the test:
```bash
forge test --mt test_refundPlayers_NotDecrementing_EthLocked_LeadsToBadAccounting
```

```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.24;

import { Test } from "forge-std/Test.sol";
import "contracts/WinnablesPrizeManager.sol";
import "contracts/WinnablesTicketManager.sol";
import "contracts/WinnablesTicket.sol";
import "@chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol";
import "@chainlink/contracts/src/v0.8/mocks/VRFCoordinatorV2Mock.sol";
import "contracts/mock/MockCCIPRouter.sol";
import { ERC677 } from "@chainlink/contracts-ccip/src/v0.8/shared/token/ERC677/ERC677.sol";

contract WinnablesTest is Test {
    address linkToken;
    address ccipRouter;
    VRFCoordinatorV2Mock vrfCoordinator;
    uint64 subId;
    bytes32 keyHash;
    WinnablesPrizeManager prizeManager;
    WinnablesTicket ticket;
    WinnablesTicketManager ticketManager;
    address alice;
    address bob;
    address admin;
    address api;
    uint256 apiPvtKey;
    uint256 constant BALANCE = 100e18;


    function setUp() external {
        alice = makeAddr("alice");
        bob = makeAddr("bob");
        admin = makeAddr("admin");
        (api, apiPvtKey) = makeAddrAndKey("api");

        linkToken = address(new ERC677("LINK", "LINK"));
        ccipRouter = address(new CCIPRouter(linkToken));
        vrfCoordinator = new VRFCoordinatorV2Mock(0.25e18, 100000);
        keyHash = 0x474e34a077df58807dbe9c96d3c009b23b3c6d0cce433e59bbf5b34f823bc56c;

        deal(linkToken, admin, BALANCE);
        vm.deal(alice, BALANCE);
        vm.deal(bob, BALANCE);

        vm.startPrank(admin);

        uint256 fundAmt = 10e18;
        subId = vrfCoordinator.createSubscription();
        vrfCoordinator.fundSubscription(subId, uint96(fundAmt));

        prizeManager = new WinnablesPrizeManager(linkToken, ccipRouter);
        ticket = new WinnablesTicket();
        ticketManager = new WinnablesTicketManager(
            linkToken,
            address(vrfCoordinator),
            subId,
            keyHash,
            address(ticket),
            ccipRouter
        );

        ticketManager.setRole(api, 1, true);
        ticket.setRole(address(ticketManager), 1, true);
        vrfCoordinator.addConsumer(subId, address(ticketManager));

        uint256 ticketManagerLinkBal = 10e18;
        IERC20(linkToken).transfer(address(ticketManager), ticketManagerLinkBal);

        vm.stopPrank();
    }

    function test_refundPlayers_NotDecrementing_EthLocked_LeadsToBadAccounting() public {
        // considering the ticket and ticketManager are on local chain
        // also assuming owner locks the prize on prize manager on any `x` chain
        // transferring cross-chain message from `x` chain to sepolia by pranking as router

        uint64 chainSelectorForAnyXChain = 1;
        vm.prank(admin);
        ticketManager.setCCIPCounterpart(address(prizeManager), chainSelectorForAnyXChain, true);

        uint256 raffleId_1 = 1;
        uint32 totalEntries = 2;
        _prepareReceivingMessageAndCreateRaffle(raffleId_1, chainSelectorForAnyXChain, totalEntries);

        // alice buys a ticket
        uint16 ticketCt_1 = 1;
        uint256 perTicketCharge = 0.01 ether;
        _createSignatureAndPurchaseTicket(raffleId_1, alice, ticketCt_1, perTicketCharge);

        // now consider whole duration of raffle finsishes and only 1 player joined, but required 2
        vm.warp(block.timestamp + 2 days);

        // calls for cancellation of raffle
        ticketManager.cancelRaffle(address(prizeManager), chainSelectorForAnyXChain, raffleId_1);

        // alice calls for refund
        address[] memory players = new address[](1);
        players[0] = alice;
        ticketManager.refundPlayers(raffleId_1, players);
        // here after calling refund players, the locked eth is not decremented
        assert(address(ticketManager).balance == 0);

        // -----------------------------------------------------------
        // Finished with Raffle 1, start raffle 2
        // -----------------------------------------------------------
        uint256 raffleId_2 = 2;
        _prepareReceivingMessageAndCreateRaffle(raffleId_2, chainSelectorForAnyXChain, totalEntries);

        // alice buys 2 ticket
        uint16 ticketCt_2 = 2;
        uint256 newPerTicketCharge = 0.02 ether;
        _createSignatureAndPurchaseTicket(raffleId_2, alice, ticketCt_2, newPerTicketCharge);


        // now drawing winner
        ticketManager.drawWinner(raffleId_2);

        // vrf coordinator calls the fulfillRandomWords
        vrfCoordinator.fulfillRandomWords({ _requestId: 1, _consumer: address(ticketManager) });
        
        ticketManager.propagateRaffleWinner(address(prizeManager), chainSelectorForAnyXChain, raffleId_2);

        // now admin tries to withdraw the eth from the tickets sales
        // as for raffle 2 amount deposited = 2 * newPerTicketCharge = 2 * 0.02 = 0.04 eth

        assert(address(ticketManager).balance == 0.04 ether);
        uint256 adminInitBalance = admin.balance;

        vm.prank(admin);
        ticketManager.withdrawETH();

        uint256 adminFinalBalance = admin.balance;

        // they should have received exactly 0.04 eth, but the lockedETH not decremented during refund
        // reduces the overall balance admin can take for the ticket sales
        assert(adminFinalBalance - adminInitBalance < 0.04 ether);
        assert(adminFinalBalance - adminInitBalance > 0);
    }

    function _prepareReceivingMessageAndCreateRaffle(uint256 raffleId, uint64 chainSelectorForAnyXChain, uint32 minMaxEntry) internal {
        Client.Any2EVMMessage memory receivingMessage = Client.Any2EVMMessage({
            messageId: bytes32(uint256(1)),
            sourceChainSelector: chainSelectorForAnyXChain,
            sender: abi.encode(address(prizeManager)),
            data: abi.encodePacked(raffleId),
            destTokenAmounts: new Client.EVMTokenAmount[](0)
        });

        vm.prank(ccipRouter);
        ticketManager.ccipReceive(receivingMessage);

        uint64 start = uint64(block.timestamp);
        uint64 end = uint64(block.timestamp + 1 days);
        vm.prank(admin);
        ticketManager.createRaffle(raffleId, start, end, minMaxEntry, minMaxEntry, minMaxEntry);
    }

    function _createSignatureAndPurchaseTicket(uint256 raffleId, address account, uint16 ticketCt, uint256 perTicketCharge) internal {
        uint256 userNonce = ticketManager.getNonce(account);
        bytes32 message = keccak256(
                abi.encodePacked(
                    account, userNonce, raffleId, ticketCt, uint256(block.number), uint256(ticketCt) * perTicketCharge
                )
            );
        bytes32 hash = keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", message));
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(apiPvtKey, hash);
        bytes memory signature = abi.encodePacked(r, s, v);

        vm.prank(account);
        ticketManager.buyTickets{value: uint256(ticketCt) * perTicketCharge}(raffleId, ticketCt, block.number, signature);
    }
}
```

## Tool used
Manual Review, Unit test in foundry

## Recommendation
When carrying out refunds, decrement the `_lockedETH`.
```diff
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
+           _lockedETH -= amountToSend;
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```