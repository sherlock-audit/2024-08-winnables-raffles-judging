Active Coal Lynx

High

# `_lockedETH` does not decrease when a raffle is cancelled and users are refunded. This breaks the `withdrawETH()` function

### Summary

When a raffle is cancelled and user's are refunded, `lockedETH` amount does not decrease. Any calls on `withdrawETH()` will revert as the `_lockedETH` value will always be greater than the contract's balance. If the unchecked math underflows to an amount that is less than the contracts balance this will result in the `withdrawETH()` function transferring incorrect amounts.

### Root Cause

In [`WinnablesTicketManager.sol:215`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L215-L228) the `refundPlayers()` function refunds users when a raffle is cancelled. However this function does not reduce the `_lockedETH` amount by the amount that is refunded to users.
```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```

### Internal pre-conditions

1. Admin needs to start a raffle
2. Users needs to buy tickets in this raffle
3. Admin needs to cancel this raffle with valid reasons
4. Anyone should call the `refundPlayers()` function

### External pre-conditions

_No response_

### Attack Path

When a raffle is created and users buy tickets for this raffle, `_lockedETH` value increases by the `msg.value` in the [`buyTickets()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L182-L210) function shown below.
```solidity
    function buyTickets(
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        bytes calldata signature
    ) external payable {
       //rest of the function
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
            _lockedETH += msg.value;
        }
       //rest of the function
    }
```
When this raffle is cancelled and users are refunded with the [`refundPlayers()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L215-L228) function shown below:
```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```
We can observe that the `_lockedETH` amount is not decreased by the refunded amount.
Taking a look at the [`withdrawETH()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L300-L306) function:
```solidity
    function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
            balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }
```
We can observe that the amount that will be sent to the admin is `Contract's balance - _lockedETH`. As the contract does not have a `fallback()` function, only way for this contract to receive funds is buying tickets meaning that the `_lockedETH` amount will always be greater than the contract's balance. This will cause the calculation to return a wrong value due to unchecked math's underflow and calls to send ETH will fail due to the contract not having enough balance. In an edge case where underflowing calculation of `Contract's balance - _lockedETH` is less than the contract balance, this function will instead send the funds that should be locked to the admin.

### Impact

`withdrawETH()` function will always revert or in an edge case transfer incorrect amounts which can include amounts that should be locked during a raffle. `_lockedETH` variable's value will be always be incorrect.

### PoC

1. Implement a getter function for `_lockedETH` to see it in logs.
2. Change the following test as shown in `TicketManager.js` file and run it to observe the vulnerability.
```jsx
    it('Should be able to refund tickets purchased', async () => {
      const contractBalanceBefore = await ethers.provider.getBalance(manager.address);
      const userBalanceBefore = await ethers.provider.getBalance(buyer2.address);
      const tx = await manager.refundPlayers(1, [buyer2.address]);
      const { events } = await tx.wait();
      expect(events).to.have.lengthOf(1);
      const [ event ] = events;
      expect(event.event).to.equal('PlayerRefund');
      const contractBalanceAfter = await ethers.provider.getBalance(manager.address);
      const userBalanceAfter = await ethers.provider.getBalance(buyer2.address);
      expect(contractBalanceAfter).to.eq(contractBalanceBefore.sub(100));
      expect(userBalanceAfter).to.eq(userBalanceBefore.add(100));
      const { withdrawn } = await manager.getParticipation(1, buyer2.address);
      expect(withdrawn).to.eq(true);
      console.log("locked ETH:", await manager.getLockedETH());
      console.log("contract balance:", await ethers.provider.getBalance(manager.address));
      await expect(manager.withdrawETH()).to.be.revertedWithCustomError(manager, 'ETHTransferFail');
    });
```
Test can be adjusted by implementing a `fallback()` function, sending various amounts of ETH in the test to the contract to see that either `amount - _lockedETH` will be transferred in `withdrawETH()` or call will fail if sent ETH is less than the `_lockedETH`.

### Mitigation

Correctly deduct the refunded amounts from the `_lockedETH`.