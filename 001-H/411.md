Future Butter Horse

High

# Anyone can prevent the winner of the raffle from claiming their prize.

## Summary

When the raffle status is `FULFILLED`, any user can call `propagateRaffleWinner` with arbitrary `address` and `chainSelector` parameters. This redirects the `ccipSend` to an unintended destination instead of the legitimate `WinnablesPrizeManager` (on Ethereum), effectively blocking the raffle winner from claiming their prize.

## Vulnerability Detail

When the raffle ends and the VRF [`fulfillRandomWords`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L350-L361), the [status is set](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L358) to `FULFILLED`.

Then, someone should call [`propagateRaffleWinner`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345) to broadcast the winner from `WinnablesTicketManager` (on Avalanche) to `WinnablesPrizeManager` (on Ethereum). The [`propagateRaffleWinner`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345) function accepts three parameters:`prizeManager`address,`chainSelector`, and`raffleId`. Notably, there's no validation performed on these inputs.

```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
    raffle.status = RaffleStatus.PROPAGATED;
    address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

    _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    unchecked {
        _lockedETH -= raffle.totalRaised;
    }
}
```

This vulnerability allows any user to call the function with arbitrary `prizeManager` address and `chainSelector` parameters. They can specify a contract that supports `ccipReceive`, redirecting the message to an unintended destination.

The [`BaseCCIPSender._sendCCIPMessage`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPSender.sol#L15-L50) function executes the call to the untrusted contract without performing any additional validation.

```solidity
function _sendCCIPMessage(
    address ccipDestAddress,
    uint64 ccipDestChainSelector,
    bytes memory data
) internal returns(bytes32 messageId) {
    if (ccipDestAddress == address(0) || ccipDestChainSelector == uint64(0)) {
        revert MissingCCIPParams();
    }

    // Send CCIP message to the desitnation contract
    IRouterClient router = IRouterClient(CCIP_ROUTER);
    LinkTokenInterface linkToken = LinkTokenInterface(LINK_TOKEN);

    Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
        receiver: abi.encode(ccipDestAddress),
        data: data,
        tokenAmounts: new Client.EVMTokenAmount[](0),
        extraArgs: "",
        feeToken: LINK_TOKEN
    });

    uint256 fee = router.getFee(
        ccipDestChainSelector,
        message
    );
    uint256 currentLinkBalance = linkToken.balanceOf(address(this));

    if (fee > currentLinkBalance) {
        revert InsufficientLinkBalance(currentLinkBalance, fee);
    }

    messageId = router.ccipSend(
        ccipDestChainSelector,
        message
    );
}
```

However, the raffle status [will be set to](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L337) `PROPAGATED`, preventing subsequent calls to `propagateRaffleWinner` due to [the status check](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L336). 

As a result, the winner cannot claim their prize because the [`WinnablesPrizeManager._ccipReceive`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L278) function is never invoked, leaving the raffle winner [unset](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L275-L276).

## Impact

Any raffle winner can be permanently blocked from claiming their reward. The prize will remain indefinitely locked in the `WinnablesPrizeManager`, with neither the winner nor the admin able to withdraw it. 

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345

[](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345)https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPSender.sol#L46-L49

## Tool used

Manual Review

## Recommendation

The issue can be mitigated in two ways:

- If there are no plans to support other chains, remove the `chainSelector` and `prizeManager` parameters.
- Allow only the winner to call the `propagateRaffleWinner` function.

```diff
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external onlyWinner {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

+ 			if (winner != msg.sender) {
+ 				revert Unauthorized();
+ 			}

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```