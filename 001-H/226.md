Lone Peanut Swallow

High

# Raffle prizes can be locked indefinitely, requiring an admin action to free them

## Summary

Raffles prizes can be permanently locked within the [WinnablesPrizeManager.sol](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol) contract by exploiting permissionless state-advancing functions that accept the CCIP message destination and  chain selector as input parameters.

## Vulnerability Detail

Given that:
- [`BaseCCIPSender._sendCCIPMessage`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPSender.sol#L15) implements minimal validation on the destination address and chain selector.
- [`WinnablesTicketManager.propagateRaffleWinner`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345) and [`WinnablesTicketManager.cancelRaffle`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278-L288) can be called by an unauthenticated actor and that they accept the destination address and chain selector as input parameters, accepting them with no validation.

When a raffle complies with the conditions for either of the methods to be successfully called, a malicious actor can rush to call the adequate method and provide an incorrect address or chain selector for the `prizeManager` and `chainSelector` parameters.
Because neither of these methods implement a check leveraging the `_ccipContracts` mapping like the [`WinnablesTicketManager._ccipReceive`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L368-L370) method does, which effectively implements a whitelist for CCIP interactions, the methods can be tricked into sending a CCIP message to an incorrect destination.
As per the normal happy path of both methods, the raffle's state will be advanced to either the `PROPAGATED` or the `CANCELED` state.

Thanks to a correctly implemented state-guard mechanism, the same methods will not be callable on either of the states the raffle has transitioned to.

As a consequence of the above, the `WinnablesPrizeManager.sol` instance on Ethereum mainnet will never receive the CCIP message to signal that a winner was drawn, or that a raffle was cancelled. In both situations, given that the contract still believes the raffle to be ongoing, it will provide no way to retrieve the raffle prize.
Given that the CCIP message cannot be replayed by the `WinnablesTicketManager.sol` instance on Avalanche, the prize is effectively stuck.

The only remediation possible for such issue would require a system admin to deploy a new contract to replay the dropped CCIP message and add it as a new trusted counterpart to `WinnablesPrizeManager.sol`. In this case, the admin would be required to monitor all of the instances in which this vector is exploited and trusted to provide the correct message and data within the replayed message.

## Impact

High.
The core functionality of the protocol is compromised, leaving raffle prizes stuck and requiring admin manual intervention to free them.

## Code Snippet

```solidity
function _sendCCIPMessage(address ccipDestAddress, uint64 ccipDestChainSelector, bytes memory data)
	internal
	returns (bytes32 messageId)
{
	if (ccipDestAddress == address(0) || ccipDestChainSelector == uint64(0)) {
		revert MissingCCIPParams();
	}

	// Send CCIP message to the desitnation contract
	IRouterClient router = IRouterClient(CCIP_ROUTER);
	LinkTokenInterface linkToken = LinkTokenInterface(LINK_TOKEN);

	Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
		receiver: abi.encode(ccipDestAddress),
		data: data,
		tokenAmounts: new Client.EVMTokenAmount[](0),
		extraArgs: "",
		feeToken: LINK_TOKEN
	});

	uint256 fee = router.getFee(ccipDestChainSelector, message);
	uint256 currentLinkBalance = linkToken.balanceOf(address(this));

	if (fee > currentLinkBalance) {
		revert InsufficientLinkBalance(currentLinkBalance, fee);
	}

	messageId = router.ccipSend(ccipDestChainSelector, message);
}
```

```solidity
function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
	_checkShouldCancel(raffleId);

	_raffles[raffleId].status = RaffleStatus.CANCELED;
	_sendCCIPMessage(
		prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
	);
	IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
}
```

```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
	Raffle storage raffle = _raffles[raffleId];
	if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
	raffle.status = RaffleStatus.PROPAGATED;
	address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

	_sendCCIPMessage(
		prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner)
	);
	IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
	unchecked {
		_lockedETH -= raffle.totalRaised;
	}
}
```

## Tool used

Manual Review

## Recommendation

The `WinnablesTicketManager.propagateRaffleWinner` and `WinnablesTicketManager.cancelRaffle` methods should either:
1. Read the destination address and chain selector from storage, which should be set by an authenticated party.
2. Validate that the combination of the destination address and chain selector make up a valid CCIP counterpart.

In order to ensure the CCIP message is sent to the correct destination.