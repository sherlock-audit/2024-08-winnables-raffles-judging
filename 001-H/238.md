Sleepy Umber Shrimp

High

# Permanently blocking fair winners from claiming their prizes and getting propagated; and fair users -- from cancelling their raffles -- by maliciously constructing a fake prizeManager contract

## Summary
Fair users may get DoS'ed (maliciously blocked) from cancelling their raffles and propagating the winner of a raffle to the Ethereum-based `WinnablesPrizeManager` contract.

The problem is due to a missing sanitization of the `prizeManager` argument in the Avalance-based `WinnablesTicketManager` contract's functions, namely in `propagateRaffleWinner` and `cancelRaffle`.

## Vulnerability Detail
While the `WinnablesTicketManager`'s `_ccipReceive` function is inherited and implemented to sanitize the the `counterpart` appropriately:
```solidity
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
```
, the `_ccipSend` function can arbitrary transmit messages to any user-specified `prizeManager`, as long as the `prizeManager` is not `address(0)`.

And while the **real** integrated `WinnablesPrizeManager` will not be notified about the `RAFFLE_CANCELLED` or `WINNER_DRAWN` action and the intent, **the respective effects, such as decreasing the `_lockedETH` global state variable and setting the raffle's <code>raffles[raffleId].status</code> to `RaffleStatus.CANCELED`**.

## Impact
1. If targeted maliciously, this leads to a permanent DoS of the ability to cancel a raffle properly and notify the Ethereum-based `WinnablesPrizeManager` about that happening (because the **raffle**'s status will be switched to <code>CANCELED</code>, and the <code>_checkShouldCancel</code> pre-condition check will always fail to double-cancel a raffle because it requires the <code>raffle.status</code> to be either <code>RaffleStatus.PRIZE_LOCKED</code> or <code>RaffleStatus.IDLE</code>);
2. There's a 100% attacker-controlled way and ability to block the fair winners from getting propagated to the Ethereum-based <code>WinnablesPrizeManager</code> contract side, too. And this will, of course, lead to them not being able to claim their fair prices, etc.

## Code Snippet
```solidity
    /// @notice (Public) Cancel a raffle if it can be canceled
    /// @param raffleId ID of the raffle to cancel
    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );

        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```

```solidity
    /// @notice (Public) Send a cross-chain message to the Prize Manager to
    ///         mark the prize as claimable by the winner
    /// @param raffleId ID of the Raffle we wish to draw a winner for
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));



        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```

## PoC
<ol>
<li><h4>An attacker creates a fake "<code>WinnablesPrizeManager</code>" contract on any of the Chainlink Router's supported chains (as far as I can see now, it doesn't even have to be deployed on Ethereum specifically);</h4>
<ul>
<li><strong>Please note that this fake contract only has to implement the basic CCIP Receiver interface, and can be used multi-purposely for any <code>raffleId</code>'s actions manipulation. Be it blocking the winner's message propagation to the real <code>WinnablesPrizeManager</code> contract or permanently blocking a raffle from getting cancelled.</strong></li>
</ul>
</li>
<li><h4>The attacker has his fake contract ready to be impersonating the <code>prizeManager</code> address.</h4>
<li><h4>Now he either picks the right moment for the attack, or front-runs the victim's transaction -- it can happen in both cases when the time and state is applicable, as in particularly:</h4>

<ul>
<li>
<strong>
For blocking cancelling:</strong> <em>all of the conditions have to be right for the <code>_checkShouldCancel</code> to evaluate without reverting;</em></li>

<li><strong>
For blocking propagating the rightful winner and further blocking the winner from claiming their rightful prizes:</strong> <em>the raffle's status should be <code>FULFILLED</code></em></li>
</ul>
</li>
<li>
<h4>The attacker calls the <strong>PERMISSIONLESS (!)</strong> functions as usual, with the exception of passing the malicious <code>prizeManager</code> contract address in the arguments, for his malicious contract to be notified instead of the real <code>WinnablesPrizeManager</code> getting notified.</h4>
<ul>
<li>
As you can see, the <code>propagateRaffleWinner</code> function can be called permissionlessly by anyone when the conditions apply, as well as it is evident that the <code>prizeManager</code> contract is not sanitized:

```solidity
    /// @notice (Public) Send a cross-chain message to the Prize Manager to
    ///         mark the prize as claimable by the winner
    /// @param raffleId ID of the Raffle we wish to draw a winner for
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));



        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```
</li>
<li>
And in the <code>cancelRaffle</code> function, there're no <code>msg.sender</code> nor <code>prizeManager</code> checks either there:

```solidity
    /// @notice (Public) Cancel a raffle if it can be canceled
    /// @param raffleId ID of the raffle to cancel
    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );

        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
</li>
</ul>
</li>
</ol>

# To elaborate on why it causes a permanent DoS and why the issue is indeed severe:
<ol>
<li>
If the <code>cancelRaffle</code> function is front-run with the malicious and attacker-controlled <code>prizeManager</code> address, the <code>raffles[raffleId]</code> status will be set to <code>CANCELED</code>, and the pre-condition checks in the <code>_checkShouldCancel</code> sequence will always revert if a <strong>FAIR USER</strong> attempts to re-cancel with a valid <code>prizeManager</code> contract and <code>chainSelector</code>:


```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```

<em>Nor a fair user, nor ANYONE else will be able to cancel properly, and the victim raffle will be just blocked from further any actions on its behalf.</em>
</li>
<li>
If the <code>propagateRaffleWinner</code> function is targeted maliciously, <strong>then any further propagation attempts will fail due to a revert here in the status assertion check:</strong>:

```solidity
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
```

in:

```solidity
    /// @notice (Public) Send a cross-chain message to the Prize Manager to
    ///         mark the prize as claimable by the winner
    /// @param raffleId ID of the Raffle we wish to draw a winner for
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));



        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```
</li>
<li>
<strong>Also, the <strong>_lockedETH</strong> value will be inflated as well!</strong>
</li>
</ol>

As you can verify, the `_ccipSend` function doesn't check the validity of the `ccipDestAddress`:

```solidity
    function _sendCCIPMessage(
        address ccipDestAddress,
        uint64 ccipDestChainSelector,
        bytes memory data
    ) internal returns(bytes32 messageId) {
        if (ccipDestAddress == address(0) || ccipDestChainSelector == uint64(0)) {
            revert MissingCCIPParams();
        }

        // Send CCIP message to the desitnation contract
        IRouterClient router = IRouterClient(CCIP_ROUTER);
        LinkTokenInterface linkToken = LinkTokenInterface(LINK_TOKEN);

        Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
            receiver: abi.encode(ccipDestAddress),
            data: data,
            tokenAmounts: new Client.EVMTokenAmount[](0),
            extraArgs: "",
            feeToken: LINK_TOKEN
        });

        uint256 fee = router.getFee(
            ccipDestChainSelector,
            message
        );
        uint256 currentLinkBalance = linkToken.balanceOf(address(this));

        if (fee > currentLinkBalance) {
            revert InsufficientLinkBalance(currentLinkBalance, fee);
        }

        messageId = router.ccipSend(
            ccipDestChainSelector,
            message
        );
    }
```

---

Normally, the following code on the `WinnablesPrizeManager` behalf would be executed if the proper `prizeManager` and `chainSelector` were passed to these functions:

```solidity
    /// @notice Callback called by CCIP Router. Receives CCIP message and handles it
    /// @param message CCIP Message
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
        uint256 raffleId;
        address winner;
        if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
            raffleId = _decodeRaffleCanceledMessage(message.data);
            
            _cancelRaffle(raffleId);
            return;
        }

        (raffleId, winner) = _decodeWinnerDrawnMessage(message.data);
        _rafflePrize[raffleId].winner = winner;
        emit WinnerPropagated(raffleId, winner);
    }
```

## Tool used
Manual Review.

## Recommendation
<ol>
<li><strong>Implement proper <code>prizeManager</code> validation, for example, by introducing a whitelist for that purpose -- apparently, the best approach;</strong>

The best way to modify the <code>_ccipSend</code> function would be adding the following (potentially lacking, not omitted by design?) check:

```solidity
        if (!_ccipContracts[ccipDestinationAddress]) revert UnauthorizedCCIPSender();
```

Like the following example, my suggestion:
```solidity
    function _sendCCIPMessage(
        address ccipDestAddress,
        uint64 ccipDestChainSelector,
        bytes memory data
    ) internal returns(bytes32 messageId) {
        if (ccipDestAddress == address(0) || ccipDestChainSelector == uint64(0)) {
            revert MissingCCIPParams();
        }

        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        // Send CCIP message to the desitnation contract
        IRouterClient router = IRouterClient(CCIP_ROUTER);
        LinkTokenInterface linkToken = LinkTokenInterface(LINK_TOKEN);

        Client.EVM2AnyMessage memory message = Client.EVM2AnyMessage({
            receiver: abi.encode(ccipDestAddress),
            data: data,
            tokenAmounts: new Client.EVMTokenAmount[](0),
            extraArgs: "",
            feeToken: LINK_TOKEN
        });

        uint256 fee = router.getFee(
            ccipDestChainSelector,
            message
        );
        uint256 currentLinkBalance = linkToken.balanceOf(address(this));

        if (fee > currentLinkBalance) {
            revert InsufficientLinkBalance(currentLinkBalance, fee);
        }

        messageId = router.ccipSend(
            ccipDestChainSelector,
            message
        );
    }
```

</li>
<li>
Or, at the worst case, consider adding access control to the `propagateRaffleWinner` and `cancelRaffle` functions -- which, in my opinion, is not a good approach though, as it compromises the decentralization of the functions and the Winnables Raffles protocol as a whole.
</li>
</ol>

*Possibly the `ccipDestChainSelector` should be checked, too.*


## Code references
1. https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L334-L337
2. https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L278-L281
3. https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPSender.sol#L15-L49