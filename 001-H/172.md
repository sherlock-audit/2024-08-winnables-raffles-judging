Silly Burgundy Scallop

High

# PrizeManager tokens are lost when wrong chainSelector is given

## Summary

Everyone can call `WinnablesTicketManager::propagateRaffleWinner` and knowing that any user can provide `chainSelector` which doesn’t correspond on Ethereum, where `WinnablesPrizeManager` is deployed can make the reward tokens forever locked.

## Vulnerability Detail

When the winner is drawn in `WinnablesTicketManager::drawWinner` and the VRF request is received in `WinnablesTicketManager::fulfillRandomWords`, the state of the raffle is set to `FULFILLED`, then **everyone** can propagate the winner to the `WinnablesPrizeManager` where the reward will be unlocked and winner can claim it:

```solidity
/// @notice (Public) Send a cross-chain message to the Prize Manager to
///         mark the prize as claimable by the winner
/// @param raffleId ID of the Raffle we wish to draw a winner for
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
    raffle.status = RaffleStatus.PROPAGATED;
    address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

    _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    unchecked {
        _lockedETH -= raffle.totalRaised;
    }
}
```

The problem is that `chainSelector` is not validated if it corresponds to the Ethereum's one, which is 5009297550715157269 [here](https://docs.chain.link/ccip/supported-networks/v1_2_0/mainnet#ethereum-mainnet), and any malicious user, for example, someone who hasn’t won the prize can pass a different chain’s selector that will cause the message to be sent to another chain instead and this message to never be delivered to the `PrizeManager`. 

Attacker should only give any other **valid `chainSelector`,** for example Arbitrum one (4949039107694359620), since it also has onRamp configured for Avalanche → Arbitrum.

Note that attack won’t work if random numbers are given, they should correspond to valid chain selector ≠ mainnet.

As a result the state of the raffle in `TicketManager` will be `PROPAGATED`, which means it cannot be canceled, so no refund is possible in `PrizeManager` either, since no message is delivered and `WinnablesTicketManager::ccipReceive` is never executed with this `raffleId` - there will be no winner, making the locked prize forever lost:

```solidity
function _ccipReceive(
    Client.Any2EVMMessage memory message
) internal override {
    (address _senderAddress) = abi.decode(message.sender, (address));//NOTE this is of type bytes, not address
    bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
    if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
    (uint256 raffleId) = abi.decode(message.data, (uint256));
    if (_raffles[raffleId].status != RaffleStatus.NONE) {
        // The raffle cannot be created, send back a cancel message to unlock the prize
        _sendCCIPMessage(
            _senderAddress,
            message.sourceChainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        return;
    }
    _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

    emit RafflePrizeLocked(
        message.messageId,
        message.sourceChainSelector,
        raffleId
    );
}
```

The attack can be done both intentionally when someone wants the rewards to be lost, or unintentionally by providing the wrong chain selector due to the excitement of winning the reward. All this is possible due to missing input verification.

Another occurrence where tokens can be permanently locked is `WinnablesTicketManager::cancelRaffle`, this function is also permissionless and `chainSelector` is not validated, so malicious user can frontrun the honest users call it with wrong selector that will lead to message not being delivered to the Ethereum so the tokens to be unlocked:

```solidity
function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
      _checkShouldCancel(raffleId);

      _raffles[raffleId].status = RaffleStatus.CANCELED;
      _sendCCIPMessage(
          prizeManager,
          chainSelector,
          abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
      );
      IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
  }
```

## Impact

Rewards will be lost forever due to maliciously passing the wrong `chainSelector`.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L275

## Tool used

Manual Review

## Recommendation

Expose setter function to set the chain selector for Ethereum, also add input validation in `propagateRaffleWinner` and `cancelRaffle`