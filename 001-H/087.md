Dandy Blonde Carp

High

# Malicious user can pass different `prizeManager` address to cancelRaffle(), successfully trapping ETH/NFT/token in prizeManager.sol

## Summary
Malicious user can pass different `prizeManager` & `chainSelector` to cancelRaffle() as it doesn't verify them, successfully trapping ETH/NFT/token in prizeManager.sol

## Vulnerability Detail
A user can cancel the raffle using cancelRaffle(). For canceling, it takes `prizeManager`, `chainSelector`, `raffleId` & sends CCIP message using _sendCCIPMessage() to the prizeManager.sol
```solidity
    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
@>      _sendCCIPMessage(
            prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
In the prizeManager.sol, when `CCIPMessageType = RAFFLE_CANCELED` then it unlocks the NFT/ETH/token(which were locked initially to create raffle)
```solidity
 function _ccipReceive(Client.Any2EVMMessage memory message) internal override {
...
        CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
        uint256 raffleId;
        address winner;
@>      if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
            raffleId = _decodeRaffleCanceledMessage(message.data);
 @>         _cancelRaffle(raffleId);
         return;
        }
...
    }
```
```solidity
 function _cancelRaffle(uint256 raffleId) internal {
        RaffleType raffleType = _rafflePrize[raffleId].raffleType;
        if (_rafflePrize[raffleId].status == RafflePrizeStatus.CANCELED) revert InvalidRaffle();
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage nftInfo = _nftRaffles[raffleId];
@>          _nftLocked[nftInfo.contractAddress][nftInfo.tokenId] = false;
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage tokenInfo = _tokenRaffles[raffleId];
            unchecked {
@>              _tokensLocked[tokenInfo.tokenAddress] -= tokenInfo.amount;
            }
        } else if (raffleType == RaffleType.ETH) {
            unchecked {
@>              _ethLocked -= _ethRaffles[raffleId];
            }
        } else {
            revert InvalidRaffle();
        }
        _rafflePrize[raffleId].status = RafflePrizeStatus.CANCELED;
        emit PrizeUnlocked(raffleId);
    }
```
Now the problem is, cancelRaffle() doesn't verify the `prizeManagerAddress` & `chainSelector` passed by user. 

A malicious user can deploy a dummy contract with `CCIPReceiver` and use it in cancelRaffle() to cancel the raffle, which will set the status of raffle to `CANCELED` & also it will not make any call to real/original prizeManager.sol to unlock the ETH/NFT/Token locked to create raffle.

## Impact
ETH/NFT/Token locked in prizeManager.sol will be locked forever as cancelRaffle() doesn't make call to original/real prizeManager.sol

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278C1-L288C6
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L270C8-L274C10
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L280C4-L295C1

## Tool used
Manual Review

## Recommendation
Verify the prizeManger & chainSelector before sending CCIP message
```diff
  function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);
+       bytes32 counterpart = _packCCIPContract(prizeManager, chainSelector);
+       if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```