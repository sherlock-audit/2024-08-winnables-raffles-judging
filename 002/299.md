Savory Chiffon Gazelle

Medium

# Misleading Parameter in `Role Management Function` Causes Inability to Remove Roles

### Summary

The `Roles.sol::_setRole` [function](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29-L33) is intended to manage `user roles` by either `adding` or `removing` a role based on the `status` parameter. However, due to a flaw in the bitwise operation logic, the `status` parameter is now ineffective, `causing roles to only be added but never removed`. This results in users being unable to have roles revoked, which could lead to unauthorized access and security risks.

### Root Cause

In `Roles.sol::_setRole` [function](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29-L33), the status parameter was initially meant to control whether a role is added or removed:
```javascript
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    _addressRoles[user] = bytes32(roles | (1 << role));
    emit RoleUpdated(user, role, status);
}
```
The current implementation only adds the role by setting the bit corresponding to the `role` to `1`, regardless of the `status` parameter's value. The intended functionality for removing the role when status is false was not implemented correctly.

### Internal pre-conditions

1. The function `_setRole(user, role, true)` is called to `add a role`.
2. The function `_setRole(user, role, false)`  is called with the intention to `remove a role`.

### External pre-conditions

1. The contract depends on correctly `setting` and `removing` `roles` for security and proper access control.

### Attack Path

1. **Role Addition:** When `_setRole(user, role, true)` is called:
    - The role is correctly added to the user's roles by setting the corresponding bit to `1`.
2. **Role Removal Attempt:** When `_setRole(user, role, false)` is called:
    - The intended removal of the role does not occur because the function lacks logic to clear the bit corresponding to the role.
    - As a result, the role remains active, and the user retains access to functions or resources associated with that role.

### Impact

- **Security Risk:** Users may retain roles that were intended to be removed, leading to unauthorized access to restricted functions or resources.
- **Operational Risk:** This bug could prevent proper role management, leading to a situation where revoked roles are still active, potentially compromising the contract’s security model.
- **Loss of Trust:** If users can retain roles indefinitely, even when their access should be revoked, this could lead to a loss of trust in the system's access control mechanism.

### PoC

Here’s a simplified example showing how the bug manifests:
```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";

contract TestRoles is Test {
    event RoleUpdated(address indexed user, uint256 indexed role, bool indexed status);
    address public user1 = address(0x1);
    address public owner = address(0x2);
    
    mapping(address => bytes32) private _addressRoles;

    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
        emit RoleUpdated(user, role, status);
    }

    function setUp() public {}

    function testCheckRolesSetAndRevoke() public {
        uint8 role = 1;
        assertEq(uint256(_addressRoles[user1]) & (1 << role) , 0, "Role should not be set");
        _setRole(user1, role, true); //set a role
        assertNotEq(uint256(_addressRoles[user1])& (1 << role), 0, "Role should be set");
        _setRole(user1, role, false); //unset a role
        assertEq(uint256(_addressRoles[user1]) & (1 << role), 0, "Role should not be set as it is removed"); //This should fail
    }
}

```

Run it by: `forge test --mt testCheckRolesSetAndRevoke -vvvvv`

Output:
```javascript
[FAIL. Reason: Role should not be set as it is removed: 2 != 0] testCheckRolesSetAndRevoke() (gas: 34821)
Traces:
  [144] TestRoles::setUp()
    └─ ← [Stop] 

  [34821] TestRoles::testCheckRolesSetAndRevoke()
    ├─ [0] VM::assertEq(0, 0, "Role should not be set") [staticcall]
    │   └─ ← [Return] 
    ├─ emit RoleUpdated(user: 0x0000000000000000000000000000000000000001, role: 1, status: true)
    ├─ [0] VM::assertNotEq(2, 0, "Role should be set") [staticcall]
    │   └─ ← [Return] 
    ├─ emit RoleUpdated(user: 0x0000000000000000000000000000000000000001, role: 1, status: false)
    ├─ [0] VM::assertEq(2, 0, "Role should not be set as it is removed") [staticcall]
    │   └─ ← [Revert] Role should not be set as it is removed: 2 != 0
    └─ ← [Revert] Role should not be set as it is removed: 2 != 0

Suite result: FAILED. 0 passed; 1 failed; 0 skipped;
```

### Mitigation

To fix the issue, the function should properly handle the `status` parameter by adding or removing the role based on its value. The corrected function is:

```diff
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
+  if (status) {
        _addressRoles[user] = bytes32(roles | (1 << role)); // Add role
+   } else {
+      _addressRoles[user] = bytes32(roles & ~(1 << role)); // Remove role
+   }
    emit RoleUpdated(user, role, status);
}

```

Doing the test with the updated code:
```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";

contract TestRoles is Test {
    event RoleUpdated(address indexed user, uint256 indexed role, bool indexed status);
    address public user1 = address(0x1);
    address public owner = address(0x2);
    
    mapping(address => bytes32) private _addressRoles;

    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        if (status) {
            _addressRoles[user] = bytes32(roles | (1 << role)); // Add role
        } else {
            _addressRoles[user] = bytes32(roles & ~(1 << role)); // Remove role
        }
        emit RoleUpdated(user, role, status);
    }


    function setUp() public {}

    function testCheckRolesSetAndRevoke() public {
        uint8 role = 1;
        assertEq(uint256(_addressRoles[user1]) & (1 << role) , 0, "Role should not be set");
        _setRole(user1, role, true);
        assertNotEq(uint256(_addressRoles[user1])& (1 << role), 0, "Role should be set");
        _setRole(user1, role, false);
        assertEq(uint256(_addressRoles[user1]) & (1 << role), 0, "Role should not be set as it is removed");
    }
}
```
Run it by: `forge test --mt testCheckRolesSetAndRevoke -vvvvv`

Output:
```javascript
[PASS] testCheckRolesSetAndRevoke() (gas: 23850)
Traces:
  [144] TestRoles::setUp()
    └─ ← [Stop] 

  [28063] TestRoles::testCheckRolesSetAndRevoke()
    ├─ [0] VM::assertEq(0, 0, "Role should not be set") [staticcall]
    │   └─ ← [Return] 
    ├─ emit RoleUpdated(user: 0x0000000000000000000000000000000000000001, role: 1, status: true)
    ├─ [0] VM::assertNotEq(2, 0, "Role should be set") [staticcall]
    │   └─ ← [Return] 
    ├─ emit RoleUpdated(user: 0x0000000000000000000000000000000000000001, role: 1, status: false)
    ├─ [0] VM::assertEq(0, 0, "Role should not be set as it is removed") [staticcall]
    │   └─ ← [Return] 
    └─ ← [Stop] 

Suite result: ok. 1 passed; 0 failed; 0 skipped;
```

This change ensures that when `status` is `true`, the role is added, and when `status` is `false`, the role is removed, restoring the intended functionality of role management in the contract.