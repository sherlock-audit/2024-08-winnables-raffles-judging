Lone Peanut Swallow

Medium

# Admin can't remove roles from any account once the role has been granted

## Summary

The protocol admin isn't able to revoke any role granted.

## Vulnerability Detail

Because [`Roles._setRole`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29-L33) uses the bit-wise inclusive OR, a bit in the `_addressRoles` bitmap can never be reset to 0.
This limits the admin's ability to respond to emergency situations in which removing a role from a compromised trusted party is necessary. 
Most importantly, the inability to react to such a situation would require the protocol team to push its users to migrate to a new deployment of the protocol, given that the current version cannot be rescued.

As a minor side note, the method completely ignores the `status` boolean parameter it is passed, using it only for an event emission at [Roles.sol#L32](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L32).
Combining these two facts one can see how an admin, when calling `Roles.setRole(_, _, false)` will be led to believe that the role was removed when in fact it wasn't.

## Impact

Medium.
Admin isn't able to revoke granted roles, breaking the access control system implemented.

## Code Snippet

```solidity
function _setRole(address user, uint8 role, bool status) internal virtual {
	uint256 roles = uint256(_addressRoles[user]);
	_addressRoles[user] = bytes32(roles | (1 << role)); // <@ once set, this will never lower the bit
	emit RoleUpdated(user, role, status);
}

function setRole(address user, uint8 role, bool status) external virtual onlyRole(0) {
	_setRole(user, role, status);
}
```

## Tool used

Manual Review

## Recommendation

`Roles._setRole` should base its computation based on the value of the `status` parameter:
1. If `status == true`, the function can use the current implementation.
2. If `status == false`, the function should employ a bit mask to lower the `role` bit within `_addressRoles[user]`, e.g.:

```diff
@@ -28,7 +29,14 @@ contract Roles {
 
     function _setRole(address user, uint8 role, bool status) internal virtual {
         uint256 roles = uint256(_addressRoles[user]);
-        _addressRoles[user] = bytes32(roles | (1 << role));
+
+        if (status) {
+            _addressRoles[user] = bytes32(roles | (1 << role));
+        } else {
+            uint256 mask = uint256(~(1 << role));
+            _addressRoles[user] = bytes32(roles & mask);
+        }
+
         emit RoleUpdated(user, role, status);
     }
```
