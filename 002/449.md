Spare Parchment Millipede

High

# `Roles::setRole()` can not be used to reset/remove any role

## Summary

The `Roles::setRole()` function in the contract is intended to manage role assignments by allowing roles to be set or removed for specific addresses. 

However, the implementation only allows roles to be set, not removed or reset, due to a flaw in the underlying logic. This flaw compromises the flexibility and security of the role management system.


## Vulnerability Detail

The `setRole` function allows assigning a role to an address by invoking the internal `_setRole` function. The issue lies in the `_setRole` function, where the `status` parameter is only used for emitting events and does not influence the actual role assignment. The role assignment is managed by a bitwise OR operation `bytes32(roles | (1 << role))`, which can only add a role but cannot remove or reset it.

### Example:

Letâ€™s assume `role = 1` corresponds to the `minter` role:

1. **Initial Assignment**:
   - The role is added using `setRole(user, 1, true);`.
   - The bitwise OR operation sets the 2nd bit (corresponding to role 1).
   - _addressRoles[user] becomes 0x02 (binary: 0000 0010).

2. **Attempt to Remove Role**:
   - The role is attempted to be removed using `setRole(user, 1, false);`.
   - The bitwise OR operation still sets the 2nd bit, regardless of the status being false.
   - _addressRoles[user] remains 0x02, so the minter role is not removed.

The same case happens for `role=0` i.e the `admin` which allows its members to grant or deny roles to other users.

This means that once a role is assigned to an address, it cannot be removed or reset, leading to potential security risks and an inability to revoke privileges.

## Impact

- **Inability to Revoke Roles**: 
  - Roles such as `minter` or `owner` cannot be removed once assigned, leading to an inability to revoke privileges even when necessary.

- **Security Risks**:
  - If a privileged account (e.g., a `minter` or `owner`) is compromised, the inability to revoke the role leaves the contract vulnerable to unauthorized actions, potentially leading to loss of control over critical functions.

- **Ownership Transfer Complications**:
  - The constructor assigns role 0 (admin rights) to the initial contract deployer (`msg.sender`). If ownership is transferred to a new address, the old owner retains their admin role because the role cannot be revoked, even by the new owner. This undermines the purpose of ownership transfer as the old owner retains full administrative control.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29-L37

```solidity
function setRole(address user, uint8 role, bool status) external virtual onlyRole(0) {
    _setRole(user, role, status);
}

function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    _addressRoles[user] = bytes32(roles | (1 << role));
    emit RoleUpdated(user, role, status);
}
```

## Coded POC
Add the following test:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";
import "../contracts/WinnablesTicket.sol";

contract WinnablesRolesTests is Test {
    WinnablesTicket winnablesTicket;
    address owner = makeAddr("owner");
    address minter = makeAddr("minter");
    function setUp() public {
        vm.startPrank(owner);
        winnablesTicket = new WinnablesTicket();
        assertEq(winnablesTicket.owner(), owner);
        vm.stopPrank();
    }

    function testWinnablesRolesNotWorking() public{
        console.log("setting minter role to address(minter)");
        vm.startPrank(owner);
        winnablesTicket.setRole(minter, 1, true);
        vm.stopPrank();
        console.log("minting 10 ID 1 to user 2");
        vm.startPrank(minter);
        address user2 = makeAddr("user2");
        winnablesTicket.mint(user2, 1, 10);
        console.log("balance of user2 for ID 1", winnablesTicket.balanceOf(user2, 1));
        vm.stopPrank();

        vm.startPrank(owner);
        console.log("owner trying to remove minter");
        console.log("setting role status as false");
        winnablesTicket.setRole(minter, 1, false);
        console.log("assigning arbitrary role `9` to minter except for 0 and 1");
        winnablesTicket.setRole(minter, 9, true);
        vm.stopPrank();
        
        // testing whether the minter is still able to mint
        vm.startPrank(minter);
        console.log("testing if minter can still mint");
        winnablesTicket.mint(user2, 1, 100);
        console.log("balance of user2 for ID 1", winnablesTicket.balanceOf(user2, 1));
        vm.stopPrank();

        address newOwner = makeAddr("newOwner");
        vm.startPrank(owner);
        console.log("transferring ownership to newOwner and setting role 0");
        winnablesTicket.transferOwnership(newOwner);
        assertEq(winnablesTicket.owner(), newOwner);
        console.log("setting role 0 for newOwner");
        winnablesTicket.setRole(newOwner, 0, true);
        vm.stopPrank();

        vm.startPrank(newOwner);
        console.log("newOwner tries to remove old owner using various ways");
        console.log("1. setting status as false");
        console.log("2. setting arbitrary role 9 old owner");
        winnablesTicket.setRole(owner, 0, false);
        winnablesTicket.setRole(owner, 9, true);
        vm.stopPrank();

        vm.startPrank(owner);
        console.log("checking if old owner can still set minter");
        address randomAddr = makeAddr("randomAddr");
        winnablesTicket.setRole(randomAddr, 1, true);
        vm.stopPrank();

        vm.startPrank(randomAddr);
        console.log("checking if minter set by old owner can mint or not");
        winnablesTicket.mint(user2, 1, 200);
        console.log("balance of user2 for ID 1", winnablesTicket.balanceOf(user2, 1));
        vm.stopPrank();
    }
}
```
Run the test by the following command:
```bash
forge test --mt testWinnablesRolesNotWorking -vvv
```
#### Console LOG:
```bash
[PASS] testWinnablesRolesNotWorking() (gas: 279500)
Logs:
  setting minter role to address(minter)
  minting 10 ID 1 to user 2
  balance of user2 for ID 1 10
  owner trying to remove minter
  setting role status as false
  assigning arbitrary role `9` to minter except for 0 and 1
  testing if minter can still mint
  balance of user2 for ID 1 110
  transferring ownership to newOwner and setting role 0
  setting role 0 for newOwner
  newOwner tries to remove old owner using various ways
  1. setting status as false
  2. setting arbitrary role 9 old owner
  checking if old owner can still set minter
  checking if minter set by old owner can mint or not
  balance of user2 for ID 1 310
```
## Tool Used

- **Manual Review**
- **Foundry** for testing and validation.

## Recommendation

To resolve this issue, the `_setRole` function should be updated to take the `status` parameter into account when assigning or removing roles. This can be achieved by modifying the bitwise operation to handle both adding and removing roles based on the `status` parameter.

### Suggested Code Change:

```solidity
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    if (status) {
        _addressRoles[user] = bytes32(roles | (1 << role)); // Set role
    } else {
        _addressRoles[user] = bytes32(roles & ~(1 << role)); // Remove role
    }
    emit RoleUpdated(user, role, status);
}
```

This change will ensure that roles can be both set and removed, providing the necessary flexibility and security in role management.