Cheesy Chambray Trout

High

# Admin is Not Able to Remove Role

## Summary
The _setRole(address user, uint8 role, bool status) function does not allow the removal of a role from a user.

## Vulnerability Detail
In the _setRole(address user, uint8 role, bool status) function, the status parameter is intended to add or remove a role from a user. However, this parameter has no effect in the method, which results in the inability to remove a role from an admin user.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29

## Impact
Once the admin role (0) or role 1 is assigned to a user, it becomes impossible to remove it because the status parameter is not considered in the _setRole(address user, uint8 role, bool status) function. Additionally, no error is returned, leading the admin to believe that the role has been successfully removed from the user when, in fact, it has not.

## Code Snippet
The project should be configured to use foundry forge to run the forge build and forge test commands

Create the base test class BaseTest.sol

```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "../src/WinnablesPrizeManager.sol";
import {Test} from "forge-std/Test.sol";
import {WinnablesTicket} from "../src/WinnablesTicket.sol";
import {WinnablesTicketManager} from "../src/WinnablesTicketManager.sol";
//import {MockLink} from "../src/mock/MockLink.sol";
import {VRFCoordinatorV2BetterMock} from "../src/mock/VRFCoordinatorV2BetterMock.sol";
import {DemoNFT} from "../src/mock/DemoNFT.sol";
import {DemoToken} from "../src/mock/MockToken.sol";
import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import {IMockLink} from "../src/interfaces/IMockLink.sol";

contract BaseTest is Test {
    //initialize variables of LinkToken and CCIPRouter with address on Sepolia and Avax testnet
    address linkTokenETH = 0x779877A7B0D9E8603169DdbD7836e478b4624789;
    address linkTokenAVX = 0x0b9d5D9136855f6FEc3c0993feE6E9CE8a297846;

    address ccipRouterETH = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;
    address ccipRouterAVX = 0xF694E193200268f9a4868e4Aa017A0118C9a8177;

    // Ethereum chain selector
    uint64 chainSlectorEth = 16015286601757825753;
    // Avalanche chain selector
    uint64 chainSlectorAvax = 14767482510784806043;

    //Arbritrum chain selector
    uint64 chainSlectorArbitrum = 3478487238524512106;

    //Chainlink VRF con fig
    address vrfCoordinator;
    uint64 subscriptionId;
    bytes32 keyHash;

    // 3 contracts of the protocol
    WinnablesTicket tickets;
    WinnablesTicketManager ticketManager;
    WinnablesPrizeManager prizeManager;

    // User of the application
    address admin = address(0xabc0);
    address user1 = address(0xabc1);
    address user2 = address(0xabc2);
    address user3 = address(0xabc3);
    address user4 = address(0xabc4);
    address user5 = address(0xabc5);

    // NFT and Token contracts
    DemoNFT nft1;
    DemoToken token1;
    DemoNFT nft2;
    DemoToken token2;
    DemoNFT nft3;
    DemoToken token3;

    // LINK and CCIPRouter contracts
    IMockLink linkTokenETHContract;
    IMockLink linkTokenAVXContract;

    IRouterClient ccipRouterETHContract;
    IRouterClient ccipRouterAVXContract;

    VRFCoordinatorV2BetterMock vrfCoordinatorContract;

    function setUp() public virtual {
        //Instantiate and deploy the contracts LinkToken and CCIPRouter
        linkTokenETH = deployCode("MockLink.sol", "");
        linkTokenETHContract = IMockLink(linkTokenETH);
        linkTokenAVX = deployCode("MockLink.sol", "");
        linkTokenAVXContract = IMockLink(linkTokenAVX);

        ccipRouterETH = deployCode("MockCCIPRouter.sol", abi.encode(linkTokenETH));
        ccipRouterETHContract = IRouterClient(ccipRouterETH);
        ccipRouterAVX = deployCode("MockCCIPRouter.sol", abi.encode(linkTokenAVX));
        ccipRouterAVXContract = IRouterClient(ccipRouterAVX);

        uint96 baseFee = 0.25 ether;
        uint96 gasPriceLink = 1e9;

        // DEPLOY VRF COORDINATOR
        vrfCoordinator = deployCode("VRFCoordinatorV2BetterMock.sol", abi.encode(linkTokenETH, baseFee, gasPriceLink));
        subscriptionId = 16015286601757825753; //;
        keyHash = 0x787d74caea10b2b357790d5b5247c2f63d1d91572a9846f780606e4d953677ae;

        vm.startPrank(admin);
        address prizeManagerAddress = deployCode("WinnablesPrizeManager.sol", abi.encode(linkTokenETH, ccipRouterETH));
        prizeManager = WinnablesPrizeManager(prizeManagerAddress);

        address ticketsAddress = deployCode("WinnablesTicket.sol", "");
        tickets = WinnablesTicket(ticketsAddress);

        address ticketManagerAddress = deployCode(
            "WinnablesTicketManager.sol",
            abi.encode(linkTokenAVX, vrfCoordinator, subscriptionId, keyHash, ticketsAddress, ccipRouterAVX)
        );

        ticketManager = WinnablesTicketManager(ticketManagerAddress);

        vm.stopPrank();

        prizeManager = WinnablesPrizeManager(prizeManagerAddress);
        tickets = WinnablesTicket(ticketsAddress);
        ticketManager = WinnablesTicketManager(ticketManagerAddress);

        vm.startPrank(admin);
        prizeManager.setCCIPCounterpart(address(ticketManager), chainSlectorAvax, true);
        ticketManager.setCCIPCounterpart(address(prizeManager), chainSlectorEth, true);

        //Create Token and NFT
        nft1 = new DemoNFT();
        token1 = new DemoToken();
        nft2 = new DemoNFT();
        token2 = new DemoToken();
        nft3 = new DemoNFT();
        token3 = new DemoToken();
        vm.stopPrank();
    }
}

```

Create the TicketManagerTest.t.sol class to test role assignment and removal.
```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "../src/WinnablesPrizeManager.sol";
import {Test, console} from "forge-std/Test.sol";
import {WinnablesTicket} from "../src/WinnablesTicket.sol";
import {WinnablesTicketManager} from "../src/WinnablesTicketManager.sol";
//import {MockLink} from "../src/mock/MockLink.sol";
import {VRFCoordinatorV2BetterMock} from "../src/mock/VRFCoordinatorV2BetterMock.sol";
import {BaseTest} from "./BaseTest.sol";
import {IWinnablesPrizeManager} from "../src/interfaces/IWinnablesPrizeManager.sol";
import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";

contract TicketManagerTest is BaseTest {
    function setUp() public override {
        super.setUp();
    }

    function test_remove_role() public {
        //1 - user1 try to mint tickets but it has no role 1 so it should revert
        vm.prank(user1);
        vm.expectRevert();
        tickets.mint(user1, 1, 1);

        //2 - admin gives role to user1
        vm.prank(admin);
        tickets.setRole(user1, 1, true);

        //3 - user1 mint tickets and it should work
        vm.prank(user1);
        tickets.mint(user1, 1, 1);

        //4 - admin removes role 1 from user1 so user1 should not be able to mint tickets
        vm.prank(admin);
        tickets.setRole(user1, 1, false);

        //5 - user1 try to mint tickets but it has no role 1 so it should revert but it work
        vm.prank(user1);
        vm.expectRevert();
        tickets.mint(user1, 1, 1);
    }
}
```

Then run the command

```shell
forge test --mt test_remove_role
```

You'll see that the command does not revert as expected

```shell
(base) ➜  winnable-raffles git:(main) ✗ forge test --mt test_remove_role  -vv  
[⠒] Compiling...
[⠔] Compiling 1 files with Solc 0.8.24
[⠑] Solc 0.8.24 finished in 334.49ms
No files changed, compilation skipped

Ran 1 test for test/TicketManagerTest.t.sol:TicketManagerTest
[FAIL. Reason: next call did not revert as expected] test_remove_role() (gas: 153584)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 10.23ms (1.05ms CPU time)

Ran 1 test suite in 156.39ms (10.23ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/TicketManagerTest.t.sol:TicketManagerTest
[FAIL. Reason: next call did not revert as expected] test_remove_role() (gas: 153584)
```

## Tool used

Manual Review et foundry 

## Recommendation

to fix the problem replace the function _setRole(address user, uint8 role, bool status) with 
```javascript
    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);

        if (status) {
            // Add the role by setting the bit to 1
            roles |= (1 << role);
        } else {
            // Remove the role by setting the bit to 0
            roles &= ~(1 << role);
        }

        _addressRoles[user] = bytes32(roles);
        emit RoleUpdated(user, role, status);
    }
```
