Dandy Blonde Carp

Medium

# `Roles::setRole()` doesn't work properly as admin `can't` remove users from their existing roles

## Summary
`Roles::setRole()` doesn't work properly as admin `can't` remove users from their existing roles

## Vulnerability Detail
`Admin` ie Role0 can set/assign other address for roles like `Role1` using `setRole()`.
```solidity
    function setRole(address user, uint8 role, bool status) external virtual onlyRole(0) {
        _setRole(user, role, status);
    }

    function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
        emit RoleUpdated(user, role, status);
    }
```

This is from ReadMe
> Role 0 is an admin role and it allows its members to grant or deny roles to other users

 Now the problem is, once a address is set for a role like Role1, it `can't` be removed from that role.

//Here is the PoC is foundry
Note: Before running this test, make _hasRole() public function
```solidity
// SPDX-License-Identifier: UNLICENSED
// Based on OpenZeppelin's ERC1155 v4.8.0
pragma solidity 0.8.24;

import {WinnablesTicket} from "../contracts/WinnablesTicket.sol";
import {Test, console2} from "../lib/forge-std/src/Test.sol";
import {MockERC20Token} from "./MockERC20Token.sol";

contract TicketTest is Test {
    WinnablesTicket tickets;
    address admin = address(0x1);
    address bob = address(0x2);

    function setUp() external {
        vm.startPrank(admin);
        tickets = new WinnablesTicket();
        vm.stopPrank();
    }

    function test_RolesCantRemoved() public {
        //Checking admin is role 0
        //Note: I've made _hasRole() public function for this testing
        bool isAdminRole0 = tickets._hasRole(admin, 0);
        assertTrue(isAdminRole0);

        //Admin setting bob as role 1
        vm.prank(admin);
        tickets.setRole(bob, 1, true);
        bool beforBobRole1 = tickets._hasRole(bob, 1);
        assertTrue(beforBobRole1);

        //Admin removing bob from role 1 but still bob is role 1
        vm.prank(admin);
        tickets.setRole(bob, 1, false);
        bool afterBobRole1 = tickets._hasRole(bob, 1);
        assertTrue(afterBobRole1);
    }
}
```
I've used `winnablesTicket.sol` for this testing but similar issue will arise in `winnablesTickedManager.sol` & `winnnablesPrizeManager.sol`

In winnablesTickedManager.sol, role1 signs the trx for ticket buying, if role1 turned malicious and started signing buy tickets with very low amount then it will a loss for the protocol. And admin will not be able to remove that address.
## Impact
Admin can't remove users from their roles

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L35C3-L37C6
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L29C1-L33C6
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/Roles.sol#L18

## Tool used
Manual Review

## Recommendation
Fix _setRole() in Roles.sol to allow admin to remove others from their roles