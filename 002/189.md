Itchy Mahogany Turtle

High

# Role Management Flaw Grants Permanent Access to Users

### Summary

The _setRole function in the Roles contract does not correctly handle the status parameter, which is intended to either assign (true) or revoke (false) a role. The current implementation always sets the role to active, regardless of the status.

### Root Cause

The _setRole function uses a bitwise OR operation to set the role, but does not use the status parameter to conditionally revoke the role. This causes roles to be assigned correctly, but never revoked.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/Roles.sol#L29C5-L33C6

```solidity
   function _setRole(address user, uint8 role, bool status) internal virtual {
        uint256 roles = uint256(_addressRoles[user]);
        _addressRoles[user] = bytes32(roles | (1 << role));
        emit RoleUpdated(user, role, status);
    

```


### Internal pre-conditions

An admin assigns a role to a specific address.
For any reason, it becomes necessary to revoke the previously assigned role from the address.

### External pre-conditions

none

### Attack Path

When an attempt is made to revoke the role by calling setRole with status set to false, the role remains active due to the improper handling of the status parameter.

### Impact

Privilege Retention:

Users or attackers retain roles and associated permissions that should have been revoked, leading to over-privileged accounts.
Security Vulnerability:

This can result in unauthorized actions, privilege escalation, or abuse of roles intended for limited use, compromising the security and integrity of the system.
Operational Risk:

Administrators cannot reliably manage and update user roles, causing operational inefficiencies and the potential for malicious exploitation.

### PoC

```solidity

// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/Roles.sol";  

contract RolesTest is Test {
    Roles roles;

    function setUp() public {
        roles = new Roles();
    }

    function testRoleSetAndRevoke() public {
        address user = address(1);

        // Initially, the user has no roles
        bytes32 initialRoles = roles.getRoles(user);
        console.log("Initial Roles: ", uint256(initialRoles));

        // Set role 1 for the user
        roles.setRole(user, 1, true);
        bytes32 rolesAfterSet = roles.getRoles(user);
        console.log("Roles after setting role 1: ", uint256(rolesAfterSet));
        assertTrue((uint256(rolesAfterSet) & (1 << 1)) > 0, "User should have role 1");

        // Revoke role 1 for the user
        roles.setRole(user, 1, false);
        bytes32 rolesAfterRevoke = roles.getRoles(user);
        console.log("Roles after revoking role 1: ", uint256(rolesAfterRevoke));
        assertTrue((uint256(rolesAfterRevoke) & (1 << 1)) == 0, "User should not have role 1 after revoke");
    }
}

```


### Mitigation

Update the `_setRole` function to properly handle both assigning and revoking roles based on the `status` parameter.

```solidity
function _setRole(address user, uint8 role, bool status) internal virtual {
    uint256 roles = uint256(_addressRoles[user]);
    if (status) {
        // Set the role
        _addressRoles[user] = bytes32(roles | (1 << role));
    } else {
        // Revoke the role
        _addressRoles[user] = bytes32(roles & ~(1 << role));
    }
    emit RoleUpdated(user, role, status);
}

```