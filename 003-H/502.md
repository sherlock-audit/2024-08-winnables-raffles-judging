Merry Plastic Poodle

High

# Attacker can DOS raffle creation by canceling all `PRIZE_LOCKED` raffles using `WinnablesTicketManager::cancelRaffle`

## Summary
`WinnablesTicketManager::cancelRaffle` allows anyone to cancel a raffle which is in `PRIZE_LOCKED` state. To create a raffle, raffle must be in `PRIZE_LOCKED` state. hence, anyone is able to grief raffle creation by canceling all the raffles which are in `PRIZE_LOCKED` state

## Vulnerability Detail
As stated in the code docs, `cancelRaffle` is a public function which allows any address to cancel a raffle under right conditions:
```solidity
    /// @notice (Public) Cancel a raffle if it can be canceled
    /// @param raffleId ID of the raffle to cancel
    //@audit anyone is able to cancel a raffle if its in PRIZE_LOCKED state
    function cancelRaffle(
        address prizeManager,
        uint64 chainSelector,
        uint256 raffleId
    ) external {
       //@audit-info check if raffle is cancelable
        _checkShouldCancel(raffleId);
        //@audit-info cancel it
        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId) //8 bit + raffleId
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
if we take a look at `_checkShouldCancel` we can see that if status of the raffle is `PRIZE_LOCKED` we simply return:
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```

which allows us to bypass all conditions and set status of raffle to `CANCELED`:
```solidity
    function cancelRaffle(...) external {
       //@audit-info check if raffle is cancelable, since status is PRIZE_LOCKED we can bypasss this
        _checkShouldCancel(raffleId);
        //@audit-info cancel it
        _raffles[raffleId].status = RaffleStatus.CANCELED;
        //...
```

this in result, prevents this raffle from creation:
```solidity
    function createRaffle(... ) external onlyRole(0) {
        //...
        //@audit-info do not allow raffle to be created, since raffle status is `CANCELED`
        if (raffle.status != RaffleStatus.PRIZE_LOCKED) revert PrizeNotLocked();
       //...
    }
```
## Impact
Attacker can prevent all the raffles from creation.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L264
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278-L288
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L436

## Tool used

Manual Review