Delightful Inky Panda

High

# Attacker can frontrun `WinnablesTicketManager.createRaffle` to cancel any raffle

## Summary

Due to improper check, an attacker can always frontrun admin's `createRaffle` transaction to cancel raffle.

## Vulnerability Detail

Let's investigate how a raffle is created:

1. admin call `WinnablesPrizeManager.lockNFT()`, `WinnablesPrizeManager.lockETH()` or `WinnablesPrizeManager.lockTokens()` on Ethereum mainnet. This operation calls `_sendCCIPMessage` to inform `WinnablesTicketManager`(on Avalanche) that a raffle can be created:

```solidity
    function lockTokens(
        address ticketManager,
        uint64 chainSelector,
        uint256 raffleId,
        address token,
        uint256 amount
    ) external onlyRole(0) {
        RafflePrize storage rafflePrize = _checkValidRaffle(raffleId);
        uint256 tokenBalance = IERC20(token).balanceOf(address(this));
        if (tokenBalance < amount + _tokensLocked[token]) revert InvalidPrize();
        rafflePrize.raffleType = RaffleType.TOKEN;
        unchecked { _tokensLocked[token] += amount; }
        _tokenRaffles[raffleId].tokenAddress = token;
        _tokenRaffles[raffleId].amount = amount;

        _sendCCIPMessage(ticketManager, chainSelector, abi.encodePacked(raffleId));
        emit TokenPrizeLocked(raffleId, token, amount);
    }
```
2. CCIP message get executed, `WinnablesTicketManager._ccipReceive` is called:
```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
        (uint256 raffleId) = abi.decode(message.data, (uint256));
        if (_raffles[raffleId].status != RaffleStatus.NONE) {
            // The raffle cannot be created, send back a cancel message to unlock the prize
            _sendCCIPMessage(
                _senderAddress,
                message.sourceChainSelector,
                abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
            );
            return;
        }
        _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

        emit RafflePrizeLocked(
            message.messageId,
            message.sourceChainSelector,
            raffleId
        );
    }
```
Let's consider the case of normal execution, when `RaffleStatus` is set to `PRIZE_LOCKED`.

3. Admin call `createRaffle()` to start the raffle and set `RaffleStatus` to `IDLE`(require current `RaffleStatus` == `PRIZE_LOCKED`):

```solidity
    function createRaffle(
        uint256 raffleId,
        uint64 startsAt,
        uint64 endsAt,
        uint32 minTickets,
        uint32 maxTickets,
        uint32 maxHoldings
    ) external onlyRole(0) {
        _checkRaffleTimings(startsAt, endsAt);
        if (maxTickets == 0) revert RaffleRequiresTicketSupplyCap();
        if (maxHoldings == 0) revert RaffleRequiresMaxHoldings();
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.PRIZE_LOCKED) revert PrizeNotLocked();

        raffle.status = RaffleStatus.IDLE;
        ...
    }
```

However, due to improper check, attacker can perform `cancelRaffle` between steps 2 and 3.
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;  //@Audit incorrect
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```
If `(raffle.status == RaffleStatus.PRIZE_LOCKED)` , function call will return, and `_checkShouldCancel` check passes.

On Avalanche,  [CCIP Executing DON](https://docs.chain.link/ccip/architecture#executing-don) is responsible for executing `_ccipReceive` transactions, and winnables admin is responsible for `createRaffle`, which means Step2 and Step3 must be executed asynchronously. As long as there are enough MEV resources, an attacker can always frontrun step 3 and force the raffle to get cancelled.

## Impact

Impact: HIGH - Every raffle can get DoS.

Likelihood: HIGH - No additional conditions are required for the frontrun, and the block where CCIP message is executed is predictable.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434

## Tool used

Manual Review

## Recommendation

`_checkShouldDraw` should not simply return when `raffle.status == RaffleStatus.PRIZE_LOCKED`.