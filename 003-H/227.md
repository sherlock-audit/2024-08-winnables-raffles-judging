Cheery Tangerine Bear

Medium

# An attacker can immediately cancel the raffle

## Summary
After the `WinnablesTicketManager` on the Avalanche chain receives a CCIP message from Ethereum indicating that the prize has been locked, there is a lack of permission control before the `WinnablesTicketManager` administrator calls `createRaffle`, allowing the raffle to be canceled by anyone.
## Vulnerability Detail
After the `WinnablesTicketManager` on the Avalanche chain receives a CCIP message from Ethereum indicating that the prize pool has been locked, the corresponding status changes to `PRIZE_LOCKED`.
```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
        (uint256 raffleId) = abi.decode(message.data, (uint256));
         ...
        _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

        emit RafflePrizeLocked(
            message.messageId,
            message.sourceChainSelector,
            raffleId
        );
    }
```
When a raffle is in the `PRIZE_LOCKED` status, anyone can call `cancelRaffle` to cancel it.
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        ...
    }

    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
An attacker can monitor the contract and, as soon as the contract emits the `RafflePrizeLocked` event, immediately send a transaction to call cancelRaffle and cancel the raffle.

This would result in a loss for the protocol. And have the opportunity to continuously obstruct the normal operation of the raffle.
## Impact
Such attack would result in:
1. Disrupt the normal functioning of the raffle.
2. Cause the `WinnablesPrizeManager` administrator to waste expensive gas fees on the Ethereum and `LinkTokens` to pay CCIP message.
3. Since the `LinkTokens` used to pay for CCIP messages are provided by the protocol, the CCIP messages sent by the attacker calling `cancelRaffle` would also consume the protocol's LinkTokens.  

In contrast, the attacker only needs to pay the relatively cheap gas fees on the Avalanche chain.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434
## Tool used

Manual Review

## Recommendation
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
-        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
+        if (raffle.status == RaffleStatus.PRIZE_LOCKED){
+           if (!_hasRole(msg.sender, 1)) revert Unauthorized();
+        }
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }

```