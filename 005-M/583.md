Glamorous Porcelain Toad

High

# `_packCCIPContract` function zero out the chain selector unintentionally causing the address to call from any chain.

### Summary

CCIPReceive function can be trigerred from any arbitrary chain, where an attacker can deploy a smart contract to an address that have been configured for some other chain because the chain id is never in consideration and is zeroed out. 

### Root Cause

The following function is used to configure the address and the chain from which messaged can be received and important function like choosing the winner, cancelling the raffle and distribution can be done. 

It is implemented as follow:

```solidity
    function _setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) internal {
        bytes32 counterpart = _packCCIPContract(contractAddress, chainSelector);
        _ccipContracts[counterpart] = enabled;
    }
```

Here note that chain selector is 64 bits.

Now let's look at the code for _packCCIPContract 

```solidity
    function _packCCIPContract(address contractAddress, uint64 chainSelector)
        return bytes32(
            uint256(uint160(contractAddress)) |
            uint256(chainSelector << 160)
        );
    }
    
```

The problem is following code in above snippet

```solidity
uint256(chainSelector << 160)
```

We left shift a 64 bit number by 160 bit zeroing it all out. 

This behaviour can be tested in foundry by using the following code:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

contract winnableTest is Test {
        function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
            uint256(chainSelector << 160)
        );
    }

    function _unpackCCIPContract(bytes32 packed) internal pure returns(address contractAddress, uint64 chainSelector) {
    contractAddress = address(uint160(uint256(packed)));
    chainSelector = uint64(uint256(packed) >> 160);
}

    function test_encode_decode_fuzz(address _contractAddress, uint64 _chainSelector) public
    {
        vm.assume(_chainSelector != 0);
        vm.assume(_contractAddress != address(0));

        bytes32 packed = _packCCIPContract(_contractAddress, _chainSelector);
        console.log("Packed: ");
        console.logBytes32(packed);
        (address contractAddress, uint64 chainSelector) = _unpackCCIPContract(packed);
        assertEq(_contractAddress, contractAddress);
        assertEq(_chainSelector, chainSelector);
    } 


    function test_encode_decode_comparison_fuzz(address _contractAddress, uint64 _chainSelector) public
    {
        vm.assume(_chainSelector != 0);
        vm.assume(_contractAddress != address(0));

        uint64 _chainSelectorHardcoded = 98774893;

        bytes32 packedHardCoded = _packCCIPContract(_contractAddress, _chainSelectorHardcoded);

        bytes32 packed = _packCCIPContract(_contractAddress, _chainSelector);
        console.log("Packed: ");
        console.logBytes32(packed);
                console.log("Packed Hardcoded: ");
        console.logBytes32(packedHardCoded);

        assert(packed != packedHardCoded);
        
    } 

} 

```



### Internal pre-conditions

1. Admin set up a CCIP counter part, let's say addres 0x1 on base chain with id of 1. 
2. The stored bytes 32 counter part will be 0x1..000, instead of 0x1...001 (this is arbitary but makes sense).
3. Now since only the address is stored and chain id is zeroed out, it will generate the same counterpart for every chain.
4. So let's say someone deploys a contract on address 0x01 and tries to send a message to winnable smart conteract on EVM, following check will pass:

```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));


        // ccip router calls it, sender address from any chain can call it, what if the address is controlled by some AAA wallet. 
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
        uint256 raffleId;
        address winner;
        if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
            raffleId = _decodeRaffleCanceledMessage(message.data);
            _cancelRaffle(raffleId);
            return;
        }
        (raffleId, winner) = _decodeWinnerDrawnMessage(message.data);
        _rafflePrize[raffleId].winner = winner;
        emit WinnerPropagated(raffleId, winner);
    }
    
```

Look at following lines:
```solidity
  bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
```

If message is coming from address 0x01 from base which is winnable controlled, or it is coming from 0x01 from optimism let's say which is attacker controlled address it will run the logic and the if condition will pass since in both cases it check for 0x1000 which is set, and gives all the power to the attacker where he could cencel raffles, draw winner arbitrarily, or change the raffle status. Anything in ccip receive on both ends is in attacker control. 


https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L278

### External pre-conditions

_No response_

### Attack Path

Discribed the attack path already. 

### Impact

All the control will go to arbitrary address on and arbitrary chain, only requirement is that, that chain must have chain link ccip deployed. 

### PoC

Can provide if required. 

### Mitigation

Correct the logic for bit shifting, make it higher precision and than do the bit shifting that will prevent it from zeroing out. 