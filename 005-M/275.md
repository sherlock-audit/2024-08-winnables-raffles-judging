Savory Chiffon Gazelle

Medium

# Incorrect Packing of `Address` and `Chain Selector` in `BaseCCIPContract.sol::_packCCIPContract` Leads to Improper Mapping Key Generation

### Summary


The function `BaseCCIPContract::_packCCIPContract` in the `BaseCCIPContract` contract is intended to create a `bytes32` mapping [`key`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L11-L15) by packing an `address` into the lower 160 bits ([0...159](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L13)) and a `chainSelector` into the higher 64 bits [(160...223).](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L14)

However, due to incorrect bitwise operations, the function fails to correctly position the `chainSelector`, resulting in an incorrect mapping key. This can cause incorrect mappings in the `_ccipContracts mapping`, potentially leading to mismatches and failures in cross-chain communication.



### Root Cause

In the function `BaseCCIPContract.sol::_packCCIPContract` [code](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L40-L45)
```javascript
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
            uint256(chainSelector << 160)
        );
    }
```
`uint256(chainSelector << 160)` shifts the `chainSelector` to the left by 160 bits `before converting it to a uint256`. This causes a problem because `the shift operation is performed while chainSelector is still a uint64`, meaning the result of the shift is still a `uint64` before it gets cast to a uint256.

For example:
In `BaseCCIPContract.sol::_packCCIPContract` 
if we pass:
1. `contractAddress = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59`
2. `chainSelector = 16015286601757825752`

->  Then the bytes32 it is returning is: `0x0000000000000000000000000bf3de8c5d3e8a2b34d2beeb17abfcebaf363a59`
->  But according to [The Logic](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L11-L15) it should return `0x00000000de41ba4fc9d91ad80bf3de8c5d3e8a2b34d2beeb17abfcebaf363a59` where `0bf3de8c5d3e8a2b34d2beeb17abfcebaf363a59 is the contract address denoting (0...159)` and `41ba4fc9d91ad80 is the chainSelector in HexaDecimal denoting (160...223)`

### Internal pre-conditions

1. `contractAddress` needs to be a valid Ethereum address.
2. `chainSelector` needs to be a 64-bit unsigned integer representing the chain selector.

For `any` `chainSelector` and `a particular contractAddress` value, the `BaseCCIPContract.sol::_packCCIPContract` will return the same value always.

### External pre-conditions

The contract depends on the correct packing of `contractAddress` and `chainSelector` to generate mapping keys.

### Attack Path

- **Initialization of Counterpart Contracts:**
    - The administrator calls the `setCCIPCounterpart` function in either the [`WinnablesPrizeManager::setCCIPCounterpart`)](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L134-L140) (on Ethereum) or [`WinnablesTicketManager::setCCIPCounterpart`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L238-L244) (on Avalanche) to set approved counterpart contracts.
    - The function `_packCCIPContract` is called internally to generate a `bytes32` key from the `contractAddress` and `chainSelector`.
    - Due to the incorrect bitwise operation, the `chainSelector` value is not accurately represented in the generated key.

- **Cross-Chain Communication:**
    - When a cross-chain message is sent to the contract, the `_ccipReceive` function is triggered in both [`WinnablesPrizeManager::_packCCIPContract`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L278) and [`WinnablesTicketManager::_packCCIPContract`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L365-L388).
    - The function `_packCCIPContract` is called again to generate the key to verify the counterpart contract.
    - Due to the improper handling of the `chainSelector`, the key generated might not match the correct approved counterpart stored in the `_ccipContracts` mapping.

- **Message Rejection:**
    - The cross-chain message verification fails because the keys do not match.
    - The contract reverts with an `UnauthorizedCCIPSender` error, even if the message was sent from a legitimate counterpart contract.
    - This prevents valid cross-chain operations, such as locking prizes or propagating raffle results, potentially disrupting the entire raffle process.
- **Impact on Raffles:**
    - If the legitimate cross-chain messages are consistently rejected, the raffle system fails to function as intended.
    - Prizes may not be unlocked or claimed, leading to a loss of trust in the system and possible financial losses for users.

### Impact

1. The protocol may fail to correctly identify or authenticate the correct cross-chain contract due to improper key generation.
2. This could lead to failed transactions or the inability to properly handle cross-chain messages.
3. This can lead to the rejection of valid cross-chain messages, causing the raffle system to malfunction. This could result in locked prizes, disrupted raffle operations, and loss of trust in the platform.

### PoC

The test file in foundry, where I have re-written the function `_packCCIPContract` to test it.

Code:
```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";

import {Raffle} from "../src/BaseCCIPContractTest.sol";

contract RaffleTest is Test {
    address owner = address(0x1);

    function _packCCIPContract(address contractAddress, uint64 chainSelector) public pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
@>          uint256(chainSelector << 160)
        );
    }

    function setUp() public {}

    function test_packCCIPContract() public {
        vm.startPrank(owner);
        bytes32 x = _packCCIPContract(address(0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59), 16015286601757825752);
        console.logBytes32(x);
        vm.stopPrank();
    }

}
```

Output:
```javascript
Logs:
  0x0000000000000000000000000bf3de8c5d3e8a2b34d2beeb17abfcebaf363a59
```

This output only contains the `address` and not the `chainSelector`

### Mitigation

To fix the issue, the `chainSelector` should be properly combined with the `contractAddress` to ensure that both are accurately represented in the resulting bytes32 value. The corrected function is:

```diff
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
-     uint256(chainSelector << 160)
+     (uint256(chainSelector) << 160)
    );
}
```

### This change will work because:
`(uint256(chainSelector) << 160)` first converts `chainSelector` to a `uint256` and `then shifts the result to the left by 160 bits`. This ensures that the shift operation is applied to a 256-bit value, preserving all the bits of chainSelector during the shift.

This produces the correct output where both the address and chainSelector are represented accurately in the bytes32 value.


### Proof after resolving the issue
```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";

import {Raffle} from "../src/BaseCCIPContractTest.sol";

contract RaffleTest is Test {
    address owner = address(0x1);

    function _packCCIPContract(address contractAddress, uint64 chainSelector) public pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
            (uint256(chainSelector) << 160)
        );
    }

    function setUp() public {}

    function test_packCCIPContract() public {
        vm.startPrank(owner);
        bytes32 x = _packCCIPContract(address(0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59), uint64(16015286601757825752));
        console.logBytes32(x);
        vm.stopPrank();
    }

}
```
#### Output:
```javascript
Logs:
  0x00000000de41ba4fc9d91ad80bf3de8c5d3e8a2b34d2beeb17abfcebaf363a59
```

The output correctly includes both the `contractAddress` and the `chainSelector` because the `shift` is correctly applied to a `uint256`.
