Future Butter Horse

Medium

# `_ccipReceive` is not properly access-restricted because the `ChainSelector` is not included in the packed `BaseCCIPContract._ccipContracts` mapping

## Vulnerability details

When the admin calls the `setCCIPCounterpart` function to enable the trusted contract on the opposite chain, the internal function [`BaseCCIPContract._packCCIPContract`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPContract.sol#L40-L45) should pack the `contractAddress` and `chainSelector` into a single bytes32 value. However, due to incorrect shifting, the `chainSelector` value is lost. Subsequently, when `_ccipReceive` is called, the `chainSelector` remains unvalidated:

```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
        
        ...
    }
```

Additionally, according to CCIP documentation best practices, the source chain of the `_ccipReceive` function should be verified.

https://docs.chain.link/ccip/best-practices#verify-source-chain

However, in the `_ccipReceive` function, although there's an attempt at validation, the flaw in `_packCCIPContract` makes this check ineffective.

## Impact

This vulnerability allows the source chain to remain unverified, enabling untrusted addresses to call the `_ccipReceive` function on both [`WinnablesTicketManager`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L365-L388) and [`WinnablesPrizeManager`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L278) from unauthorized chains. This access could potentially compromise the entire game, allowing malicious actors to cancel raffles or manipulate winner selection.

## Proof of Concept

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {BaseCCIPContract} from "../contracts/BaseCCIPContract.sol";

contract POC is Test {
    BaseCCIPContractTest ccipContract;

    function setUp() public {
        ccipContract = new BaseCCIPContractTest();
    }
    
    function test_pack() public {
        address addr = address(779467539909814889708631910782017610483164022920); // address(0x8888888888888888888888888888888888888888)
        uint64 num = type(uint64).max;
        
        emit log_named_address("contract address ", addr);
        emit log_named_uint("chain selector   ", num);

        bytes32 pack = ccipContract.packCCIPContract(addr, num);
        bytes32 packFixed = ccipContract.packCCIPContractFixed(addr, num);

        emit log_named_bytes32("packCCIPContract      ", pack);
        emit log_named_bytes32("packCCIPContractFixed ", packFixed);

        (address a, uint64 n) = ccipContract.unpack(pack);
        emit log_named_address("unpacked contract address ", a);
        emit log_named_uint("unpacked chain selector   ", n);

        (a, n) = ccipContract.unpack(packFixed);

        emit log_named_address("fixed unpacked contract address ", a);
        emit log_named_uint("fixed unpacked chain selector   ", n);
    }
}

contract BaseCCIPContractTest is BaseCCIPContract {
    constructor() BaseCCIPContract(address(0)) {}

    function packCCIPContract(address contractAddress, uint64 chainSelector) external returns(bytes32) {
        return _packCCIPContract(contractAddress, chainSelector);
    }

    function packCCIPContractFixed(address contractAddress, uint64 chainSelector) external returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) | 
            uint256(chainSelector) << 160
        );
    }

    function unpack(bytes32 packedData) external returns(address, uint64) {
        // Extract the first 160 bits for the address
        address contractAddress = address(uint160(uint256(packedData)));

        // Extract the upper 64 bits for the chainSelector
        uint64 chainSelector = uint64(uint256(packedData >> 160));

        return (contractAddress, chainSelector);
    }
}
```

Result: 

```solidity
Logs:
  contract address : 0x8888888888888888888888888888888888888888
  chain selector   : 18446744073709551615
  packCCIPContract      : 0x0000000000000000000000008888888888888888888888888888888888888888
  packCCIPContractFixed : 0x00000000ffffffffffffffff8888888888888888888888888888888888888888
  unpacked contract address : 0x8888888888888888888888888888888888888888
  unpacked chain selector   : 0
  fixed unpacked contract address : 0x8888888888888888888888888888888888888888
  fixed unpacked chain selector   : 18446744073709551615
```

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L368-L370

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L263-L265

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPContract.sol#L40-L45

## Tools Used

Manual Review

## Recommendation

Instead of shifting `chainSelector` 160 bits to the left before converting it to `uint256`, it should be converted first and then shifted

```diff
    function _packCCIPContractFixed(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) | 
-           uint256(chainSelector << 160)
+           uint256(chainSelector) << 160
        );
    }
```