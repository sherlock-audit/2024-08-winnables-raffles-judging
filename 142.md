Delightful Inky Panda

Medium

# Raffle cannot be drawn when Ticket supply is large

## Summary

Raffle cannot be drawn when Ticket supply is large.

## Vulnerability Detail

Let's examine how winners are chosen:
```solidity
    function _getWinnerByRequestId(uint256 requestId) internal view returns(address) {
        RequestStatus storage request = _chainlinkRequests[requestId];
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(request.raffleId);
        uint256 winningTicketNumber = request.randomWord % supply;
        return IWinnablesTicket(TICKETS_CONTRACT).ownerOf(request.raffleId, winningTicketNumber);
    }
```
and in `WinnablesTicket.sol`:
```solidity
  function ownerOf(uint256 id, uint256 ticketId) public view returns (address) {
    if (ticketId >= _supplies[id]) {
      revert InexistentTicket();
    }
    while (_ticketOwnership[id][ticketId] == address(0)) {
      unchecked { --ticketId; }
    }
    return _ticketOwnership[id][ticketId];
  }
```
In searching for the winner, this loop is executed many times:
```solidity
    while (_ticketOwnership[id][ticketId] == address(0)) {
      unchecked { --ticketId; }
    }
```
In this loop, the single consumption of gas is：
- read mapping from storage cost 2100
- `==` cost 3
- `--` cost 3

In the CONTEST readme it is stated that:

> Are there any limitations on values set by admins (or other roles) in the codebase, including restrictions on array lengths?
> 
> None other than the restrictions inherent to the types used (for example: WinnablesTicketManager::buyTickets() take a uint16 argument for ticketCount. That means the maximum number of tickets that can be purchased in a single transaction is type(uint16).max)

Lets consider the case that a user purchases `type(uint16).max)` tickets in a transaction and the last ticket is the winner, so the loop would goes to the first ticket. Here is a simple coded poc to estimate the gas cost:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import {Test, console} from "forge-std/Test.sol";

contract Testloop{

    mapping(uint256 => mapping(uint256 => address)) private _ticketOwnership;

    function setmapping(uint256 id, uint256 startId, address to) public {
        _ticketOwnership[id][startId] = to;
    }

  function ownerOf(uint256 id, uint256 ticketId) public view returns (address) {
    //if (ticketId >= _supplies[id]) {
    //  revert InexistentTicket();
    //}
    while (_ticketOwnership[id][ticketId] == address(0)) {
      unchecked { --ticketId; }
    }
    return _ticketOwnership[id][ticketId];
  }
}

contract CounterTest is Test {
    Testloop public testloop;

    function setUp() public {
        testloop = new Testloop();
        testloop.setmapping(uint256(0), uint256(0), address(0xFF));
    }

    function test_Loopgas() public {
        testloop.ownerOf(uint256(0), uint16(type(uint16).max));
    }
}
```

and output:

```text
$ forge test -vvv
[⠢] Compiling...
No files changed, compilation skipped

Ran 2 tests for test/test.t.sol:CounterTest
[PASS] test_Loopgas() (gas: 150673070)
[PASS] testloop() (gas: 2415)
Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 71.28ms (71.20ms CPU time)
```
As we can see, more than 150000000 gwei is consumed on this transaction. However, gas limit on Avalanche-C is set to 15000000, so the raffle can never get settled.

https://avascan.info/blockchain/c/block/49400000

## Impact

Impact: HIGH - once it happens, all related funds will be locked permanently.

Liklihood: LOW - such raffle is rare, but can happen per contest readme.

Severity: MEDIUM

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L472

## Tool used

Manual Review

## Recommendation

Fully acknowledge the risk.