Silly Burgundy Scallop

High

# Rewards are lost if winner has different addr on Avalanche and Mainnet

## Summary

Prizes will be forfeited because the winner's address is not the same on Avalanche and Mainnet.

## Vulnerability Detail

When a raffle is created, any user can participate in it by purchasing tokens via `buyTickets()` on Avalanche and a `WinnablesTicket`(ERC1155) will be minted to them **on Avalanche**. Then, when the raffle is over and a winner is selected with `drawWinner()` and `propagateRaffleWinner()`, the owner's address of the winning ticket (ERC1155 token) will be sent to `WinnablesPrizeManager.sol` on the `Mainnet` via CCIP. The owner's address on ****Avalanche will be set as the winner of this raffle on Mainnet, then this address can claim his prize via `claimPrize()`.

But the owner address on Avalanche and Mainnet will not always be the same (eg Gnosis safes, custom multisig wallets, etc.)

```solidity
function buyTickets(
    uint256 raffleId,
    uint16 ticketCount,
    uint256 blockNumber,
    bytes calldata signature
) external payable {
    if (ticketCount == 0) revert InvalidTicketCount();
    _checkTicketPurchaseable(raffleId, ticketCount);
    _checkPurchaseSig(raffleId, ticketCount, blockNumber, signature);

    Raffle storage raffle = _raffles[raffleId];
    uint256 participation = uint256(raffle.participations[msg.sender]);
    uint128 totalPaid = uint128(participation) + uint128(msg.value);
    uint32 totalPurchased = uint32(participation >> 128) + uint32(ticketCount);
    unchecked {
        raffle.participations[msg.sender] = bytes32(
            (participation & type(uint256).max << 160)
            | totalPaid |
            uint256(totalPurchased) << 128
        );
    }
    unchecked {
        raffle.totalRaised += msg.value;
        _userNonces[msg.sender]++;
        _lockedETH += msg.value;
    }
    IWinnablesTicket(TICKETS_CONTRACT).mint(msg.sender, raffleId, ticketCount);
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
}
```

```solidity
function drawWinner(uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    _checkShouldDraw(raffleId);
    raffle.status = RaffleStatus.REQUESTED;

    uint256 requestId = VRFCoordinatorV2Interface(VRF_COORDINATOR).requestRandomWords(
        KEY_HASH,
        SUBSCRIPTION_ID,
        3,
        100_000,
        1
    );
    _chainlinkRequests[requestId] = RequestStatus({
        raffleId: raffleId,
        randomWord: 0
    });
    raffle.chainlinkRequestId = requestId;
    emit RequestSent(requestId, raffleId);
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
}

/// @notice (Public) Send a cross-chain message to the Prize Manager to
///         mark the prize as claimable by the winner
/// @param raffleId ID of the Raffle we wish to draw a winner for
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
    raffle.status = RaffleStatus.PROPAGATED;
    address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

    _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    unchecked {
        _lockedETH -= raffle.totalRaised;
    }
}
```

```solidity
function _ccipReceive(
    Client.Any2EVMMessage memory message
) internal override {
    (address _senderAddress) = abi.decode(message.sender, (address));
    bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
    if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

    CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
    uint256 raffleId;
    address winner;
    if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
        raffleId = _decodeRaffleCanceledMessage(message.data);
        _cancelRaffle(raffleId);
        return;
    }
    (raffleId, winner) = _decodeWinnerDrawnMessage(message.data);
    _rafflePrize[raffleId].winner = winner;
    emit WinnerPropagated(raffleId, winner);
}
```

## Impact

Loss of prize due to the assumption that the winner's wallet address is always the same on Avalanche and Mainnet.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesPrizeManager.sol#L276

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L340

## Tool used

Manual Review

## Recommendation

Allow the user to set the recipient address when buying tickets (can be achieved with simple mapping inside `buyTickets()` and then fetching it on `propagateRaffleWinner()`), and if he wins, use that address as the winner on the Mainnet.