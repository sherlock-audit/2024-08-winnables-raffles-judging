Spare Parchment Millipede

High

# `claimPrize` in `WinnablesPrizeManager.sol` is vulnerable to re-entrancy

## Summary
The `claimPrize(uint256 raffleId)` is an externally callable method that is called by the raffle winner to claim the prize of the Raffle.

However, the code does not follow CEI pattern which leads to re-entrancy vulnerability and for the raffle winner to be able drain all the locked ETH in the contract!

## Vulnerability Detail
The function `claimPrize()` is supposed to distribute the raffle prize based on the `RaffleType` associated with the `raffleId`. 

The cause of the issue is that we are sending the prize of the raffle to the winner before setting the
`rafflePrize.status == RafflePrizeStatus.CLAIMED`.

For `RaffleType.NFT` and `RaffleType.TOKEN` this is not an issue as the protocol readme states that they will not be using any weird ERC20s so we can assume that the tokens will not be re-entreable.

However, in case of `RaffleType.ETH` the contract is  calling `_sendETHPrize` which makes an external call to the winner as seen below:

```solidity
function _sendETHPrize(uint256 amount, address winner) internal {
    (bool success, ) = winner.call{ value: amount }("");
    if (!success) revert ETHTransferFail();
}
```

The check `if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();` is happening afterwards the call.

So the malicious contract can re-enter the `WinnablePrizeManager::claimPrize()` via its malicious `receive()/fallback()` method and claim the prize again!

This would mean that the malicious contract can keep on calling the contract till the eth is drained.

```solidity
function claimPrize(uint256 raffleId) external {
    RafflePrize storage rafflePrize = _rafflePrize[raffleId];
    RaffleType raffleType = rafflePrize.raffleType;
    if (raffleType == RaffleType.NFT) {
        NFTInfo storage raffle = _nftRaffles[raffleId];
        _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
        _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
    } else if (raffleType == RaffleType.TOKEN) {
        TokenInfo storage raffle = _tokenRaffles[raffleId];
        unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
        _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
    } else if (raffleType == RaffleType.ETH) {
        unchecked { _ethLocked -= _ethRaffles[raffleId]; }
        // @audit high -> since check happening after interaction
        // re-entrancy possible in case of malicious winner!
        _sendETHPrize(_ethRaffles[raffleId], msg.sender);
    } else revert InvalidRaffle();
    if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
    if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
    rafflePrize.status = RafflePrizeStatus.CLAIMED;
    emit PrizeClaimed(raffleId, msg.sender);
}
```

## Impact

The malicious winner can drain all the eth locked in the `WinnablePrizeManager`.

Lets go through a sample proof of concept:

1. Lets say there are 2 raffles of 1 eth and 4 eth respectively.
2. Winner of `raffle1` is `maliciousContract`, while `raffle2` is still ongoing.
3. Total `_ethLocked` in contract is 5 eth, claimable amount by `maliciousContract` is 1 eth
4. `maliciousContract` calls `claimPrize`, receives 1 eth via receive()/fallbac() and calls back into `claimPrize`
5. The `maliciousContract` can keep on doing this all the eth in the  account is drained.

## Code Snippets
`claimPrize()`: https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105-L124

`_sendEthPrize()`: https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L314-L317

## Tool used

Manual Review

## Recommendation
1. Use `reentrancyGuard` from `openzeppelin`
2. Follow check-effect-interaction and update the status before making external calls to distribue prize.
   
```diff
function claimPrize(uint256 raffleId) external {
    RafflePrize storage rafflePrize = _rafflePrize[raffleId];
    RaffleType raffleType = rafflePrize.raffleType;
+   if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+   if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+    rafflePrize.status = RafflePrizeStatus.CLAIMED;

    if (raffleType == RaffleType.NFT) {
        NFTInfo storage raffle = _nftRaffles[raffleId];
        _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
        _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
    } else if (raffleType == RaffleType.TOKEN) {
        TokenInfo storage raffle = _tokenRaffles[raffleId];
        unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
        _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
    } else if (raffleType == RaffleType.ETH) {
        unchecked { _ethLocked -= _ethRaffles[raffleId]; }
        _sendETHPrize(_ethRaffles[raffleId], msg.sender);
    } else revert InvalidRaffle();
-   if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-   if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-    rafflePrize.status = RafflePrizeStatus.CLAIMED;
    emit PrizeClaimed(raffleId, msg.sender);
}
```