Helpful Gauze Hawk

High

# Reentrancy in `claimPrize` can drain ETH prizes

### Summary

An attacker can steal ETH prizes from the protocol by reentering the `WinnablesPrizeManager:claimPrize()` function when multiple ETH prize raffles are ongoing and they have won a raffle.

### Root Cause

There are two root issues here. 
1. `claimPrize()` does not follow the CEI pattern. These checks and raffle status change should happen at the beginning of the function, not the end 
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L120-#L122
```solidity
if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
rafflePrize.status = RafflePrizeStatus.CLAIMED;
```
2. The second issue is that `_sendETHPrize()` uses the `.call()` method without gas limits. This allows an attacker to create a smart contract that can reenter the `claimPrize()` function and claim extra ETH that should be reserved for other raffles.

### Internal pre-conditions

1. The attacker must win an ETH raffle from their attacking smart contract.
2. There must be at least double the prize amount of ETH the attacker originally won locked in `WinnablesPrizeManager.sol` most likely from other ongoing ETH raffles. 

### External pre-conditions

_No response_

### Attack Path

1. The attacker creates a smart contract that will call `claimPrize` when it receives ETH, can enter ETH raffles, and has the same address on both Ethereum and Avalanche.
2. The attacker wins an ETH raffle 
3. The attacker waits until there is one or more ETH raffles ongoing and the amount of `_lockedETH` is at least double the original prize amount the attacker won.
4. The attacker calls the `claimPrize` function which sends ETH via `.call` 
5. When the attacking contract receives the ETH from the raffle it rightfully won it calls back and reenters the `claimPrize` function.
6. The attacker claims the same amount of ETH again stealing it from other ongoing ETH raffles.
7. The attacker can repeat this until there is less ETH in the contract than the original ETH prize they won.

### Impact

The attacker gains double or more ETH than they originally should have. 
Victims of the attack are other ETH raffle winners who will not be able to claim their rightful prizes.
If ETH raffles that are affected by this attack, are not canceled or unable to be canceled, then the leftover ETH will be stuck in the contract as it will not be decremented from `_lockedETH`.

### PoC

_No response_

### Mitigation

Follow CEI in `claimPrize`

```solidity
function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
+       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
-        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
