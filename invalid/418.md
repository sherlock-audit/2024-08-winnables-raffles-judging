Rural Oily Crocodile

High

# Improper CEI pattern allows winner of a raffle to re-enter and drain the contract of ETH funds

## Summary
Improper CEI pattern allows winner of an ETH raffle to drain the contract from all funds.

## Vulnerability Detail
When an ETH raffle winner claims their prize, an [external call to send the prize](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L116-L118) is made before [setting the prize status](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L122) to `RafflePrizeStatus.CLAIMED`. 

The function that sends out the ETH is a simple [low-level external `.call`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L314-L317):

```solidity
    function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```
What this allows is for the winner to use a contract (no limitations on that) as the receiver of the prize of an ETH raffle, re-enter when claiming and drain the contract of all eth funds in it.
## Impact
Since the protocol is going to run multiple raffles simultaneously, when there is more than 1 ETH raffle running at a time, the winner of the first one will be able to drain the funds from the other ones that are still running. 

Let's look at a step by step example:

**Environment**:
- 3 Simultaneous ETH raffles running, each with 10ETH prize
- `_ethLocked` will be 30ETH in this case

**PoC**:
1. Raffle A concludes and Alice wins, Raffle B & C are still on-going
2. Alice calls `claimPrize()` with the `raffleId`
3. `RaffleType == ETH`, so we enter the 3rd `else-if`
4. `_ethLocked -= _ethRaffles[raffleId]` or `30ETH - 10ETH` 20ETH left in the contract for B & C
5. Alice's attacker contract receives the prize and in the fallback function re-enters to claim again
6. Function flow of `claimPrize` begins again from the start
7. Same thing repeats 2 more times, so 3 in total, Alice receives all 30ETH allocated for 3 raffles instead of her prize of 10ETH
8. Alice's fallback function has a flag to only re-enter twice, so on the 3rd fallback her contract returns 
9. `claimPrize` continues and the `rafflePrize.status` is finally set to `CLAIMED`
10. Function flow ends, Alice has drained the contract from all ETH raffle prizes locked in it, she gains 30ETH instead of 10

The testing environment is in hardhat whilst I've only used foundry so I was unable to provide a coded PoC unfortunately.
## Code Snippet
```solidity
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
@>          _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
@>      rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
## Tool used
Manual Review
## Recommendation
Proper CEI pattern, set the CLAIMED flag before the external call.