Itchy Mahogany Turtle

Medium

# Raffle Start Time Adjustment Fails to Update Properly

### Summary

The `WinnablesTicketManager` smart contract exhibits a flaw in the `_checkRaffleTimings` function, which inadequately handles the adjustment of the raffle start times (`startsAt`). The issue is specifically related to instances where the specified start time is in the past compared to the current blockchain time (`block.timestamp`). The function correctly calculates an adjusted start time to prevent past-dated raffles but fails to propagate this adjusted value back to the raffle creation logic. This oversight can cause raffles to incorrectly begin in the past, leading to potential administrative complications and participant confusion.

### Root Cause



The root cause of the issue lies in the scope and mutability of the variable `startsAt` within the `_checkRaffleTimings` function. The function is designed to check and adjust the `startsAt` variable to ensure it is not set in the past:

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L399C5-L402C6

```solidity
function _checkRaffleTimings(uint64 startsAt, uint64 endsAt) internal view {
    if (startsAt < block.timestamp) startsAt = uint64(block.timestamp);
    if (startsAt + MIN_RAFFLE_DURATION > endsAt) revert RaffleClosingTooSoon();
}

```

Here, `startsAt` is a local copy of the argument passed to the function. When this local variable is updated (i.e., set to the current `block.timestamp` if it was originally in the past), the change does not affect the actual `startsAt` value used in the `createRaffle` function. This is because primitive data types like uint64 are passed by value in Solidity, not by reference. Therefore, modifications to `startsAt` within the function do not propagate back to the calling context.

As a result, even though `_checkRaffleTimings` internally enforces that raffles cannot start in the past by adjusting `startsAt,` this adjustment is not reflected in the raffle's recorded start time, allowing the creation of raffles with start times that have already elapsed.

### Internal pre-conditions



No special internal conditions required for this bug.  

### External pre-conditions

No External pre-conditions are required. 



### Attack Path

Call `createRaffle` with `startsAt` in the past. 

### Impact

## Impact

The failure to correctly update the `startsAt` parameter for raffles with start times set in the past can lead to several impactful consequences:

1. **Participant Confusion**: Participants may be misinformed about the actual start time of a raffle, leading to confusion and possibly a loss of trust if the raffle appears to start earlier than advertised. This could affect participant engagement and the perceived reliability of the platform.

2. **Financial Implications**: An unexpectedly early raffle start can result in a shorter ticket sales window, potentially reducing the total revenue generated from ticket sales. This could impact the financial outcomes for both organizers and potential beneficiaries of the raffle proceeds.



### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/WinnablesTicketManager.sol";
import "../contracts/interfaces/IWinnables.sol"; // Check if IWinnables is the correct interface as used in the main contract.


contract MockIERC20 is IERC20 {
    function totalSupply() external view returns (uint256) {}
    function balanceOf(address account) external view returns (uint256) {}
    function transfer(address recipient, uint256 amount) external returns (bool) {}
    function allowance(address owner, address spender) external view returns (uint256) {}
    function approve(address spender, uint256 amount) external returns (bool) {}
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {}
}


contract MockVRFConsumerBaseV2 is VRFConsumerBaseV2 {
    constructor(address coordinator) VRFConsumerBaseV2(coordinator) {}

    // Implementing the abstract function with an empty body for testing
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {}
}

contract WinnablesTicketManagerTest is Test, IWinnables {
    WinnablesTicketManager winnablesTicketManager;
    address mockLinkToken;
    address mockVrfCoordinator;
    uint64 subscriptionId = 1;
    bytes32 keyHash = "0xabcdef";
    address mockTicketsContract;
    address mockCCIPRouter;

    struct Any2EVMMessage {
        bytes32 messageId; // MessageId corresponding to ccipSend on source
        uint64 sourceChainSelector; // Source chain selector
        bytes sender; // abi.decode(sender) if coming from an EVM chain
        bytes data; // payload sent in original message
        EVMTokenAmount[] tokenAmounts; // Tokens and their amounts at destination
    }

    struct RaffleDetails {
        uint64 startsAt;
        uint64 endsAt;
        uint32 minTicketsThreshold;
        uint32 maxTicketSupply;
        uint32 maxHoldings;
        uint256 totalRaised;
        RaffleStatus status;
        uint256 chainlinkRequestId;
    }
    
    struct EVMTokenAmount {
        address token; // token address on local blockchain
        uint256 amount;
    }

    function setUp() public {
        mockLinkToken = address(new MockIERC20());
        mockVrfCoordinator = address(new MockVRFConsumerBaseV2(address(this)));
        mockTicketsContract = address(new MockIERC20());
        mockCCIPRouter = address(this);
        
        winnablesTicketManager = new WinnablesTicketManager(
            mockLinkToken, 
            mockVrfCoordinator, 
            subscriptionId,
            keyHash, 
            mockTicketsContract, 
            mockCCIPRouter
        );
    }

    function getAndLogRaffleDetails(uint256 raffleId) internal returns (uint64, uint64, uint32, uint32, uint32, uint256, RaffleStatus, uint256) {
    (uint64 startsOut, uint64 endsOut, uint32 minOut, uint32 maxOut, uint32 maxHoldOut, uint256 totalRaisedOut, RaffleStatus statusOut, uint256 chainlinkRequestIdOut) 
        = winnablesTicketManager.getRaffle(raffleId);

    console.log("Starts At: %s, Ends At: %s", startsOut, endsOut);
    console.log("Min Tickets: %s, Max Tickets: %s, Max Holdings: %s", minOut, maxOut, maxHoldOut);
    console.log("Total Raised: %s, Status: %s, Chainlink Request ID: %s", totalRaisedOut, uint(statusOut), chainlinkRequestIdOut);

    return (startsOut, endsOut, minOut, maxOut, maxHoldOut, totalRaisedOut, statusOut, chainlinkRequestIdOut);
}

    function testCreateRaffleStartTimeBeforeTimestamp() public {
    uint256 raffleId = 1;
    uint64 startsAt = 1;
    uint64 endsAt = startsAt + 1 days;
    uint32 minTickets = 10;
    uint32 maxTickets = 100;
    uint32 maxHoldings = 5;

    // Simulate the prize being locked via a CCIP message
    address senderAddress = address(this); // Simulate sender address
    uint64 chainSelector = 1; // Simulate a chain selector, typically the identifier for the chain
    address _senderAddress = senderAddress;    
    bytes memory ccipData = abi.encode(raffleId);

    // Encode and construct the Any2EVMMessage
    Client.EVMTokenAmount[] memory emptyTokenAmounts = new Client.EVMTokenAmount[](0);
    Client.Any2EVMMessage memory message = Client.Any2EVMMessage({
        messageId: keccak256(abi.encodePacked(block.timestamp)),
        sourceChainSelector: chainSelector,
        sender: abi.encode(_senderAddress),
        data: ccipData,
        destTokenAmounts: emptyTokenAmounts
    });

    winnablesTicketManager.setCCIPCounterpart(_senderAddress, chainSelector, true);

    vm.warp(800);

    vm.prank(_senderAddress); // Mock the caller to be the sender or router

    winnablesTicketManager.ccipReceive(message);
    
    winnablesTicketManager.createRaffle(raffleId, startsAt, endsAt, minTickets, maxTickets, maxHoldings);

    //uint256 timestamp = block.timestamp;
    console.log("current timestamp:  ", block.timestamp);

    // Check initial raffle state
    getAndLogRaffleDetails(raffleId);

    
}


}
```


Test Log

```solidity

[PASS] testCreateRaffleStartTimeBeforeTimestamp() (gas: 77073)
Logs:
  current timestamp:   800
  Starts At: 1, Ends At: 86401
  Min Tickets: 10, Max Tickets: 100, Max Holdings: 5
  Total Raised: 0, Status: 2, Chainlink Request ID: 0

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.61ms (372.70Âµs CPU time)

```

### Mitigation

To address the issue of the `_checkRaffleTimings` function not propagating updates to `startsAt` correctly, the following mitigation strategies can be employed:

1. **Update Function Signature and Return**: Modify the `_checkRaffleTimings` function to return the potentially updated `startsAt`, and update the `createRaffle` function to use this returned value. This ensures that any adjustment made to the start time within the `_checkRaffleTimings` function is effectively used when creating or updating the raffle.

    ```solidity
    function _checkRaffleTimings(uint64 startsAt, uint64 endsAt) internal view returns (uint64) {
        if (startsAt < block.timestamp) startsAt = uint64(block.timestamp);
        if (startsAt + MIN_RAFFLE_DURATION > endsAt) revert RaffleClosingTooSoon();
        return startsAt;
    }
    ```

    In `createRaffle`, adjust accordingly:
    ```solidity
    raffle.startsAt = _checkRaffleTimings(startsAt, endsAt);
    ```