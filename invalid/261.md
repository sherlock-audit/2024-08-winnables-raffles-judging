Abundant Leather Cow

High

# [High] - Reentrancy in the `claimPrize` function allows an attacker to drain funds, tokens, from the contract.

## Summary
The `WinnablesPrizeManager.sol::claimPrize` function is vulnerable to reentrancy attacks at various points in the code. An attacker can reenter the function before the raffle's claimed state is updated, leading to a reentrancy attack.

## Vulnerability Detail
The `claimPrize` function does not follow the Check-Effects-Interactions (CEI) pattern, as the status of the raffle is checked and updated after the interactions part of the function.
```solidity
if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
```
This creates an opportunity for an attacker to reenter the function during external calls within the function, such as in Ether transfers, token  transfers.
For example, during an Ether transfer, the attacker will reenter the function through the external call:-
```solidity
_sendETHPrize(_ethRaffles[raffleId], msg.sender);
```

## Impact

A reentrancy attack can result in the contract losing funds, tokens, and NFTs.
## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105
```solidity
function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked {
                _tokensLocked[raffle.tokenAddress] -= raffle.amount;
            }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked {
                _ethLocked -= _ethRaffles[raffleId];
            }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else {
            revert InvalidRaffle();
        }

       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
       emit PrizeClaimed(raffleId, msg.sender);
    }
```
## Proof of Concept

The raffle's claimed status is checked and updated at the end of the claimPrize function, creating an opportunity for an attacker to reenter the function and withdraw funds before the status is updated.

The attack goes like this for `raffleType == RaffleType.ETH`->
the `_ethLocked` variable stored all the ether that is locked by all the raffleIds in the contract to distribute as prize.
```solidity
_ethLocked -= _ethRaffles[raffleId];
```
Additionally, failing to reset the fund amount associated with a raffle ID after withdrawal can allow an attacker to repeatedly claim funds for the same raffle.
The winner of the raffle can withdraw the prize from the contract, potentially allowing them to reenter the function through an external call
```solidity
_sendETHPrize(_ethRaffles[raffleId], msg.sender);
```

Both the check for a claimed raffle and the update of the raffle's claimed status occur after the external call to transfer the prize, attacker can claim the prize multiple times.

```solidity
if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
```

## Tool used

Manual Review

## Recommendation
the following changes can be done to prevent the attack :-
1.
```diff
function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;

+      if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+        rafflePrize.status = RafflePrizeStatus.CLAIMED;

        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked {
                _tokensLocked[raffle.tokenAddress] -= raffle.amount;
            }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked {
                _ethLocked -= _ethRaffles[raffleId];
            }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else {
            revert InvalidRaffle();
        }

-       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-        rafflePrize.status = RafflePrizeStatus.CLAIMED;
       emit PrizeClaimed(raffleId, msg.sender);
    }
```

2. To prevent reentrancy attacks, incorporating OpenZeppelin's ReentrancyGuard contract and applying the nonReentrant modifier to the claimPrize function is recommended.

