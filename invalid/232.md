Brave Onyx Cuckoo

High

# Reentrancy in WinnablesPrizeManager::claimPrize, which allows the winner to claim the prize multiple times

### Summary

Reentrancy in WinnablesPrizeManager::claimPrize, which allows the winner to claim the prize multiple times

### Root Cause

In `WinnablesPrizeManager::claimPrize` function, the state variable `rafflePrize.status` is updated after the prize is sent, without following CEIï¼š
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L120-L122
```solidity
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED; // @audit: here
        emit PrizeClaimed(raffleId, msg.sender);
    }
```

In this case, the winner can use the reentrancy vulnerability to claim the reward multiple times.


### Internal pre-conditions

1. The attacker needs to use a malicious contract address to become the winner, so that he can perform a reentrancy attack after becoming the winner.

### External pre-conditions

None

### Attack Path

1. The attacker can use malicious contracts to participate in the raffle (the attacker can choose to use multiple malicious smart contracts to participate in the raffle to increase the success rate)
2. Once any malicious smart contract becomes the winner, a reentrancy attack can be launched, calling the WinnablesPrizeManager::claimPrize function multiple times, such as claiming ETH multiple times

### Impact

After the attacker becomes the winner, he can perform a reentrancy attack, calling the `WinnablesPrizeManager::claimPrize` function multiple times to transfer all the ETH in the WinnablesPrizeManager contract.

### Mitigation

```solidity
diff --git a/public-contracts/contracts/WinnablesPrizeManager.sol b/public-contracts/contracts/WinnablesPrizeManager.sol
index 72ba110..d9a6ad8 100644
--- a/public-contracts/contracts/WinnablesPrizeManager.sol
+++ b/public-contracts/contracts/WinnablesPrizeManager.sol
@@ -105,6 +105,10 @@ contract WinnablesPrizeManager is Roles, BaseCCIPSender, BaseCCIPReceiver, IWinn
     function claimPrize(uint256 raffleId) external {
         RafflePrize storage rafflePrize = _rafflePrize[raffleId];
         RaffleType raffleType = rafflePrize.raffleType;
+        
+        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+        rafflePrize.status = RafflePrizeStatus.CLAIMED;
         if (raffleType == RaffleType.NFT) {
             NFTInfo storage raffle = _nftRaffles[raffleId];
             _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
@@ -117,9 +121,7 @@ contract WinnablesPrizeManager is Roles, BaseCCIPSender, BaseCCIPReceiver, IWinn
             unchecked { _ethLocked -= _ethRaffles[raffleId]; }
             _sendETHPrize(_ethRaffles[raffleId], msg.sender);
         } else revert InvalidRaffle();
-        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-        rafflePrize.status = RafflePrizeStatus.CLAIMED;
+        
         emit PrizeClaimed(raffleId, msg.sender);
     }
```