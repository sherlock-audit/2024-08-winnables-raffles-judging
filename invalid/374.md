Best Cotton Rooster

Medium

# The raffle may be canceled due to the attacker's front running.

## Summary
In special cases, an attacker can use `cancelRaffle()` to cancel the raffle and stop selecting winner, thereby damaging the core functionality of the protocol.

## Vulnerability Detail
The `WinnablesTicketManager` contract contains the `cancelRaffle()` and the `drawWinner()`.
Users use the `cancelRaffle()` to cancel the Raffle corresponding to `raffleId` and use the `drawWinner()` to send a request for random number from Chainlink VRF.
```solidity
    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
```solidity
    function drawWinner(uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        _checkShouldDraw(raffleId);
        raffle.status = RaffleStatus.REQUESTED;

        uint256 requestId = VRFCoordinatorV2Interface(VRF_COORDINATOR).requestRandomWords(
            KEY_HASH,
            SUBSCRIPTION_ID,
            3,
            100_000,
            1
        );
        _chainlinkRequests[requestId] = RequestStatus({
            raffleId: raffleId,
            randomWord: 0
        });
        raffle.chainlinkRequestId = requestId;
        emit RequestSent(requestId, raffleId);
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
However, in order to execute the two functions above, the following conditions must be satisfied.
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
@       if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```
```solidity
    function _checkShouldDraw(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        uint256 currentTicketSold = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (currentTicketSold == 0) revert NoParticipants();

        if (block.timestamp < raffle.endsAt) {
            if (currentTicketSold < raffle.maxTicketSupply) revert RaffleIsStillOpen();
        }
@       if (currentTicketSold < raffle.minTicketsThreshold) revert TargetTicketsNotReached();
    }
```
As you can see, when `supply` is equal to `raffle.minTicketSupply`, there can be execute both the `cancelRaffle()` and the `drawWinner()`.
As a result, the raffle is canceled if the attacker executes the `cancelRaffle()` before the the `drawWinner()`function is executed.

Therefore, a raffle may be canceled even though it is a valid raffle sufficient to select a winner and distribute the prize.

## Impact
A raffle may be canceled even though it is a valid raffle sufficient to select a winner and distribute the prize. Therefore participants do not receive prize.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278-L288
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310-L329
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434-L441
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L422-L432

## Tool used

Manual Review

## Recommendation
The `_checkShouldCancel()` function has to be modified as follow.
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
---     if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
+++     require(supply < raffle.minTicketsThreshold, "supply is bigger than minTicketsThreshold");
    }
```