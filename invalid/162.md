Polite Ultraviolet Anteater

High

# Reentrancy allows prize winner of a single raffle to steal ethLocked from other raffles

## Summary

Reentrancy vulnerability wheneve a winner claims an ETH prize. 

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L118

## Vulnerability Detail

In `WinnablesPrizeManager.sol` once the winner is picked for a raffle they can claim their prize by calling the external function `WinnablesPrizeManager::claimPrize`:

```solidity
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; } 
            _sendETHPrize(_ethRaffles[raffleId], msg.sender); // audit-high - reentrancy - winner can claim prize multiple times and drain the eth 
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```

When the price is in ETH (`RaffleType.ETH`) then the function `WinnablesPrizeManager::_sendETHPrize` is called which transfers ETH to the rightful winner of the raffle:

`WinnablesPrizeManager::_sendETHPrize`:
```solidity
    /// @dev Transfers the NFT prize to the winner
    /// @param amount Amount of ETH to send
    /// @param winner Address of the winner
    function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```

However, if the rightful owner is a contract which has a receive/fallback function which calls back the `WinnablesPrizeManager::_sendETHPrize` then they will be able to retrieve again the prize - Reentrancy.

The reason for this vulnerability is NOT following the CEI (Check-Effects-Interactions) pattern.

## Impact

Impact: High
Likelihood: High

## Proof of Concept

1. Raffle winner is picked.
2. Raffle winner is actually a contract which calls the `WinnablesPrizeManager::claimPrize` to claim their reward of ETH.
3. Once the logic inside `WinnablesPrizeManager::_sendETHPrize` is executed which uses a low level call:
```solidity
    (bool success, ) = winner.call{ value: amount }("");
```
Then the Raffle winner's receive/fallback function is hit, which calls back the `WinnablesPrizeManager::claimPrize` function for the same raffle.
4. Attacker drains the `_ethLocked` which contains ETH for other raffles as well.

## Tool used
Manual Review

## Recommendation

To prevent this reentrancy follow the CEI (Check-Effects-Interactions) pattern and move the Check-Effects part above the interactions:

```diff
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
+       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; } 
            _sendETHPrize(_ethRaffles[raffleId], msg.sender); // audit-high - reentrancy - winner can claim prize multiple times and drain the eth 
        } else revert InvalidRaffle();
-       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
