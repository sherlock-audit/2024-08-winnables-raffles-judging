Faithful Cornflower Cod

High

# Anyone can break a raffle by canceling it or propagating a winner with wrong parameters

### Summary

The function `WinnablesTicketManager::propagateRaffleWinner` lets anyone propagate the winner of a raffle.
This function sends a CCIP message to the remote chain and marks the raffle as `FULFILLED`.
This is intended to inform `WinnablesPrizeManager`, on the remote chain, who won a raffle.

However, the input parameters are not checked. If an attacker sets either `prizeManager` or `chainSelector` to values that do not match the official remote message receiver, the remote will never know the winner of a raffle and thus will never allow them to withdraw their prize.

The same issue happens in the function `WinnablesTicketManager::cancelRaffle`.

### Root Cause

In `WinnablesTicketManager::propagateRaffleWinner` [(`WinnablesTicketManager.sol:334`)](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334) and `WinnablesTicketManager::cancelRaffle` [(`WinnablesTicketManager.sol:278`)](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278), there's a missing check for correctness of `prizeManager` and `chainSelector` parameters.

For clarity, below is one of the affected functions. It can be called with arbitrary, incorrect values for prizeManager and chainSelector, breaking the raffle forever and locking funds on the remote.

```solidity

    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }

```

### Internal pre-conditions

1. The admin creates a raffle.
2. People join.
3. Someone calls `drawWinner` to pick a winner.
4. Someone calls `propagateRaffleWinner` with bogus `prizeManager` address or `chainSelector`.

OR

1. The admin creates a raffle.
2. People join.
3. Someone cancels the raffle as special conditions are met, but uses bogus `prizeManager` address or `chainSelector`.

### External pre-conditions

1. Chainlinks CCIP works as intended, delivering a message (to a bogus address or chain).

### Attack Path

1. After a raffle has been created and the winner is decided, anyone calls `propagateRaffleWinner` with bogus `prizeManager` address or `chainSelector`.

OR

1. After a raffle has been created and not enough tickets have been sold, anyone calls `cancelRaffle` with bogus `prizeManager` address or `chainSelector`.

### Impact

**Impact: funds get locked forever in WinnablesPrizeManager**.

The remote chain contract which stores ETH, tokens or NFTs for each raffle never receives a message declaring a winner for them or canceling the raffle and unlocking the prize. Which means these funds will be forever stuck in the `WinnablesPrizeManager` contract, preventing the winner for claiming their rightful prize.

It's important to note that not even the admins are able to recover the funds, since the admin withdrawal functions will check for locks.

### PoC

In the included Hardhat tests, we can include in test/TicketManager.js:786:

```javascript
    it('Propagate to random chain and address', async () => {
      const { events } = await (await manager.propagateRaffleWinner(counterpartContractAddress, 99, 1)).wait();
      expect(events).to.have.lengthOf(3);
      const ccipEvent = ccipRouter.interface.parseLog(events[0]);
      expect(ccipEvent.args.chain).to.eq(99);
      expect(ccipEvent.args.receiver).to.eq('0x' + counterpartContractAddress.toLowerCase().slice(-40).padStart(64, '0'));
      expect(ccipEvent.args.data).to.have.lengthOf(108);
      const drawnWinner = ethers.utils.getAddress('0x' + ccipEvent.args.data.slice(-40));
      expect(buyers.find(b => b.address === drawnWinner)).to.not.be.undefined;
      expect(ccipEvent.args.data.slice(0, 68)).to.eq('0x010000000000000000000000000000000000000000000000000000000000000001');
    });

```

Here, we see that we are successfully propagating the winner of raffle 1 with chainSelector == 99, as opposed to the correct chainSelector == 1.

Doing that, the callback at `WinnablesPrizeManager::_ccipReceive` never runs and the funds locked in that raffle are forever gone.

### Mitigation

To prevent this issue, don't allow a user to input whatever he wants in `WinnablesTicketManager::propagateRaffleWinner` and `WinnablesTicketManager::cancelRaffle`.

Instead, remove these parameters from the aforementioned functions and use private variables initialized in the constructor to store `prizeManager` and `chainSelector`.

For example, in WinnablesTicketManager.sol:

```solidity

...
    address private PRIZE_MANAGER;
    uint64 private PRIZE_MANAGER_CHAIN_SELECTOR;
         
    constructor(
        address _linkToken,
        address _vrfCoordinator,
        uint64 _subscriptionId,
        bytes32 _keyHash,
        address _tickets,
        address _ccipRouter,
        address _prizeManager,
        uint64 _prizeManagerChainSelector,
    ) VRFConsumerBaseV2(_vrfCoordinator) BaseCCIPContract(_ccipRouter) BaseLinkConsumer(_linkToken) {
        VRF_COORDINATOR = _vrfCoordinator;
        SUBSCRIPTION_ID = _subscriptionId;
        KEY_HASH = _keyHash;
        TICKETS_CONTRACT = _tickets;
        _setRole(msg.sender, 0, true); // Deployer is admin by default
        LinkTokenInterface(LINK_TOKEN).approve(_ccipRouter, type(uint256).max);

        PRIZE_MANAGER = _prizeManager;
        PRIZE_MANAGER_CHAIN_SELECTOR = _prizeManagerChainSelector,
    }

```
In WinnablesTickerManager::cancelRaffle: 

```solidity

    function cancelRaffle(uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            PRIZE_MANAGER,
            PRIZE_MANAGER_CHAIN_SELECTOR,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```

In WinnablesTickerManager::propagateRaffleWinner: 
```solidity


    function propagateRaffleWinner(uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(PRIZE_MANAGER, PRIZE_MANAGER_CHAIN_SELECTOR, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }

```

We should note that WinnablesPrizeManager.sol also sends CCIP messages for locking tokens, ETH and NFTs. Although these calls are protected by admin access, they also allow arbitrary addresses and chain selectors, meaning admins may make mistakes that break the protocol.

I recommend to adopt the same strategy and store these values in contract variables.