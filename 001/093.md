Recumbent Ultraviolet Ram

High

# Unupdated `_lockedETH` might prevent the admin from withdrawing funds due to the potential underflow

## Summary
Not updating `_lockedETH` in WinnablesTicketManager.sol::refundPlayers() may cause potentially an underflow, which will result in unwithdrawable ETH.
## Vulnerability Detail
[_lockedETH](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L42) is created for potential refunds . Each time a ticket is bought by the user, it is increased in buyTicket():
```solidity
 function buyTickets(
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        bytes calldata signature
    ) external payable {
        if (ticketCount == 0) revert InvalidTicketCount();
        _checkTicketPurchaseable(raffleId, ticketCount);
        _checkPurchaseSig(raffleId, ticketCount, blockNumber, signature);

        Raffle storage raffle = _raffles[raffleId];
        uint256 participation = uint256(raffle.participations[msg.sender]);
        uint128 totalPaid = uint128(participation) + uint128(msg.value);
        uint32 totalPurchased = uint32(participation >> 128) + uint32(ticketCount);
        unchecked {
            raffle.participations[msg.sender] = bytes32(
                (participation & type(uint256).max << 160)
                | totalPaid |
                uint256(totalPurchased) << 128
            );
        }
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
@>            _lockedETH += msg.value;
        }
        IWinnablesTicket(TICKETS_CONTRACT).mint(msg.sender, raffleId, ticketCount);
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
In case the requirements are not met for a raffle, the raffle is cancelled and the users are refunded by refundPlayers():
```solidity
 function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```
here as you can see there is no update of `_lockedETH`, `amountToSend` will be deducted from the contract's balance but `_lockedETH` will stay same. The issue arises when the admin calls [withdrawETH](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L300):
```solidity
 /// @notice (Admin) Withdraw ETH from a canceled raffle or ticket sales
    function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
            balance = address(this).balance - _lockedETH;
        } //@audit-issue that will result in underflow in case of a raffle's cancel 
        _sendETH(balance, msg.sender);
    }
```
The line
 ```solidity
unchecked {
            balance = address(this).balance - _lockedETH;
        }
```
will cause an underflow, which means the admin will not able to withdraw the funds. Here  are 2 example scenarios:
**First**:
1. The contract is deployed and Initial State would be like : 
Contract Balance: 0 ETH
_lockedETH: 0 ETH
2. The admin creates a raffle by calling `createRaffle()`. 
3. Users buy tickets by calling `buyTicket()`,(_lockedETH will be increased by the amount of ETH sent by the users). The minThreshold is not met and the raffle is cancelled. Let's say at this point,  `_lockedETH` is 2 ETH,  and `contract's balance` will be 2 ETH (assuming no other transfer has  been made yet)
4. The users are refunded by `refundPlayers()`. Since no decrease of `_lockedETH` is made,
Contract Balance: 0 ETH (refunded to users)
_lockedETH: 2 ETH (still reflecting the initial amount collected)
5. The Admin calls `withdrawsETH()`:
`balance = address(this).balance - _lockedETH;` will be `balance = 0 - 2 = -2 ETH` , which will lead to  underflow, The underflow will result in a large positive number. The contract will attempt to send a large amount of ETH that it doesn't actually have, leading to potential issues.
You might say well the contract balance is 0 , so no impact really. However  that may not be the case always. Consider the following case.
**Second Scenario:**
After the contract is deployed, many raffles are successfully done but assume 5 of them are cancelled. With each cancellation, there will refund. However since `_lockedETH` is not decreased in `refundPlayers()`, it will hold the amount of Eth sent for cancelled raffles (*Recall _lockedEth is increased by the Eth done by each user in buyTicket()*). Let's assume `_lockedETH` at this point is 10 ETH. In the meantime intentionally or unintentionally some ETH has been  transfered to the contract. Let's say `contract Balance `at  this point is 5 ETH. The admin decided to withdraw the ETH. Due to underflow, the funds will not be withdrawable. It will be stuck in the contract.
NOTE: The numbers used for clarification, There might be worse cases
## Impact
The funds will be stuck in the contract. Even the admin will not able to withdraw them

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L206
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L300
## Tool used
Manual Review, Vs Code
## Recommendation
Decrease `_lockedETH` by the `amountToSend` in [refundPlayer()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L215). Add the following line:
```solidity
    _lockedETH -= amountToSend;  // Decrease _lockedETH
```