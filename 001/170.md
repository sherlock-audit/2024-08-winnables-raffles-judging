Silly Burgundy Scallop

High

# _packCCIPContract() always return only the contractAddress

## Summary

Wrong bit shift will cause all counterpart CCIP results to contain only destination address.

## Vulnerability Detail

A CCIP counterpart must be configured for both contracts `WinnablesPrizeManager.sol` and `WinnablesTicketManager.sol` in order when `_ccipReceive()` is called to validate the call.

```solidity
function _ccipReceive(
    Client.Any2EVMMessage memory message
) internal override {
    (address _senderAddress) = abi.decode(message.sender, (address));
    bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
    if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();   <--------------------------------
    (uint256 raffleId) = abi.decode(message.data, (uint256));
    if (_raffles[raffleId].status != RaffleStatus.NONE) {
        // The raffle cannot be created, send back a cancel message to unlock the prize
        _sendCCIPMessage(
            _senderAddress,
            message.sourceChainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        return;
    }
    _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

    emit RafflePrizeLocked(
        message.messageId,
        message.sourceChainSelector,
        raffleId
    );
}
```

But `_packCCIPContract()` creates the pack incorrectly and will always skip the selector and always return only the contract address of the other chain.

Here are the calculation steps:

- The `chainSelector`, which is a `uint64` (64-bit value), is first shifted left by 160 bits.
- Since `chainSelector` is only 64 bits wide, shifting it left by 160 bits essentially shifts all of its bits out of the 64-bit range, resulting in `0`.
- This `0` is then cast to `uint256`, but since it's already `0`, the cast doesn't change anything.

```solidity
function _packCCIPContract(address contractAddress, uint64 chainSelector) public pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
        uint256(chainSelector << 160)
    );
}
```

Result of these example values will be only the address: 

- contractAddress = `0x4859614cBE8bbe9cCAd991Cc69394343943CD52D`
- chainSelector = `16015286601757825753`
- result - `0x0000000000000000000000004859614cbe8bbe9ccad991cc69394343943cd52d`

## Impact

`pack` of the destination address and selector will always be wrong, this will cause all messages to pass no matter which `Client.Any2EVMMessage.sourceChainSelector` is passed.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPContract.sol#L43

## Tool used

Manual Review

## Recommendation

The team wants the `pack` to be in this format:

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPContract.sol#L13-L14

```solidity
/// [0..159]    address sourceContract
/// [160..223]  uint64  sourceChainSelector
```

To achieve this, the `sourceChainSelector` must first be made 256 bits and then shifted.

```diff
function _packCCIPContract(address contractAddress, uint64 chainSelector) public pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
-       uint256(chainSelector << 160)
+       uint256(chainSelector) << 160
    );
}
```