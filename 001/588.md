Breezy Amethyst Penguin

High

# Executing `uint256(chainSelector << 160)` in `BaseCCIPContract._packCCIPContract` function equals 0 mistakenly, which allows unauthorized CCIP sender corresponding to unauthorized `message.sourceChainSelector` to call `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions to change corresponding raffle's state unexpectedly, such as setting such raffle's winner to someone controlled by such unauthorized CCIP sender

### Summary

Because executing `uint256(chainSelector << 160)` in the `BaseCCIPContract._packCCIPContract` function is a code mistake that would equal 0, an unauthorized CCIP sender corresponding to an unauthorized `message.sourceChainSelector` can call the `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions to change the corresponding raffle's state unexpectedly, such as setting such raffle's winner to someone controlled by such unauthorized CCIP sender.

### Root Cause

Executing `uint256(chainSelector << 160)` in the `BaseCCIPContract._packCCIPContract` function is a code mistake that would equal 0 because the `chainSelector` input is `uint64`.

### Internal pre-conditions

Not applicable

### External pre-conditions

1. A CCIP sender's address on an unauthorized source chain is the same as the authorized counterpart contract's address on the authorized source chain.

### Attack Path

1. This protocol's admin does not call the `WinnablesPrizeManager.setCCIPCounterpart` or `WinnablesTicketManager.setCCIPCounterpart` function, which further calls the `BaseCCIPContract._setCCIPCounterpart` function, to approve such CCIP sender that is on the unauthorized source chain so such CCIP sender is unauthorized.
2. Such unauthorized CCIP sender calls the `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions for unexpectedly changing the corresponding raffle's state.
3. In the `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions, the `_ccipContracts[counterpart]` for such unauthorized CCIP sender would equal true because the corresponding `_ccipContracts[counterpart]` stored by the `BaseCCIPContract._setCCIPCounterpart` function, which calls `BaseCCIPContract._packCCIPContract` function, only includes the bit mapping for the counterpart contract address and does not correctly contain the bit mapping for the authorized CCIP Chain selector. Thus, such unauthorized CCIP sender's `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` function calls do not revert.
4. Such unauthorized CCIP sender can change the corresponding raffle's state unexpectedly, such as setting such raffle's winner to an address controlled by it.

### Impact

The corresponding raffle's state can be changed unexpectedly, such as that its winner can be set by the unauthorized CCIP sender.

### PoC

The `BaseCCIPContract._setCCIPCounterpart` function calls the `BaseCCIPContract._packCCIPContract` function for creating a `bytes32` key that should contain the `[160..223]` bit mapping for the authorized CCIP Chain selector according to `_ccipContracts`'s comment.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L31-L38
```solidity
    function _setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) internal {
        bytes32 counterpart = _packCCIPContract(contractAddress, chainSelector);
@>      _ccipContracts[counterpart] = enabled;
    }
```

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L40-L45
```solidity
    function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
@>          uint256(chainSelector << 160)
        );
    }
```

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L11-L15
```solidity
    /// @dev Linked CCIP contracts
    /// The mapping key is a packed bytes32 with the following bit mapping
    /// [0..159]    address sourceContract
@>  /// [160..223]  uint64  sourceChainSelector
    mapping(bytes32 => bool) internal _ccipContracts;
```

However, executing `uint256(chainSelector << 160)` in the `BaseCCIPContract._packCCIPContract` function is a code mistake that would equal 0 since the `chainSelector` input is `uint64`. The following `test_packCCIPContract` test can be executed and pass using Foundry Forge to prove this.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";

contract POC is Test {
    // The following _packCCIPContract function is the current _packCCIPContract function from
    //   https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L40-L45.
    function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
        return bytes32(
            uint256(uint160(contractAddress)) |
            uint256(chainSelector << 160)
        );
    }

    function test_packCCIPContract() public {
        address contractAddress = 0x80226fc0Ee2b096224EeAc085Bb9a8cba1146f7D;

        bytes32 key1 = _packCCIPContract(contractAddress, 5009297550715157269);
        bytes32 key2 = _packCCIPContract(contractAddress, 14767482510784806043);
        bytes32 key3 = _packCCIPContract(contractAddress, 16015286601757825753);
        bytes32 key4 = _packCCIPContract(contractAddress, 10000000000000000000);

        // For the same address of counterpart contract on the source chain,
        //   the current _packCCIPContract function returns the same bytes32 key for different CCIP Chain selectors of the source chain.
        // This means that the key returned by the current _packCCIPContract function does not correctly store the CCIP Chain selector of the source chain.
        assertEq(key1, key2);
        assertEq(key1, key3);
        assertEq(key1, key4);

        // Executing uint256(chainSelector << 160) in the current _packCCIPContract function is a code mistake that equals 0.
        assertEq(key1 >> 160, 0);
        assertEq(key2 >> 160, 0);
        assertEq(key3 >> 160, 0);
        assertEq(key4 >> 160, 0);
    }
}
```

When `_ccipContracts` does not correctly include the bit mapping for the authorized CCIP Chain selector, `if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender()` in the `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions become ineffective in which the `_ccipContracts[counterpart]` would equal true for an unauthorized CCIP sender on an unauthorized source chain if it has the same address as an authorized counterpart contract on the authorized source chain. In this case, an unauthorized CCIP sender corresponding to an unauthorized `message.sourceChainSelector` can call these `WinnablesPrizeManager._ccipReceive` and `WinnablesTicketManager._ccipReceive` functions to change the corresponding raffle's state unexpectedly, such as setting such raffle's winner to someone controlled by it.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L278
```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
@>      bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
@>      if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
        uint256 raffleId;
        address winner;
        if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
            raffleId = _decodeRaffleCanceledMessage(message.data);
            _cancelRaffle(raffleId);
            return;
        }
        (raffleId, winner) = _decodeWinnerDrawnMessage(message.data);
@>      _rafflePrize[raffleId].winner = winner;
        emit WinnerPropagated(raffleId, winner);
    }
```

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L365-L388
```solidity
    function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
@>      bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
@>      if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
        (uint256 raffleId) = abi.decode(message.data, (uint256));
        if (_raffles[raffleId].status != RaffleStatus.NONE) {
            // The raffle cannot be created, send back a cancel message to unlock the prize
            _sendCCIPMessage(
                _senderAddress,
                message.sourceChainSelector,
                abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
            );
            return;
        }
        _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

        emit RafflePrizeLocked(
            message.messageId,
            message.sourceChainSelector,
            raffleId
        );
    }
```

### Mitigation

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L43 can be updated to the following code.

```solidity
            uint256(chainSelector) << 160
```