Magic Green Raccoon

High

# Reentrancy in the claimPrize function will drain funds in the prize pool

## Summary
All funds in the `WinnablePrizeManager` contract are vulnerable to theft due to a re-entrancy issue in the `claimPrize` function.

## Vulnerability Detail

The `claimPrize` function in the `WinnablePrizeManager` contract updates a raffle's status to `CLAIMED` after distributing the rewards. If the raffle type is `ETH`, the function calls the `_sendETHPrize` function, which sends `ETH` using a low-level-call. A malicious contract could exploit this by re-entering the `claimPrize` function through its fallback function after receiving the `ETH`. Since the raffle status is not updated in time, the reentrancy will result in the theft of all funds in the `WinnablePrizeManager` contract.

## Impact
All funds can be drained in the `WinablePrizeManager` contract. 

## Code Snippet

Vulnerable codes ref: https://github.com/Winnables/public-contracts/blob/267dd06e2d5636f30e3311c46ed24b76ac6614c5/contracts/WinnablesPrizeManager.sol#L105


```solidity
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            // @audit malicious contract could ennter the function here (after the low level call)
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        // @audit root cause: the status of the raffle has not been updated in time
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```

```solidity
    function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```

## Tool used

Manual Review.

## Recommendation

Add a re-entrancy guard to the claimPrize function or apply the Checks-Effects-Interactions principle.