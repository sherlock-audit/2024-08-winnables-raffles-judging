Merry Sepia Whale

High

# Attacker can lock up canceled raffle's prize in the Prize Manager

## Summary
An attacker can lockup the prize of a canceled raffle in the Prize Manager even though the raffle has now refunded all users who participated in it by inputting wrong `prizeManager` or `chainSelector` in the `cancelRaffle` function of the `WinnablesTicketManager` contract.

## Vulnerability Detail
```solidity
// @audit attacker can cause loss of locked fund in prize manager
function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```

```solidity
function _ccipReceive( // @audit un-executed for the raffleId the contract on Avalanche intended to propagate to
        Client.Any2EVMMessage memory message
    ) internal override {
        ....

 @>       if (messageType == CCIPMessageType.RAFFLE_CANCELED) { // @audit will not happen and the locked funds will remain locked
            raffleId = _decodeRaffleCanceledMessage(message.data);
            _cancelRaffle(raffleId);
            return;
        }
        ...
    }
```

```solidity
function _cancelRaffle(uint256 raffleId) internal {
        RaffleType raffleType = _rafflePrize[raffleId].raffleType;

        if (_rafflePrize[raffleId].status == RafflePrizeStatus.CANCELED) revert InvalidRaffle();

        if (raffleType == RaffleType.NFT) {
            ...
        } else if (raffleType == RaffleType.TOKEN) {
            ...
        } else if (raffleType == RaffleType.ETH) {
@>            unchecked { _ethLocked -= _ethRaffles[raffleId]; } // @audit e.g raffleId 2 locked price cannot be unlocked or recovered
        } else revert InvalidRaffle();
        
        _rafflePrize[raffleId].status = RafflePrizeStatus.CANCELED;
        emit PrizeUnlocked(raffleId);
    }
```

```solidity
function withdrawETH(uint256 amount) external onlyRole(0) {
        uint256 balance = address(this).balance;
        uint256 availableBalance;
@>   unchecked { availableBalance = balance - _ethLocked; } // @audit remains locked
        if (availableBalance < amount) revert InsufficientBalance();
        (bool success,) = msg.sender.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```

Attack steps: 

1. Suppose `prizeManager` on Ethereum chain is address `0x1edA3ff69FC5E50CEBECe4946BE741825E443D21`
2. And suppose `chainSelector` on Ethereum chain is `16015286601757825753`
3. Also, the address of the `WinnablesTicketManager` contract on Avalanche is `0x2e521497bf560762c24193BCE88F859aedfB366a`
4. The proper arguments that guarantee delivery of the message propagated from Avalance to Ethereum mainnet is to set `prizeManager =  0x1edA3ff69FC5E50CEBECe4946BE741825E443D21`, `chainSelector = 16015286601757825753`.
5. What the attacker does instead is specify the  `WinnablesTicketManager` address as the address to propagate to e.g `prizeManager = 0x2e521497bf560762c24193BCE88F859aedfB366a`, `chainSelector = 16015286601757825753`
6. Or they could just set the `chainSelector` to another chain's selector e.g one on Base `10344971235874465080` while setting the `prizeManager =  0x1edA3ff69FC5E50CEBECe4946BE741825E443D21` as the correct one on Ethereum
7. What happens is that the message will be sent but it will not be delivered on Ethereum because the chainSelector does not match or the address the attacker specified as the `raffleManager` does not implement the `_ccipReceive` function on Ethereum mainnet and not the right `prizeManager` contract
8. Hence, the raffle's prize will remain locked in the prize manager contract on Ethereum because the ticket manager contract on Avalanche sent an undelivered message and already marked the state for the raffle as `CANCELED` which cannot be re-initiated/canceled.

## Impact
Loss of the canceled raffle's prize in the `WinnablesPrizeManager` contract on Ethereum mainnet.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278-L288
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L270-L274
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L290

## Tool used
Manual Review

## Recommendation
Whitelist the allowed `prizeManager` and `chainSelector` values and only send the messages using those parameters getting rid of the arguments in the `cancelRaffle` function e.g:

```diff
+ address public prizeManager;
+ uint64 chainSelector;

+ function setMessageParams(address _prizeManager, uint64 _chainSelector) external onlyRole(0) {
+ prizeManager = _prizeManager;
+ chainSelector = _chainSelector;
+ }

+ function cancelRaffle(uint256 raffleId) external {
- function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```