Rapid Marmalade Seal

Medium

# Inconsistency in validations of `_checkShouldDraw()` and `_checkShouldCancel()` functions of `WinnablesTicketManager` contract

### Summary

The validations of the [`_checkShouldDraw()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L422-L432) and [`_checkShouldCancel()`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434-L441) functions in the `WinnablesTicketManager` contract are inconsistent, which could allow a malicious user to cancel a raffle and interrupt selecting a winner.
This means that the 'drawWinner()' function, a core function of the protocol, may be broken for valid raffles.

### Root Cause

Both of these functions are triggered when `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`. This allows the `drawWinner()` function to be reverted by front-running the `cancelRaffle()` function before the `drawWinner()` function is executed.

### Internal pre-conditions

1. `block.timestamp >= raffle.endsAt`
2. `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`

### External pre-conditions

1. The `cancelRaffle()` function must be front-running before the `drawWinner()` function is executed.

### Attack Path

1. When `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`, the malicious user executes the `cancelRaffle()` function before `drawWinner`function is executed.
2. In the `cancelRaffle()` function, `_raffles[raffleId].status` is set to `RaffleStatus.CANCELED`.
3. When the `drawWinner()` function is executed, the transaction fails because `raffle.status != RaffleStatus.IDLE` in the `_checkShouldDraw()` function.

### Impact

Malicious users can cancel valid raffles and interrupt selecting a winner.

### PoC

The `_checkShouldDraw()` function checks whether the raffle in question is a valid raffle that can select a winner in the `drawWinner()` function.
```solidity
    function _checkShouldDraw(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        uint256 currentTicketSold = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (currentTicketSold == 0) revert NoParticipants();

        if (block.timestamp < raffle.endsAt) {
            if (currentTicketSold < raffle.maxTicketSupply) revert RaffleIsStillOpen();
        }
-->     if (currentTicketSold < raffle.minTicketsThreshold) revert TargetTicketsNotReached();
    }
```

Meanwhile, the `_checkShouldCancel()` function checks whether the raffle can be canceled in the `cancelRaffle()` function.
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
-->     if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
-->     if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```
As you can see in the code snippet provided on the above, these two functions are both triggered when `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`.

As a result, a raffle may be canceled even though it is a valid raffle sufficient to select a winner and sned the prize to the winner.

This means that the core functionality of the protocol may be damaged when `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`.

### Mitigation

It is recommended to modify the `_checkShouldCancel()` function as follows:
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
---     if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
+++     if (supply >= raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```