Keen Scarlet Loris

High

# A malicious raffle winner can steal most ETH from the contract

## Summary
If a raffle's prize is ETH, a malicious winner can re-enter into the `claimPrize` function and steal most ETH from the contract.

## Vulnerability Detail
In WinnablesPrizeManager.sol, `claimPrize()` function is used to receive the prize from a raffle, for example, ETH prize. However this function lacks a reentrancy guard, so a malicious winner of an ETH raffle can steal ETH from the protocol if they use a contract and re-enter into the same function until most ETH is transferred to them.
```solidity
function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
@>          _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
```solidity
function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```

## Impact
The protocol could get drained of ETH by a malicious raffle winner

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105-L124
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L314-L317

## Tool used

Manual Review

## Recommendation
Implement a reentrancy guard on the `claimPrize()` function and/or rewrite the code so it follows CEI pattern.