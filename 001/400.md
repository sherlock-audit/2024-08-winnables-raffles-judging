Fancy Blonde Mustang

High

# Problem with setting ccip contract

## Summary

The `BaseCCIPContract` includes functionality for whitelisting a tuple of `contractAddress` and `chainSelector`. However, the underlying function that combines these two values, `BaseCCIPContract::_packCCIPContract`, contains a flaw that causes it to malfunction.

## Vulnerability Detail

The issue lies in the current implementation of `BaseCCIPContract::_packCCIPContract`.

### Problem:
The `chainSelector`, defined as a `uint64`, is shifted left by 160 bits. However, since `chainSelector` is only 64 bits, this operation shifts all of its bits out of range, resulting in a value of `0`. Consequently, the expression `uint256(chainSelector << 160)` always evaluates to `0`.

### Outcome:
As a result, the final output of the function is determined solely by the `contractAddress`, ignoring the `chainSelector`. This means that different `chainSelector` values produce the same output when combined with the same `contractAddress`.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L31-L45

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L260-L277

## Impact

A malicious user with a whitelisted address on one chain could exploit this vulnerability by bypassing security checks in the `WinnablePrizeManager::_ccipReceive` function. This would allow them to draw a winner for a raffle, potentially enabling them to steal rewards. A malicious user which address is whitelisted but on another chain could send ccip message and bypass the check, and select itself as winner and withdraw all of the rewards.

## Code Snippet

```solidity
function _setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) internal {
        bytes32 counterpart = _packCCIPContract(contractAddress, chainSelector);
        _ccipContracts[counterpart] = enabled;
    }

    function _packCCIPContract(
        address contractAddress,
        uint64 chainSelector
    ) internal pure returns (bytes32) {
        return
            bytes32(
                uint256(uint160(contractAddress)) |
                    uint256(chainSelector << 160)
            );
    }
```

## Tool Used

Manual Review

## Recommendation

To fix this issue, modify the function as follows:

```diff
function _packCCIPContract(
    address contractAddress,
    uint64 chainSelector
) internal pure returns (bytes32) {
    return
        bytes32(
            uint256(uint160(contractAddress)) |
+               (uint256(chainSelector) << 160)
-                uint256(chainSelector << 160)
        );
}
```

This change ensures that the `chainSelector` is properly shifted within a `uint256` context, allowing the function to generate a unique `bytes32` value for each combination of `contractAddress` and `chainSelector`.