Tart Clay Hippo

High

# WinnablesTicketManager.sol#refundPlayers - `_lockedETH` is not decreased leading to stuck of funds

## Summary
`_lockedETH` is not decreased inside `refundPlayers`, leading to a stuck of funds.

## Vulnerability Detail
`_lockedETH` is increased when users buy tickets because it holds all the ETH that users had paid to buy tickets for a raffle.
```js
    function buyTickets(
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        bytes calldata signature
    ) external payable {
        if (ticketCount == 0) revert InvalidTicketCount();
        _checkTicketPurchaseable(raffleId, ticketCount);
        _checkPurchaseSig(raffleId, ticketCount, blockNumber, signature);


        Raffle storage raffle = _raffles[raffleId];
        uint256 participation = uint256(raffle.participations[msg.sender]);
        uint128 totalPaid = uint128(participation) + uint128(msg.value);
        uint32 totalPurchased = uint32(participation >> 128) + uint32(ticketCount);
        unchecked {
            raffle.participations[msg.sender] = bytes32(
                (participation & type(uint256).max << 160)
                | totalPaid |
                uint256(totalPurchased) << 128
            );
        }
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
>>          _lockedETH += msg.value;
        }
        IWinnablesTicket(TICKETS_CONTRACT).mint(msg.sender, raffleId, ticketCount);
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```
Later, when the raffle ends, and the winner is chosen, that ETH is "unlocked" by decreasing the `_lockedETH`. 
```js
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(
            prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
>>            _lockedETH -= raffle.totalRaised;
        }
    }
```
Then the admin can withdraw the ETH from the ended raffles.
```js
    function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
            balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }
```
This is done so that the admin cannot withdraw ETH if the raffle has not ended. The problem is that the raffle can be canceled if not enough tickets are sold. If this happens, users will call `refundPlayers` to get their ETH back. However, when ETH is refunded to the users, `_lockedETH` is not decreased. This causes wrong accounting of the `locked ETH` and later when the admin wants to withdraw, the ETH they can withdraw will be less than what they can. 

## Impact
Stuck of funds since `locked ETH` will be more than the actual locked ETH in the raffles.

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L215-L228

## Tool used

Manual Review

## Recommendation
```diff
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
++          _lockedETH -= amountToSend;
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```