Spare Parchment Millipede

Medium

# `_packCCIPContract` in `BaseCCIPContract` ignores `chainSelector` leading to potential clash in counterpart contract states

## Summary
The `BaseCCIPContract` has `mapping(bytes32 => bool) internal _ccipContracts` to store the Linked CCIP Contract addresses. Thus, the information regarding both `contractAddress` and `chainSelector` needs to be stored.

However, the way it is implemented completely ignores the `chainSelector` of the contract. Thus, if any of the counterparts is deployed on two chains with the same address, enabling or disabling one contract will lead to doing the same for the other. 

As confirmed by the sponsors below, the protocol is flexible on deploying `WinnablesPrizeManager` on different chains; this issue has clearly become a reality.

![Screenshot 2024-08-20 at 4 01 25 PM](https://github.com/user-attachments/assets/8f0c1f95-a7a7-4a0c-984c-d8dc1d889a2b)


## Vulnerability Detail
In the implementation of `_packCCIPContract`, the code is taking `or` between
 ` uint256(uint160(contractAddress)) | uint256(chainSelector << 160)` which is incorrect. 

Given that `chainSelector` is `uint64`, it first needs to be typecasted to `uint256` and then only should it be shifted by 160 bits.

 As of now, the `chainSelector` is uint64 when it is getting shifted by 160 bits, which will always lead to having all 0s, essentially `uint256(chainSelector << 160)` will always be `0x000....00000000`.

 Thus when it is taken `or` with `uint256(uint160(contractAddress))`, the result will only have `contractAddress` saved.

```solidity
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
    /**
        * @audit potential High -> packing does not work as expected!
        * uint256(chainSelector << 160) means that we are shifting a uint64 by 160 bits before
        * typecasting it to uint256 this will lead to problem as all the bits will be shifted 
        * out of the 64 bit range it will always result in 0
        */
    return bytes32(
        uint256(uint160(contractAddress)) |
        uint256(chainSelector << 160) // chainSelector needs to be typecasted to uint256 first!
                                        // should be (uint256(chainSelector) << 160)
    );
}
```

This incorrect value is then set to `mapping(bytes32 => bool) internal _ccipContracts` by using `_setCCIPCounterpart` which internally calls `_packCCIPContract`.

## Impact
There are multiple ways in which it impacts the entire protocol!

1. `BaseCCIPContract` is inherited by both `WinnablesTicketmanager` and `WinnablesPrizeManager`, and thus `_packCCIPContract` is incorrectly used in both contracts to save the mapping of counterparts in `_ccipContracts` mapping. This breaks the assumption that both `chain` and `chainSelector` is saved

2. Given that the protocols does plan to deploy on other chains too as seen from the screenshot. Following scenario is highly troubleSome:

ideally _ccipContracts for PrizeManager on Eth -> `_ccipContract[ethChainSelector + address on ETH(address A)]`

But in reality only addressA is saved hence -> `_ccipContract[addressA]` 

Now if on any other chain (say Arbitrum) if prizeManager is deployed via create2 or via simple deploy but same wallet + same nonce (this is quite common if the wallet is performing 1st txn on both chains) the address will be same `addressA`.

In such chase PrizeManager on Arbitrum will also have -> `_ccipContract[addressA]`  -> now if you enable/disable one using `setCCIPCounterpart(chainselector, address, enabled)`, both will be enabled or disabled.

## Proof of Concept:

Add the following mock contract to `mock` folder:

<Details>
<Summary> MockCCIPContract </Summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import "../BaseCCIPContract.sol";

contract MockCCIPContract is BaseCCIPContract {
    constructor(address router) BaseCCIPContract(router) {}

    // Expose the _setCCIPCounterpart for testing
    function setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) public {
        _setCCIPCounterpart(contractAddress, chainSelector, enabled);
    }

    // Expose the mapping for validation in tests
    function isCCIPContractEnabled(bytes32 counterpart) public view returns (bool) {
        return _ccipContracts[counterpart];
    }

    // Function to manually pack addresses and selectors for testing the packing logic
    function packCCIPContract(address contractAddress, uint64 chainSelector) public pure returns (bytes32) {
        return _packCCIPContract(contractAddress, chainSelector);
    }
}
```

</Details>

Run add the following Foundry test to `test` folder:


<Details>
<Summary> Foundry Test : BasicCCIPTest.t.sol</Summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test, console} from "forge-std/Test.sol";
import "../contracts/mock/MockCCIPContract.sol";

contract BaseCCIPContractTest is Test {
    MockCCIPContract mock;
    address testRouter = address(0x1);
    /**
     * assuming winnablesPrizeManager address is same on ethereum and arbitrum, this is quite common for protocols
     * to have uniform address across chains, and can be due to two reasons:
     * 1. Using create2 to deploy contracts on a predetermined address
     * 2. Using normal deployment but with same wallet and same nonce -> this is also quite common as deployer is usually
     * a multisig and a lot of times can have nonce 0 on newer chains if there hasn't been any txn performed before
     */
    address winnablesPrizeManager = address(makeAddr("WinnablesPrizeManager")); 
    uint64 ethereumChainSelector = 16015286601757825753; // as per constants.js
    uint64 arbitrumChainSelector = 3478487238524512106; // as per constants.js

    function setUp() public {
        mock = new MockCCIPContract(testRouter);
    }

    function testPackCCIPContract() public {
        bytes32 expectedEthereumPacking =  bytes32(
            uint256(uint160(winnablesPrizeManager)) |
            (uint256(ethereumChainSelector) << 160)
        );

        bytes32 expectedArbitrumPacking =  bytes32(
            uint256(uint160(winnablesPrizeManager)) |
            (uint256(arbitrumChainSelector) << 160)
        );
        bytes32 ethereumPacking = mock.packCCIPContract(winnablesPrizeManager, ethereumChainSelector);
        bytes32 arbitrumPacking = mock.packCCIPContract(winnablesPrizeManager, arbitrumChainSelector);
        console.log("incorrect chainSelector shift by uint256(ethereumChainSelector << 160) ");
        console.logBytes32( bytes32(uint256(ethereumChainSelector << 160)));
        console.log("correct chainSelector shift by (uint256(ethereumChainSelector) << 160) ");
        console.logBytes32(bytes32((uint256(ethereumChainSelector) << 160)));

        console.log("Expcted ccipPacking on Ethereum");
        console.logBytes32(expectedEthereumPacking);
        console.log("Actual ccipPacking Ethereum");
        console.logBytes32(ethereumPacking);

        console.log("Expcted ccipPacking on Arbitrum");
        console.logBytes32(expectedArbitrumPacking);
        console.log("Actual ccipPacking Ethereum");
        console.logBytes32(arbitrumPacking);

        assertNotEq(ethereumPacking, expectedEthereumPacking);
        assertNotEq(arbitrumPacking, expectedArbitrumPacking);

        assertEq(ethereumPacking, arbitrumPacking);
    }

    function testSetCCIPCounterPartClashes() public {
        // get bytes32 counterpart packing for both chains:
        bytes32 ethereumPacking = mock.packCCIPContract(winnablesPrizeManager, ethereumChainSelector);
        bytes32 arbitrumPacking = mock.packCCIPContract(winnablesPrizeManager, arbitrumChainSelector);

        // set eth counter part to true
        mock.setCCIPCounterpart(winnablesPrizeManager, ethereumChainSelector, true);

        // set arbitrum counterpart to false
        mock.setCCIPCounterpart(winnablesPrizeManager, arbitrumChainSelector, false);

        // check both will be FALSE -> as arbitrum was executed afterwards:
        console.log("is ethereum counterpart enabled ",mock.isCCIPContractEnabled(ethereumPacking));
        console.log("is arbitrum counterpart enabled ",mock.isCCIPContractEnabled(ethereumPacking));

        assertEq(mock.isCCIPContractEnabled(ethereumPacking), false); // is false even though it was supposed to be true
        assertEq(mock.isCCIPContractEnabled(arbitrumPacking), false);
    }
}
```

</Details>


Run the test using:
```bash
forge test --match-path test/BasicCCIPTest.t.sol -vvv
```

Output log:
<Details>
<Summary> Output Log</Summary>

```bash
Ran 2 tests for test/BasicCCIPTest.t.sol:BaseCCIPContractTest
[PASS] testPackCCIPContract() (gas: 24290)
Logs:
  incorrect chainSelector shift by uint256(ethereumChainSelector << 160) 
  0x0000000000000000000000000000000000000000000000000000000000000000
  correct chainSelector shift by (uint256(ethereumChainSelector) << 160) 
  0x00000000de41ba4fc9d91ad90000000000000000000000000000000000000000
  Expcted ccipPacking on Ethereum
  0x00000000de41ba4fc9d91ad9c47c9dea870af03cfa09520fb5d7c0ed34920a38
  Actual ccipPacking Ethereum
  0x000000000000000000000000c47c9dea870af03cfa09520fb5d7c0ed34920a38
  Expcted ccipPacking on Arbitrum
  0x00000000304611b6affba76ac47c9dea870af03cfa09520fb5d7c0ed34920a38
  Actual ccipPacking Ethereum
  0x000000000000000000000000c47c9dea870af03cfa09520fb5d7c0ed34920a38

[PASS] testSetCCIPCounterPartClashes() (gas: 33432)
Logs:
  is ethereum counterpart enabled  false
  is arbitrum counterpart enabled  false

Suite result: ok. 2 passed; 0 failed; 0 skipped; finished in 1.50ms (439.12µs CPU time)
```
</Details>

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/BaseCCIPContract.sol#L40-L45

## Tool used

Manual Review, Foundry

## Recommendation
Change the `_packCCIPContract` function to:

```diff
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
-       uint256(chainSelector << 160)
+       (uint256(chainSelector) << 160)
    );
}
```