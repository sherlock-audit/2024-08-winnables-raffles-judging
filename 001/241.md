Original Frost Wombat

High

# Winner Will Be Unable To Withdraw The Prize If Using An Account Abstraction, MultiSig, or Any Contract-Based Account with Different Crosschain Addresses

### Summary

Most Account Abstraction and MultiSig Wallets employ distinct addresses across different blockchain networks. As a result, if a winner utilizes such accounts or any similar contract-based account, they will encounter difficulties in claiming their prize through the `PrizeManager::claimPrize` function. This issue arises because there is no provision for administrators to access the prizes once a winner is determined and the raffle concludes, leading to the permanent locking of the prizes within the contract.


### Root Cause

According to the documentation, the `WinnableTicketManager` contract resides on the Avalanche network. Upon receiving the random word, the administrator invokes the `propagateRaffleWinner` function to notify the `WinnablePrizeManager`, which is situated on the Ethereum network. The `propagateRaffleWinner` function retrieves the winner's address by invoking `_getWinnerByRequestId`, which, in turn, identifies the winner by interacting with the `WinnableTicket` contract, also located on the Avalanche network. Therefore, the winner's address pertains to the Avalanche network. However, when this address is transferred to Ethereum, it becomes ineffective since contract-based accounts often utilize alternative addresses across chains.
[LinkToCode](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334)
```javascript
    function propagateRaffleWinner(
        address prizeManager,
        uint64 chainSelector,
        uint256 raffleId
    ) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED)
            revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
@>      address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

@>      _sendCCIPMessage(
@>          prizeManager,
@>          chainSelector,
@>          abi.encodePacked(
@>              uint8(CCIPMessageType.WINNER_DRAWN),
@>              raffleId,
@>              winner
@>          )
        );

        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }

    function _getWinnerByRequestId(
        uint256 requestId
    ) internal view returns (address) {
        RequestStatus storage request = _chainlinkRequests[requestId];
@>      uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(
@>          request.raffleId
@>      );
@>      uint256 winningTicketNumber = request.randomWord % supply;
@>      return
@>          IWinnablesTicket(TICKETS_CONTRACT).ownerOf(
@>              request.raffleId,
@>              winningTicketNumber
@>          );
    }

```

### Internal pre-conditions

- The winner uses a contract-based account with different crosschain addresses, such as a multisig or an account abstraction.


### External pre-conditions

None

### Attack Path

None

### Impact

The prize will remain inaccessible and permanently locked within the `prizeManager` contract.


### PoC

_No response_

### Mitigation

A potential solution involves adding a `dstChainAddress` parameter during the minting process in the `WinnableTicket::mint` function. This address would then be returned upon winner selection.
```diff
-   function mint(address to, uint256 id, uint256 amount) external onlyRole(1) {
+   function mint(address to, address dstChainAddress ,uint256 id, uint256 amount) external onlyRole(1) {

        if (to == address(0)) {
            revert TransferToAddressZero();
        }
        address operator = msg.sender;
        uint256 startId = _supplies[id];

        unchecked {
            _balances[id][to] += amount;
            _supplies[id] = startId + amount;
        }

        _ticketOwnership[id][startId] = to;
+       ownerAddressToDstChainAddress[to] = dstChainAddress; // this mapping should be added also!

        emit NewTicket(id, startId, amount);
        emit TransferSingle(operator, address(0), to, id, amount);

        _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount);
    }
```
Adjustments in `_getWinnerByRequestId` to utilize the new mapping:
```diff
    function _getWinnerByRequestId(
        uint256 requestId
    ) internal view returns (address) {
        RequestStatus storage request = _chainlinkRequests[requestId];
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(
            request.raffleId
        );
        uint256 winningTicketNumber = request.randomWord % supply;
-       return
-           IWinnablesTicket(TICKETS_CONTRACT).ownerOf(
-               request.raffleId,
-               winningTicketNumber
-           );
+       address to = IWinnablesTicket(TICKETS_CONTRACT).ownerOf(request.raffleId,winningTicketNumber);
+       return IWinnablesTicket(TICKETS_CONTRACT).ownerAddressToDstChainAddress(to);

    }
```