Lone Peanut Swallow

Medium

# Raffles can be cancellable and drawable at the same time

## Summary

Under certain conditions, a raffle in the `IDLE` status can transition to both the `CANCELED` or `REQUESTED` states, which should be mutually exclusive.

## Vulnerability Detail

The [`WinnablesTicketManager._checkShouldDraw`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L422-L432) and [`WinnablesTicketManager._checkShouldCancel`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434-L441) internal functions implement the sanitization logic to verify that an idle raffle can either proceed to the randomness request stage or be cancelled.

`WinnablesTicketManager._checkShouldDraw` requires the raffle to conform with the following conditions:
1. Be in the `IDLE` state.
2. Have sold **at least** `raffle.minTicketsThreshold` tickets.
3. If the method is called before `raffle.endsAt`, the raffle should have sold out all tickets.

`WinnablesTicketManager._checkShouldCancel` requires the raffle to conform with:
1. Be in either `PRIZE_LOCKED` or `IDLE` state.
2. The method should not be called before `raffle.endsAt`.
3. The raffle should have sold **at most** `raffle.minTicketsThreshold` tickets.

Analyzing these sets of conditions, one can find that if `block.timestamp >= raffle.endsAt && IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold` then both methods will not revert, meaning that both state transitions are allowed.

As a consequence, given that both [`WinnablesTicketManager.drawWinner`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L310) and [`WinnablesTicketManager.cancelRaffle`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278) can be called by an unauthenticated party, a race condition is generated on the way the raffle evolves: the outcome depends on the first method invoked. 
In particular, an attacker is able to disrupt a valid raffle's randomness request by frontrunning a call to `drawWinner` with a call to `cancelRaffle`.

## Impact

Medium. 
Under the explained conditions, a race condition is generated to cancel or draw the randomness for a given raffle. The larger impact comes from an attacker being able to cancel a valid raffle, causing a repeatable temporary DOS on protocol core functionality.

## Code Snippet

```solidity
function _checkShouldDraw(uint256 raffleId) internal view {
	Raffle storage raffle = _raffles[raffleId];
	if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
	uint256 currentTicketSold = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
	if (currentTicketSold == 0) revert NoParticipants();

	if (block.timestamp < raffle.endsAt) {
		if (currentTicketSold < raffle.maxTicketSupply) revert RaffleIsStillOpen();
	}
	if (currentTicketSold < raffle.minTicketsThreshold) revert TargetTicketsNotReached(); // <@ supply is allowed to be == threshold
}

function _checkShouldCancel(uint256 raffleId) internal view {
	Raffle storage raffle = _raffles[raffleId];
	if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
	if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
	if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
	uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
	if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached(); // <@ supply is allowed to be == threshold
}
```

## Tool used

Manual Review

## Recommendation

The protocol should select whether `raffle.minTicketsThreshold` is an amount of tickets sold at which a raffle is allowed to proceed normally or it should be cancelled. Given such choice, the corresponding validation method should be modified to disallow the case in which `IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId) == raffle.minTicketsThreshold`