Narrow Latte Huskie

High

# Reentrancy Vulnerability in `WinnablesPrizeManager::claimPrize()` Function

## Summary

The claimPrize function in the contract allows for reentrancy attacks when claiming ETH prizes. The function does not appropriately update the raffle's status before executing external calls, leading to potential reentrancy vulnerabilities.

## Vulnerability Detail

The claimPrize function updates the status of a raffle to CLAIMED after sending the prize, rather than before. This ordering allows a malicious winner to reenter the claimPrize function during the execution of an external call (_sendETHPrize), potentially claiming more ETH than intended or disrupting the intended logic flow.

```solidity
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;

        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);

        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);

        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();

        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed(); 
        rafflePrize.status = RafflePrizeStatus.CLAIMED; // @audit-issue state variable should be changed before external call
        emit PrizeClaimed(raffleId, msg.sender);
    }
```

The `_ethLocked` variable holds the total amount of ETH locked in the contract where as the _ethRaffles[raffleId] holds the amount of ETH of the raffle. The _ethLocked value will always be either greater or equal to the _ethRaffles[raffleId] variable, a malicious user can use this to their advantage by re-entering into the contract over and over again until _ethLocked is almost depleted.

## POC

1. Malicious user is the winner of the raffle and calls `claimPrize()`
2. Once raffleType == RaffleType.ETH is checked, the contract will deduct total _ethLocked by the prize amount of the raffle
3. Then _sendETHPrize() function is called which contains the following code

```solidity
function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
```

4. This call will transfer control the malicious user's contract where they can reenter the claimPrize() function
5. The raffleType == RaffleType.ETH will be checked again where _ethLocked will be deducted again
6. Then the call will again transfer to the malicious user's contract through _sendETHPrize() function
7. The user can repeat the above steps till _ethLocked is depleted 

This is only possible because the status updated after the external call is made 

```solidity
rafflePrize.status = RafflePrizeStatus.CLAIMED; 
```

## Impact

If exploited, this vulnerability could lead to unauthorized withdrawal of funds or other unintended actions being repeated, such as multiple claiming of prizes. This can result in financial losses for legitimate participants or the depletion of contract resources.

## Code Snippet

[claimPrize()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L103-L124)
[sendETHPrize()](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L103-L124)

## Tool Used

Manual Review

## Recommendation

```diff
    /// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
+       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed(); 
+       rafflePrize.status = RafflePrizeStatus.CLAIMED;

        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);

        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);

        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();

        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
-       if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed(); 
-       rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
