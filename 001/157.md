Blunt Mulberry Wombat

High

# Smart-wallet winners can not claim their prizes on mainnet

## Summary
Smart-wallet winners on Avalanche cannot claim their prizes on the mainnet due to smart-wallet address discrepancies between chains, rendering their winnings inaccessible.

## Vulnerability Detail
Smart wallets, including multi-signature wallets and Account Abstraction wallets, are valid entities for purchasing tickets as they are explicitly supported by the `WinnablesTicket::mint` function:
```solidity
function mint(address to, uint256 id, uint256 amount) external onlyRole(1) {
    if (to == address(0)) {
      revert TransferToAddressZero();
    }
    address operator = msg.sender;
    uint256 startId = _supplies[id];

    unchecked {
      _balances[id][to] += amount;
      _supplies[id] = startId + amount;
    }

    _ticketOwnership[id][startId] = to;
    emit NewTicket(id, startId, amount);
    emit TransferSingle(operator, address(0), to, id, amount);

>>>    _doSafeTransferAcceptanceCheck(operator, address(0), to, id, amount);
}

function _doSafeTransferAcceptanceCheck(
    address operator,
    address from,
    address to,
    uint256 id,
    uint256 amount
  ) private {
>>>    if (to.isContract()) {
      try IERC1155Receiver(to).onERC1155Received(operator, from, id, amount, "") returns (bytes4 response) {
        if (response != IERC1155Receiver.onERC1155Received.selector) {
          revert TransferRejected();
        }
      } catch {
        revert TransferRejected();
      }
    }
  }
```
Once a winner is selected after the Chainlink VRF response, **the same winning address from Avalanche is propagated to the mainnet**:
```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
    raffle.status = RaffleStatus.PROPAGATED;
>>>     address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

    _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    unchecked {
        _lockedETH -= raffle.totalRaised;
    }
}

function _getWinnerByRequestId(uint256 requestId) internal view returns(address) {
    RequestStatus storage request = _chainlinkRequests[requestId];
    uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(request.raffleId);
    uint256 winningTicketNumber = request.randomWord % supply;
>>>    return IWinnablesTicket(TICKETS_CONTRACT).ownerOf(request.raffleId, winningTicketNumber);
}
```
However, if the winner is a smart wallet, its address on Avalanche will not be the same on the mainnet. This discrepancy means that smart-wallet winners cannot claim their prize on the mainnet, as the propagated address will not match their mainnet address.
## Impact
Smart-wallet winners are unable to claim their prizes on the mainnet, effectively rendering their winnings inaccessible.
## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L338-L340
## Tool used

Manual Review

## Recommendation
Consider allowing buyers to specify the address on the mainnet where they would like to receive their prize when purchasing tickets. Use this provided address when propagating the winner to the mainnet.