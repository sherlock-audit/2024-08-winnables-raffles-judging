Breezy Amethyst Penguin

Medium

# When `block.timestamp` equals corresponding raffle's `endsAt` in which such raffle's purchase period is not ended, `WinnablesTicketManager._checkShouldDraw` function does not revert given that its `maxTicketSupply` is not reached and `WinnablesTicketManager._checkShouldCancel` function does not revert, which cause winner to still be drawn for such raffle before its `maxTicketSupply` is reached and alternatively such raffle to still be canceled before its purchase period is finished though such raffle should not be allowed to draw a winner or be canceled in this case

### Summary

When `block.timestamp` equals the corresponding raffle's `endsAt` in which such raffle's purchase period is not ended, the `WinnablesTicketManager._checkShouldDraw` function does not revert given that its `maxTicketSupply` is not reached, and the `WinnablesTicketManager._checkShouldCancel` function does not revert either. Although the raffle should not be allowed to draw a winner or be canceled in this case, the winner can still be drawn for such raffle before its `maxTicketSupply` is reached, and alternatively such raffle can still be canceled before its purchase period is finished. Furthermore, if a participant's `WinnablesTicketManager.buyTickets` transaction is executed after such drawing or cancellation in the same block, such participant's `WinnablesTicketManager.buyTickets` transaction reverts, and he is denied from purchasing tickets of such raffle though he should be allowed to.

### Root Cause

The `WinnablesTicketManager._checkShouldDraw` function does not revert if `block.timestamp` equals the corresponding raffle's `endsAt` when its `maxTicketSupply` is not reached. Moreover, the `WinnablesTicketManager._checkShouldCancel` function does not revert if `block.timestamp` equals the corresponding raffle's `endsAt`.

### Internal pre-conditions

Not applicable

### External pre-conditions

1. The block to be included and executed has `block.timestamp` equaling the corresponding raffle's `endsAt`.

### Attack Path

1. Such raffle's purchase period is not ended since the corresponding block's `block.timestamp` equals its `endsAt`.
2. Although such raffle's `maxTicketSupply` is not reached yet, the `WinnablesTicketManager.drawWinner` transaction, which further calls the `WinnablesTicketManager._checkShouldDraw` function, can be executed in the corresponding block.
	- This causes the winner to be drawn for such raffle given that its `minTicketsThreshold` has been reached even though its winner should not be allowed to be drawn when its purchase period is not finished and its `maxTicketSupply` is not reached.
3. Alternatively, although such raffle's purchase period is not ended, the `WinnablesTicketManager.cancelRaffle` transaction, which further calls the `WinnablesTicketManager._checkShouldCancel` function, can be executed in the corresponding block.
	- This causes such raffle to be canceled given that its `minTicketsThreshold` has not been reached even though it should not be allowed to be canceled when its purchase period is not finished.
4. If a participant's `WinnablesTicketManager.buyTickets` transaction is executed after such drawing or cancellation in the same block, such participant's `WinnablesTicketManager.buyTickets` transaction would revert, and he is denied from purchasing tickets of such raffle even though he should be allowed to.

### Impact

When `block.timestamp` equals a raffle's `endsAt` in which such raffle's purchase period is not ended, such raffle's winner should not be allowed to be drawn if its `maxTicketSupply` is not reached, and such raffle should not be allowed to be canceled. However, the winner can still be drawn for such raffle before its `maxTicketSupply` is reached, and alternatively such raffle can still be canceled before its purchase period is finished. Also, the participant, whose `WinnablesTicketManager.buyTickets` transaction gets executed after such drawing or cancellation in the same block, is forced to revert his `WinnablesTicketManager.buyTickets` transaction and denied from purchasing tickets of such raffle though he should be allowed to.

### PoC

Because the `WinnablesTicketManager._checkTicketPurchaseable` function executes `if (block.timestamp > raffle.endsAt) revert RaffleHasEnded()`, the raffle's purchase period is not ended when `block.timestamp` equals its `endsAt`. When the participant's `WinnablesTicketManager.buyTickets` transaction is included in the block whose `block.timestamp` equals the raffle's `endsAt`, such participant should be allowed to buy such raffle's tickets.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L407-L420
```solidity
    function _checkTicketPurchaseable(uint256 raffleId, uint256 ticketCount) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.startsAt > block.timestamp) revert RaffleHasNotStarted();
        if (raffle.status != RaffleStatus.IDLE) revert RaffleHasEnded();
@>      if (block.timestamp > raffle.endsAt) revert RaffleHasEnded();
        uint256 ticketPurchased = uint256(uint32(uint256(raffle.participations[msg.sender]) >> 128));
        unchecked {
            if (ticketPurchased + ticketCount > raffle.maxHoldings) revert TooManyTickets();
        }
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        unchecked {
            if (supply + ticketCount > raffle.maxTicketSupply) revert TooManyTickets();
        }
    }
```

However, when `block.timestamp` equals the raffle's `endsAt`, calling the `WinnablesTicketManager.drawWinner` function, which further calls the `WinnablesTicketManager._checkShouldDraw` function, does not revert even if such raffle's `maxTicketSupply` is not reached given that such raffle's `minTicketsThreshold` has been reached because the `WinnablesTicketManager._checkShouldDraw` function does not revert if `block.timestamp < raffle.endsAt` is false.

Since the raffle's purchase period is not ended when `block.timestamp` equals such raffle's `endsAt`, such raffle should only allow its winner to be drawn if its `maxTicketSupply` is reached. Yet, such `WinnablesTicketManager.drawWinner` transaction can be executed in the block whose `block.timestamp` equals such raffle's `endsAt`, causing the winner to be drawn for such raffle before its `maxTicketSupply` is reached. Moreover, if the `WinnablesTicketManager.drawWinner` transaction is executed before the previously described `WinnablesTicketManager.buyTickets` transaction in the same block, such `WinnablesTicketManager.buyTickets` transaction would revert, and the corresponding participant is denied from purchasing tickets of such raffle even though he should be allowed to.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L422-L432
```solidity
    function _checkShouldDraw(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
        uint256 currentTicketSold = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (currentTicketSold == 0) revert NoParticipants();

@>      if (block.timestamp < raffle.endsAt) {
            if (currentTicketSold < raffle.maxTicketSupply) revert RaffleIsStillOpen();
        }
        if (currentTicketSold < raffle.minTicketsThreshold) revert TargetTicketsNotReached();
    }
```

Alternatively, when `block.timestamp` equals the raffle's `endsAt`, calling the `WinnablesTicketManager.cancelRaffle` function, which further calls the `WinnablesTicketManager._checkShouldCancel` function, does not revert given that such raffle's `minTicketsThreshold` has not been reached since the `WinnablesTicketManager._checkShouldCancel` function does not revert if `raffle.endsAt > block.timestamp` is false.

Because the raffle's purchase period is not ended when `block.timestamp` equals such raffle's `endsAt`, more tickets of such raffle can be purchased to reach its `minTicketsThreshold` at that time, and such raffle should not be allowed to be canceled. However, such `WinnablesTicketManager.cancelRaffle` transaction can be executed in the block whose `block.timestamp` equals such raffle's `endsAt`, which causes such raffle to be canceled before its purchase period is finished. Furthermore, if the `WinnablesTicketManager.cancelRaffle` transaction is executed before the previously described `WinnablesTicketManager.buyTickets` transaction in the same block, such `WinnablesTicketManager.buyTickets` transaction would revert, and the corresponding participant is denied from purchasing tickets of such raffle even though he should be allowed to.

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L434-L441
```solidity
    function _checkShouldCancel(uint256 raffleId) internal view {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
        if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
@>      if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
        uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
        if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
    }
```

### Mitigation

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L428-L430 can be updated to the following code.

```solidity
        if (block.timestamp <= raffle.endsAt) {
            if (currentTicketSold < raffle.maxTicketSupply) revert RaffleIsStillOpen();
        }
```

Also, https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L438 can be updated to the following code.

```solidity
        if (raffle.endsAt >= block.timestamp) revert RaffleIsStillOpen();
```