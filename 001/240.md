Generous Peanut Okapi

Medium

# When raffles are cancelled in `WinnablesTicketManager`, `_lockedETH` is not updated correspondingly

### Summary

When raffles are [cancelled](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L278) in `WinnablesTicketManager`, `_lockedETH` is not updated correspondingly, this will cause actual balance in contract to be less than `_lockedETH`, and potential DoS.

### Root Cause

When users buy tickets, `_lockedETH` is incremented based on `msg.value` users have offered:

```solidity
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
            _lockedETH += msg.value;
        }
```

Also when raffles are cancelled, users' input will be refunded:

```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
```

We can also see that, when raffles are cancelled, a cross-chain message is sent to the prize manager to notify such decision, and nothing else special:

```solidity
    function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        _checkShouldCancel(raffleId);

        _raffles[raffleId].status = RaffleStatus.CANCELED;
        _sendCCIPMessage(
            prizeManager,
            chainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```

We can notice that, when this happens, `_lockedETH` is not updated, but it should be, as the raffle is cancelled, and users' input shall be refunded. `_lockedETH` is, however, updated when raffles are propogated:

```solidity
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```


### Internal pre-conditions

A raffle is cancelled in the ticket manager contract

### External pre-conditions

Users start to withdraw their funds by calling `refundPlayers`

### Attack Path

_No response_

### Impact

As `_lockedETH` is not updated, when `refundPlayers` is called, this will make actual balance to be less than `_lockedETH`, and when this happens a number of times, `withdrawETH` will be unable to work, as there will be no enough Ether to be withdrawn from.

### PoC

_No response_

### Mitigation

Deduct `_lockedETH` either when raffles are cancelled, or during `refundPlayers` being called.