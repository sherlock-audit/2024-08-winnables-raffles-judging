Broad Red Skunk

High

# Admin can't withdraw available balance due to wrong locked ETH amount

## Summary
Normally, admin can claim ticket sales from ticket manager contract using withdrawETH() function but due to wrong _lockedETH amount, he can't claim that.

## Vulnerability Detail
After sales admin can claim available balance using following function
```solidity
    function withdrawETH() external onlyRole(0) {
        uint256 balance;
        unchecked {
&>          balance = address(this).balance - _lockedETH;
        }
        _sendETH(balance, msg.sender);
    }
 ```
_lockedETH is updated every time while receiving or sending ETH:
```solidity
    function buyTickets(
        uint256 raffleId,
        uint16 ticketCount,
        uint256 blockNumber,
        bytes calldata signature
    ) external payable {
        if (ticketCount == 0) revert InvalidTicketCount();
        _checkTicketPurchaseable(raffleId, ticketCount);
        _checkPurchaseSig(raffleId, ticketCount, blockNumber, signature);

        Raffle storage raffle = _raffles[raffleId];
        uint256 participation = uint256(raffle.participations[msg.sender]);
        uint128 totalPaid = uint128(participation) + uint128(msg.value);
        uint32 totalPurchased = uint32(participation >> 128) + uint32(ticketCount);
        unchecked {
            raffle.participations[msg.sender] = bytes32(
                (participation & type(uint256).max << 160)
                | totalPaid |
                uint256(totalPurchased) << 128
            );
        }
        unchecked {
            raffle.totalRaised += msg.value;
            _userNonces[msg.sender]++;
&>          _lockedETH += msg.value;
        }
    function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);



        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
&>          _lockedETH -= raffle.totalRaised;
        }
    }
```
 

But after refunding, _lockedETH variable should decrease but this is not happened in current contract system:
```solidity
    function refundPlayers(uint256 raffleId, address[] calldata players) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.CANCELED) revert InvalidRaffle();
        for (uint256 i = 0; i < players.length; ) {
            address player = players[i];
            uint256 participation = uint256(raffle.participations[player]);
            if (((participation >> 160) & 1) == 1) revert PlayerAlreadyRefunded(player);
            raffle.participations[player] = bytes32(participation | (1 << 160));
            uint256 amountToSend = (participation & type(uint128).max);
            _sendETH(amountToSend, player);
            emit PlayerRefund(raffleId, player, bytes32(participation));
            unchecked { ++i; }
        }
    }
    
       function _sendETH(uint256 amount, address to) internal {
        if (amount == 0) revert NothingToSend();
        (bool success, ) = to.call{ value: amount }("");
        if (!success) revert ETHTransferFail();
    }
}
```

Which means _lockedETH variable will go away from correct state every refunding process.

## Proof of Concept

### Scenario:

- A raffle event is planned and users send 100 ETH in total ( _lockedETH = 100 ether , contract ETH balance = 100 ether)
- Due to some problems, it's cancelled and users get refund ( _lockedETH = 100 ether (problematic, contract ETH balance = 0 ether))
- Another raffle event is planned and users send 100 ETH in total ( _lockedETH = 200 ether, contract ETH balance = 100 ether)
- Raffle event is finished and winner gets his price from prize manager ( _lockedETH = 100 ether, contract ETH balance = 100 ether)
- Admin wants to claim ticket sales ( ETH balance - _lockedETH = 0 ether )
- Result: Admin couldn't receive ticket sales ( ETH ) ( loss of funds )


## Impact
Loss of funds

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L215
## Tool used

Manual Review

## Recommendation
_lockedETH have to be updated while refund process