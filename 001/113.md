Faithful Lemonade Penguin

High

# Attacker can permanently lock winners tokens with malicious input in `propagateRaffleWinner`

## Summary
Since the inputs in `propagateRaffleWinner` aren't validated, an attacker can use malicious inputs to permanently lock winners NFTs, ETH, or tokens.

## Vulnerability Detail
When the `drawWinner` is called for a `raffleId`, the protocol will [request a random number from VRF and then changes the status of the raffle to `REQUESTED`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L315):
```solidity
    function drawWinner(uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        _checkShouldDraw(raffleId);
        raffle.status = RaffleStatus.REQUESTED;

@>      uint256 requestId = VRFCoordinatorV2Interface(VRF_COORDINATOR).requestRandomWords(
            KEY_HASH,
            SUBSCRIPTION_ID,
            3,
            100_000,
            1
        );
        _chainlinkRequests[requestId] = RequestStatus({
            raffleId: raffleId,
            randomWord: 0
        });
        raffle.chainlinkRequestId = requestId;
        emit RequestSent(requestId, raffleId);
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
    }
```

When the VRF calls `fulfillRandomWords` to deliver the requested random number, the status of the raffle [will be changed to `FULFILLED`](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L358).
```solidity
    function fulfillRandomWords(
        uint256 requestId,
        uint256[] memory randomWords
    ) internal override {
        RequestStatus storage request = _chainlinkRequests[requestId];
        Raffle storage raffle = _raffles[request.raffleId];
        if (raffle.status != RaffleStatus.REQUESTED) revert RequestNotFound(requestId);
        request.randomWord = randomWords[0];
@>      raffle.status = RaffleStatus.FULFILLED;
        emit WinnerDrawn(requestId);
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(request.raffleId);
    }
```
At this point, anyone who calls `propagateRaffleWinner` with the `raffleId` as input [will trigger the sending of a CCIP message to the prize manager](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L340), allowing the winner to claim their rewards:
```solidity
@>  function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
        raffle.status = RaffleStatus.PROPAGATED;
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

@>      _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
            _lockedETH -= raffle.totalRaised;
        }
    }
```

However, since the inputs of `propagateRaffleWinner` are not validated, anyone can call this function and provide malicious input for `prizeManager` and `chainSelector`, which will result in the winner's rewards being permanently locked.

## Impact
Severe loss of funds, tokens, or NFTs for the winners

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L315
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L358
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L340

## Tool used

Manual Review

## Recommendation
Do not use user inputs for structuring the CCIP message. Instead, use state variables that cannot be changed by unauthorized users
