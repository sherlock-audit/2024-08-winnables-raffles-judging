Silly Burgundy Scallop

High

# Raffle can be cancelled right after locking the asset in PrizeManager

## Summary

Any raffle **can be canceled once** the prize is locked due to wrong `if check`.

## Vulnerability Detail

When a prize is locked for a raffle, any of the three locking functions in `WinnablesPrizeManager.sol` will be called by the administrator - `lockNFT()`, `lockETH()`, `lockTokens()`. Each of them sends a CCIP message to the `WinnablesTicketManager.sol` contract, which will be received in `_ccipReceive()`.

```solidity
function _ccipReceive(
    Client.Any2EVMMessage memory message
) internal override {
    (address _senderAddress) = abi.decode(message.sender, (address));
    bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
    if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();
    (uint256 raffleId) = abi.decode(message.data, (uint256));
    if (_raffles[raffleId].status != RaffleStatus.NONE) {
        // The raffle cannot be created, send back a cancel message to unlock the prize
        _sendCCIPMessage(
            _senderAddress,
            message.sourceChainSelector,
            abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
        );
        return;
    }
    _raffles[raffleId].status = RaffleStatus.PRIZE_LOCKED;

    emit RafflePrizeLocked(
        message.messageId,
        message.sourceChainSelector,
        raffleId
    );
}
```

As you can see when the prize is locked and this is called the raffle status is updated to **`PRIZE_LOCKED`** and from there the raffle can be created via `createRaffle()` by the admin.

But `cancelRaffle()` can also be called even before the raffle is created and by anyone, due to a faulty if check.

```solidity
function cancelRaffle(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
    _checkShouldCancel(raffleId);

    _raffles[raffleId].status = RaffleStatus.CANCELED;
    _sendCCIPMessage(
        prizeManager,
        chainSelector,
        abi.encodePacked(uint8(CCIPMessageType.RAFFLE_CANCELED), raffleId)
    );
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
}
```

```solidity
function _checkShouldCancel(uint256 raffleId) internal view {
    Raffle storage raffle = _raffles[raffleId];
    if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
    if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
    if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
    uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
    if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
}
```

Once the prize is locked and `_ccipReceive()` is called, the state of the raffle will be **`PRIZE_LOCKED`** and the first check of `_checkShouldCancel()` will pass and the function will complete, then the state of the raffle will be set to `CANCELED` and the CCIP message will be send to unlock the reward on Mainnet. That way the creation of the raffle can be griefed and always after the prize is locked, everyone can call `cancelRaffle` and send it back to L1.

## Impact

Unlimited grefing of the raffle creation will not allow raffle creation and will always be canceled immediately after prize is locked.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/e8b0603f6a155c7505dacc77194ae6789d0dbe7a/public-contracts/contracts/WinnablesTicketManager.sol#L436

## Tool used

Manual Review

## Recommendation

Remove the `PRIZE_LOCKED` check, that way if `cancelRaffle()` is called when the raffle is not in `IDLE` status, the function will revert.

```diff
function _checkShouldCancel(uint256 raffleId) internal view {
    Raffle storage raffle = _raffles[raffleId];
-   if (raffle.status == RaffleStatus.PRIZE_LOCKED) return;
    if (raffle.status != RaffleStatus.IDLE) revert InvalidRaffle();
    if (raffle.endsAt > block.timestamp) revert RaffleIsStillOpen();
    uint256 supply = IWinnablesTicket(TICKETS_CONTRACT).supplyOf(raffleId);
    if (supply > raffle.minTicketsThreshold) revert TargetTicketsReached();
}
```