Merry Sepia Whale

High

# Attacker can lock up winner rewards in the Prize Manager contract by several ways during call of `propagateRaffleWinner` in Ticket Manager

## Summary
An attacker can specify a fake `prizeManager` contract to send the CCIP message to on Ethereum/counter-party chain which holds raffle prizes. They could also just input another chains' `chainSelector` which even if the `prizeManager` is correct, the `chainSelector` would force the CCIP message to be routed to another contract. Doing any of this forces the locked reward for that specific raffle to be locked and non-recoverable.

## Vulnerability Detail
The issue lies in the `propagateRaffleWinner` function where there is no validation that the `prizeManager` or `chainSelector` is the expected ones for the counter-party chain to route the message to. Hence, once the message is sent, the state change that occurs after in lines 337 and 343 of the `WinnablesTicketManager` assumes the message will be delivered whereas it wouldn't and the  raffle cannot be re-propagated because of the check in line 336 hence, the funds locked in the prize manager contract will be locked because there would be no winner for it and cannot be reclaimed by the admin/owner either

```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus();
@>        raffle.status = RaffleStatus.PROPAGATED; // @audit state change but the prizeManager and chainSelector is not the correct ones
        address winner = _getWinnerByRequestId(raffle.chainlinkRequestId);

        _sendCCIPMessage(prizeManager, chainSelector, abi.encodePacked(uint8(CCIPMessageType.WINNER_DRAWN), raffleId, winner));
        IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
        unchecked {
 @>           _lockedETH -= raffle.totalRaised; // ticket buyer's pooled fund is released
        }
    }
```

Since the attacker inputed wrong arguments during `propagateRaffleWinner` call, this function below will not be executed on the counterparty chain:

```solidity
function _ccipReceive(
        Client.Any2EVMMessage memory message
    ) internal override {
        (address _senderAddress) = abi.decode(message.sender, (address));
        bytes32 counterpart = _packCCIPContract(_senderAddress, message.sourceChainSelector);
        if (!_ccipContracts[counterpart]) revert UnauthorizedCCIPSender();

        CCIPMessageType messageType = CCIPMessageType(uint8(message.data[0]));
        uint256 raffleId;
        address winner;
        if (messageType == CCIPMessageType.RAFFLE_CANCELED) {
            raffleId = _decodeRaffleCanceledMessage(message.data);
            _cancelRaffle(raffleId);
            return;
        }
        (raffleId, winner) = _decodeWinnerDrawnMessage(message.data);
        _rafflePrize[raffleId].winner = winner;
        emit WinnerPropagated(raffleId, winner);
    }
```

Also, once the `propagateRaffleWinner` is called once, the function cannot be called again:
```solidity
function propagateRaffleWinner(address prizeManager, uint64 chainSelector, uint256 raffleId) external {
        Raffle storage raffle = _raffles[raffleId];
@>        if (raffle.status != RaffleStatus.FULFILLED) revert InvalidRaffleStatus(); // only callable once
@>       raffle.status = RaffleStatus.PROPAGATED; // too late now
        ...
    }
```

Hence, raffle can never be canceled nor claimed and ether is locked:
```solidity
function _cancelRaffle(uint256 raffleId) internal { // @note only callable when we receive a message
        RaffleType raffleType = _rafflePrize[raffleId].raffleType;
        if (_rafflePrize[raffleId].status == RafflePrizeStatus.CANCELED) revert InvalidRaffle();
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage nftInfo = _nftRaffles[raffleId];
            _nftLocked[nftInfo.contractAddress][nftInfo.tokenId] = false;
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage tokenInfo = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[tokenInfo.tokenAddress] -= tokenInfo.amount; }
        } else if (raffleType == RaffleType.ETH) {
   @>         unchecked { _ethLocked -= _ethRaffles[raffleId]; } // this is where we cancel
        } else revert InvalidRaffle();
        _rafflePrize[raffleId].status = RafflePrizeStatus.CANCELED;
        emit PrizeUnlocked(raffleId);
    }
```

```solidity
function claimPrize(uint256 raffleId) external {
        ...
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; } // @audit cannot be claimed because no winner as the message was never delivered
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim(); 
       ...
    }
```

## Impact
Loss of raffle prize winner's funds. 

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesTicketManager.sol#L334-L345

## Tool used

Manual Review

## Recommendation
Since the intent is to allow anyone initiate drawing a winner and then relaying the winner to propagate the prize manager contract so they can claim the winnings on the counter-party chain, implement a whitelist setter function that the admin can call to set the correct `prizeManager` and `chainSelector`. Doing this will remove the need for the caller to provide the arguments and hence ensure that messages sent are always delivered to the correct destination counter party chains.