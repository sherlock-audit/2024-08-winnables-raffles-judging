Blunt Mulberry Wombat

High

# Improper handling of bitwise shift operation in `_packCCIPContract` function

## Summary
The `_packCCIPContract` function does not encode `chainSelector` due to an improper bitwise shift on a `uint64` type, resulting in a packed value that omits the chain identifier

## Vulnerability Details
When performing cross-chain messaging, `_packCCIPContract` is used to verify the sender and the source chain by combining a smart contract address with a `chainSelector` into a single `bytes32` value. This function is intended to pack the contract address into the least significant 160 bits and the chainSelector into bits `160-224` of the bytes32 output:
```solidity
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
        uint256(chainSelector << 160) 
    );
}
```
The function first converts the `contractAddress` to a `uint160` and then to a `uint256`. It then attempts to shift the `chainSelector`, a `uint64`, by `160 bits` to the left intending to position it in the higher bits, while the contract address occupies the lower 160 bits. The two values are then combined using a bitwise `OR` operation.

The issue is that **the left-shift operation is performed directly on the uint64-typed `chainSelector`**:
```solidity
uint256(chainSelector << 160) 
```
Performing a left shift on a `uint64` by `160 bits` is incorrect because the `uint64` type only occupies `64 bits`. When a left shift of 160 bits is applied, it effectively shifts the bits out of the `64-bit` boundary, leading to data loss.

Consequently, the return value of `_packCCIPContract` will have the least-significant 160 bits correctly occupied by the contract address, but the higher bits, where the `chainSelector` should reside, will incorrectly contain zeros. This means the `chainSelector` is not encoded

## Proof Of Concept
This issue can be easily reproduced using `chisel`. Consider the following screenshot from a `chisel` execution
![poc](https://i.imgur.com/XRHunu7.png)
In this example, despite the `chainSelector` being set to `1`, the bits 160-224 are entirely occupied by zeros, indicating that the `chainSelector` was not encoded properly.

## Impact
The final packed value does not correctly represent the intended combination of `contractAddress` and `chainSelector` as the `chainSelector` is not encoded properly (set to **0**). As a result:
1. If the contract address is disabled for one chain via calling [WinnablesTicketManager::setCCIPCounterpart](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L238-L244) or [WinnablesPrizeManager::setCCIPCounterpart](https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesPrizeManager.sol#L134-L140), that contract will be disabled for all chains since `_packCCIPContract` does not encode the chain id at all:
    ```solidity
    function setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) external onlyRole(0) {
        _setCCIPCounterpart(contractAddress, chainSelector, enabled);
    }

    function _setCCIPCounterpart(
        address contractAddress,
        uint64 chainSelector,
        bool enabled
    ) internal {
        bytes32 counterpart = _packCCIPContract(contractAddress, chainSelector);
        _ccipContracts[counterpart] = enabled;
    }
    ```
2. cross-chain messages from any chain will be accepted

## Code Snippet
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/BaseCCIPContract.sol#L43
## Tool used
Manual Review

## Recommendations
Consider casting `chainSelector` to `uint256` before performing the shift operation:
```diff
function _packCCIPContract(address contractAddress, uint64 chainSelector) internal pure returns(bytes32) {
    return bytes32(
        uint256(uint160(contractAddress)) |
-       uint256(chainSelector << 160) 
+       (uint256(chainSelector) << 160)
    );
}
```