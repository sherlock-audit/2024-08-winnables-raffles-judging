Silly Burgundy Scallop

Medium

# requestConfirmations should not be constant

## Summary

`requestRandomWords.requestConfirmations` should not be hardcoded because there is a `minimumRequestConfirmations` that can be set greater than `3` and the protocol will lock forever.

## Vulnerability Detail

When a winner is drawn, a call to `VRF_COORDINATOR` is made with `3` as `requestConfirmations`. This number represent the blocks that should pass in order Chainlink to fulfill the request.

```solidity
function drawWinner(uint256 raffleId) external {
    Raffle storage raffle = _raffles[raffleId];
    _checkShouldDraw(raffleId);
    raffle.status = RaffleStatus.REQUESTED;

    uint256 requestId = VRFCoordinatorV2Interface(VRF_COORDINATOR).requestRandomWords(
        KEY_HASH,
        SUBSCRIPTION_ID,
        3, <----------------------------------------
        100_000,
        1
    );
    _chainlinkRequests[requestId] = RequestStatus({
        raffleId: raffleId,
        randomWord: 0
    });
    raffle.chainlinkRequestId = requestId;
    emit RequestSent(requestId, raffleId);
    IWinnablesTicket(TICKETS_CONTRACT).refreshMetadata(raffleId);
}
```

But inside `VRF_COORDINATOR.requestRandomWords` there is a check ensuring that the `requestConfirmations` passed is not lower than the `minimumRequestConfirmations`. Since this value is configured at the `VRF_COORDINATOR` level, when this value becomes greater than `3` (`requestConfirmations`), `drawWinner()` will revert.

[[VRFCoordinatorV2.sol#L370-L429](https://github.com/smartcontractkit/chainlink/blob/contracts-v0.6.1/contracts/src/v0.8/VRFCoordinatorV2.sol#L370-L429)](https://github.com/smartcontractkit/chainlink/blob/contracts-v0.6.1/contracts/src/v0.8/VRFCoordinatorV2.sol#L370-L429)

```solidity
function requestRandomWords(
  bytes32 keyHash,
  uint64 subId,
  uint16 requestConfirmations,
  uint32 callbackGasLimit,
  uint32 numWords
) external override nonReentrant returns (uint256) {
  // Input validation using the subscription storage.
  if (s_subscriptionConfigs[subId].owner == address(0)) {
    revert InvalidSubscription();
  }
  // Its important to ensure that the consumer is in fact who they say they
  // are, otherwise they could use someone else's subscription balance.
  // A nonce of 0 indicates consumer is not allocated to the sub.
  uint64 currentNonce = s_consumers[msg.sender][subId];
  if (currentNonce == 0) {
    revert InvalidConsumer(subId, msg.sender);
  }
  // Input validation using the config storage word.
  if (
    requestConfirmations < s_config.minimumRequestConfirmations || requestConfirmations > MAX_REQUEST_CONFIRMATIONS   <----------------------------------
  ) {
    revert InvalidRequestConfirmations(
      requestConfirmations,
      s_config.minimumRequestConfirmations,
      MAX_REQUEST_CONFIRMATIONS
    );
  }
  // No lower bound on the requested gas limit. A user could request 0
  // and they would simply be billed for the proof verification and wouldn't be
  // able to do anything with the random value.
  if (callbackGasLimit > s_config.maxGasLimit) {
    revert GasLimitTooBig(callbackGasLimit, s_config.maxGasLimit);
  }
  if (numWords > MAX_NUM_WORDS) {
    revert NumWordsTooBig(numWords, MAX_NUM_WORDS);
  }
  // Note we do not check whether the keyHash is valid to save gas.
  // The consequence for users is that they can send requests
  // for invalid keyHashes which will simply not be fulfilled.
  uint64 nonce = currentNonce + 1;
  (uint256 requestId, uint256 preSeed) = computeRequestId(keyHash, msg.sender, subId, nonce);

  s_requestCommitments[requestId] = keccak256(
    abi.encode(requestId, block.number, subId, callbackGasLimit, numWords, msg.sender)
  );
  emit RandomWordsRequested(
    keyHash,
    requestId,
    preSeed,
    subId,
    requestConfirmations,
    callbackGasLimit,
    numWords,
    msg.sender
  );
  s_consumers[msg.sender][subId] = nonce;

  return requestId;
}
```

## Impact

Using hardcoded `requestConfirmations` will cause `drawWinner()` to return when `s_config.minimumRequestConfirmations` is updated.

## Code Snippet

https://github.com/sherlock-audit/2024-08-winnables-raffles/blob/81b28633d0f450e33a8b32976e17122418f5d47e/public-contracts/contracts/WinnablesTicketManager.sol#L318

## Tool used

Manual Review

## Recommendation

Create a storage variable and an admin setter for this value.